ZVSE
*Set Santa Guards attributes here*
*It detects the monsters around Santa Gremlins and changes their attributes* 

!#SN:W^Santas_guards_health^/2;
!#SN:W^Santas_guards_attack^/2;         [Currently not used]
!#SN:W^Santas_guards_defense^/2;        [Currently not used]
!#SN:W^Santas_guards_shots^/1;        
!#SN:W^Santas_guards_casts^/2;          [Currently not used]

!#SN:W^Santas_damage_scaling^/5;        [Ice Bolt damage per unit]


!?FU(OnCombatRound)&v997=0;             [On First visible combat round]

!!SN:W^Santas_guards_health^/?y20;      [New HP of guards]
!!SN:W^Santas_guards_shots^/?y30;       [New shots of guards]

!!rei /0/41:;
    !!BMi:T?y3 P?y4;                    [Get Creature Type and Position]
    !!if&y3=173:;
        !!re t/0/5:;                    [loop around the creatures in cirlce 0-6 positions]
        !!FU(Calc_neighbouring_hex_of_Santas):Py4/t/?y3; [Pass position and return first neighbouring hex field]
        !!BU&y3>=0/y3<=186:Ey3/?y5;     [get sourrounding creature]
        !!BMy5&y5>=0/y5<=41:O?y6 H?y7 U3/?y8; [get attributes of sourrounding monsters]
        !!BMy5&y5>=0/y5<=41/y6=-1/y7<75:Hy20; [Set health to y20]
        !!BMy5&y5>=0/y5<=41/y6=-1/y7<75/y8>0:U3/y30; [Set Shots to y30]
        !!en:;
    !!en:;
 !!en:;     


!?FU(Calc_neighbouring_hex_of_Santas);  [Funktion to calculate neighbouring Hex cells]
!!UN:C6919200/4/?y1;                    [Y1 = combatManager]
!!VRx2:*2;
!!VRx1:*12+ y1+ 78952+ x2;
!!UN:Cx1/2/?x3;


*Set the damage of the Ice Bolt attack from Santas*
!?MR1;                                  [*Magical Combat Improvements*]

!!BG:A?y1 S?y2 N?y3;                    [Get Action, Spell Number and Creature Number]
!!BMy3:T?y4 N?y6;                       [Get Creature Type and Number of Creatures]
!!MR:F?y5;
!!if&y1=10/y2=-1/y4=173/y5>0/y6>0:;     [If Santa Gremlins casting Ice Bolt]
    !!SN:W^Santas_damage_scaling^/?y10;      
    !!VRy8:Sy6*y10;                     [calc new damage]
    !!MR:Fy8;                           [Set new Damage]
!!en:;
  

