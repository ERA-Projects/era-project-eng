ZVSE
*Script by Perry R 2021
Ballista do not wake up blinded units, finally :P
The script saves Stack,Power and Duration and applies it again after the Ranged Damage from Ballista happened


!?BG0;
!!SN:W^Ballista_Blind_Duration^/-1;     [Reset variable to -1]
!!SN:W^Ballista_Blind_Power^/-1;
!!SN:W^Ballista_Blind_Stack^/-1;

!?MF1;
!!UN:C42149568/4/?y10;                  [Get Physical Damage type]
!!FU&y10<>4455011:E;                    [Shooting Damage]
!!BG:A?y1;                              [Aktion in y1]
!!FU&y1<>7:E;                           [or Shoot]
!!BG:N?y1;                              [Attacking Stack]
!!BMy1:T?y40;                           [Get Type in y40]
!!FU&y40<>146:E;                        [Exit if no Ballista as Attacker]
!!MF:N?y4;                              [damage receiving stack]
!!BMy4:T?y40;                           [Get Type in y40]
!!BMy1:I?y5;                            [Side index as for heroes (0:left, 1:right)]
!!BA:Hy5/?y99;
!!FU&y99<0:E;                           [Exit if no Hero]

!!BA:Q?f;
!!BMy4:G62/?y10/?y11;                   [check for  62 Blind]
!!if&y10>0;                             [if duration is bigger zero]
  !!SN:W^Ballista_Blind_Duration^/y10;  [Save variabels]
  !!SN:W^Ballista_Blind_Power^/y11;
  !!SN:W^Ballista_Blind_Stack^/y4;
!!en;

!?FU(Event_AfterRangedDamage_Ballista);

!!SN:W^Ballista_Blind_Duration^/?y10;   [Get value]
!!SN:W^Ballista_Blind_Power^/?y11;
!!SN:W^Ballista_Blind_Stack^/?y4;
!!if&y10>0/y4>=0/y11>=0;                [if conditions met apply blind with same parameters]
  !!BMy4:M62/y10/y11;                   [Apply Blind again]
!!en;

****************************************************************************************************
!?FU(OnAfterErmInstructions);         
!!SN:L^EraPlugins\erm_hooker.era^/?y1;  
!!SN:Ay1/^SetHook^/?y2;                
!!SN:Ey2/1/4455129/(Event_AfterRangedDamage_Ballista); 

!?FU(OnAfterLoadGame);                  
!!SN:L^EraPlugins\erm_hooker.era^/?y1; 
!!SN:Ay1/^SetHook^/?y2;                
!!SN:Ey2/1/4455129/(Event_AfterRangedDamage_Ballista); 
