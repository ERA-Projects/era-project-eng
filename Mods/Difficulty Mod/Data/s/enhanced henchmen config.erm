ZVSE2
; Author:   Archer30
; Special thanks to Perry R
; Engine:   ERM 2.0+
; Requires: ERA 3.3+, Era Erm Framework

; Config DL for Enhanced Henchmen


!?FU(OnCustomDialogEvent)&i^dlg_id^=44/i^mouse_action^=(MOUSE_LMB_PRESSED); [checkboxes switch between check and cancel for Enhanced Henchmen]
!!if|i^mouse_item^=74/i^mouse_item^=82;
  !!SN:P^Button.wav^;
  !!UN:P49/?(wogOption:y);

  !!if&(wogOption)<>(TRUE);
    !!IF:M0/(MSG_TYPE_POPUP)/^%T(Diff_Mod.Hench_Option_Off)^;
  !!el&i^enhanced_henchmen_on^<>(TRUE);
    !!IF:M0/(MSG_TYPE_POPUP)/^%T(Diff_Mod.Hench_Missing_Mod)^;
  !!en;

  !!FU(diff_ConfigureEnhancedHench)&(wogOption)/i^enhanced_henchmen_on^:P;
!!en;

!?FU(diff_ConfigureEnhancedHench);      [open the Neutrals difficulty settings and set all names of options]
; Set Up the name of Dialogue
!!DL168:N^Hen_Set.txt^;

!!DL168:A13/(DLG_CMD_SET_TEXT)/^%T(Diff_Mod.Hench_Dlg_Title)^;

; Set up text on items
!!re i/0/8;
  !!SN:T^Diff_Mod.Hench_Option_%i^/?(text:z);

  !!if&i<=5;                            [5 - last index of buttons]
    !!VR(dlgItem:y):Si +50;
  !!el;
    !!VR(dlgItem):Si +24;
  !!en;

  !!DL168:A(dlgItem)/(DLG_CMD_SET_TEXT)/(text);
!!en;

; Set Up hints
; Here we use z70 to z77
!!re i/0/8;
  !!VR(zIndex:y):Si +70;
  !!SN:T^Diff_Mod.Hench_Option_%i^/?z(zIndex);
  !!DL168:H(zIndex)/(zIndex);
!!en;

; Hint for other elements
!!SN:T^Diff_Mod.confirm_button_hint_0^/?z60; !!DL168:H30722/60; [Hint Text for buttons]
!!SN:T^Diff_Mod.cancel_button_hint^/?z61; !!DL168:H30721/61; 

!!SN:T^Diff_Mod.Hench_Dlg_Hint1^/?z62; !!DL168:H82/62; [Hint Text for pictures]
!!SN:T^Diff_Mod.Hench_Dlg_Hint2^/?z63; !!DL168:H83/63; 
!!SN:T^Diff_Mod.Hench_Dlg_Hint3^/?z64; !!DL168:H84/64; 

!!FU(diff_RefreshEnhancedHenchDlg):P;
!!FU(diff_SetUpHenchDlgRandomMons):P;

; Show complete Dialogue number 168
!!DL168:S1;

!?FU(diff_RefreshEnhancedHenchDlg);
!!FU(DecorateInt):Pi^hench_battleExpStatic^/?(staticStr:z) Pi^hench_battleExpRandom^/?(randomStr:z) Pi^hench_battleHeroExpCoef^/?(coefStr:z)
  Pi^hench_bonusHeroLvUp^/?(heroLvStr:z) Pi^hench_bonusDaily^/?(dailyStr:z) Pi^hench_battleExpAIPct^/?(aiPctStr:z);

!!DL168:A70/(DLG_CMD_SET_TEXT)/^%(staticStr)^;
!!DL168:A71/(DLG_CMD_SET_TEXT)/^%(randomStr)^;
!!DL168:A72/(DLG_CMD_SET_TEXT)/^%(coefStr)%^;
!!DL168:A73/(DLG_CMD_SET_TEXT)/^%(heroLvStr)^;
!!DL168:A74/(DLG_CMD_SET_TEXT)/^%(dailyStr)^;
!!DL168:A74/(DLG_CMD_SET_TEXT)/^%(dailyStr)^;
!!DL168:A75/(DLG_CMD_SET_TEXT)/^%(aiPctStr)%^;

!!DL168:A10/(DLG_CMD_SET_DEF_FRAME)/i^hench_stackExpEnabled^;
!!DL168:A11/(DLG_CMD_SET_DEF_FRAME)/i^hench_bannerFree^;
!!DL168:A12/(DLG_CMD_SET_DEF_FRAME)/i^hench_reviveAnywhere^;

!?FU(OnCustomDialogEvent)&i^dlg_id^=168/i^mouse_action^=(MOUSE_LMB_PRESSED);  [checkboxes switch between check and cancel]
!!VR(flag:y):S(FALSE);
; Left side
!!if&i^mouse_item^>=70/i^mouse_item^<=75;
  !!VR(flag):S(TRUE);

  !!if&i^mouse_item^=70;
    !!VR(exp:y):Si^hench_battleExpStatic^;
  !!el&i^mouse_item^=71;
    !!VR(exp):Si^hench_battleExpRandom^;
  !!el&i^mouse_item^=72;
    !!VR(exp):Si^hench_battleHeroExpCoef^;
  !!el&i^mouse_item^=73;
    !!VR(exp):Si^hench_bonusHeroLvUp^;
  !!el&i^mouse_item^=74;
    !!VR(exp):Si^hench_bonusDaily^;
  !!el&i^mouse_item^=75;
    !!VR(exp):Si^hench_battleExpAIPct^;
  !!en;

  !!VR(index:y):Si^mouse_item^ -70;
  !!SN:T^Diff_Mod.Hench_Option_%(index)_Title^/?z2/^exp^/(exp);
  !!SN:T^Diff_Mod.Hench_Option_%(index)_Legend^/?z3;
  !!IF:D30/2/3/^^/^^/^^/^^/^^;
  !!IF:E1/30;
  !!VRz1:H1;

  !!if&i^mouse_item^=70;
    !!VRi^hench_battleExpStatic^&1/v1>-1:Vz1 F0/i^hench_battleExpMax^;
  !!el&i^mouse_item^=71;
    !!VRi^hench_battleExpRandom^&1/v1>-1:Vz1 F0/i^hench_battleExpMax^;
  !!el&i^mouse_item^=72;
    !!VRi^hench_battleHeroExpCoef^&1/v1>-1:Vz1 F0/100;
  !!el&i^mouse_item^=73;
    !!VRi^hench_bonusHeroLvUp^&1/v1>-1:Vz1 F0/1000;
  !!el&i^mouse_item^=74;
    !!VRi^hench_bonusDaily^&1/v1>-1:Vz1 F0/1000;
  !!el&i^mouse_item^=75;
    !!VRi^hench_battleExpAIPct^&1/v1>-1:Vz1 F0/1000;
  !!en;
; Right side
!!el&i^mouse_item^>=10/i^mouse_item^<=12;
  !!VR(flag):S(TRUE);
  !!SN:P^button.wav^;

  !!if&i^mouse_item^=10;
    !!if&i^hench_stackExpEnabled^;
      !!VRi^hench_stackExpEnabled^:S(FALSE);
      !!VRi^hench_bannerFree^:S(FALSE);   [Disable free banners if stack exp is disabled]
    !!el;
      !!VRi^hench_stackExpEnabled^:S(TRUE);
    !!en;
  !!el&i^mouse_item^=11;
    !!if&i^hench_bannerFree^;
      !!VRi^hench_bannerFree^:S(FALSE);
    !!el&i^hench_stackExpEnabled^;        [Enable free banners only when stack exp is enabled]
      !!VRi^hench_bannerFree^:S(TRUE);
    !!en;
  !!el&i^mouse_item^=12;
    !!if&i^hench_reviveAnywhere^;
      !!VRi^hench_reviveAnywhere^:S(FALSE);
    !!el;
      !!VRi^hench_reviveAnywhere^:S(TRUE);
    !!en;
  !!en;
!!en;

!!FU(diff_RefreshEnhancedHenchDlg)&(flag):P;

!?FU(OnCustomDialogEvent)&i^dlg_id^=168/i^mouse_action^=(MOUSE_RMB_PRESSED);                 [Show right click information all options]
!!if|i^mouse_item^=70/i^mouse_item^=50;
  !!IF:M0/(MSG_TYPE_POPUP)/^%T(Diff_Mod.Hench_Option_0_Info)^;
!!el|i^mouse_item^=71/i^mouse_item^=51;
  !!IF:M0/(MSG_TYPE_POPUP)/^%T(Diff_Mod.Hench_Option_1_Info)^;
!!el|i^mouse_item^=72/i^mouse_item^=52;
  !!IF:M0/(MSG_TYPE_POPUP)/^%T(Diff_Mod.Hench_Option_2_Info)^;
!!el|i^mouse_item^=73/i^mouse_item^=53;
  !!IF:M0/(MSG_TYPE_POPUP)/^%T(Diff_Mod.Hench_Option_3_Info)^;
!!el|i^mouse_item^=74/i^mouse_item^=54;
  !!IF:M0/(MSG_TYPE_POPUP)/^%T(Diff_Mod.Hench_Option_4_Info)^;
!!el|i^mouse_item^=75/i^mouse_item^=55;
  !!IF:M0/(MSG_TYPE_POPUP)/^%T(Diff_Mod.Hench_Option_5_Info)^;
!!el|i^mouse_item^=10/i^mouse_item^=30;
  !!IF:M0/(MSG_TYPE_POPUP)/^%T(Diff_Mod.Hench_Option_6_Info)^;
!!el|i^mouse_item^=11/i^mouse_item^=31;
  !!IF:M0/(MSG_TYPE_POPUP)/^%T(Diff_Mod.Hench_Option_7_Info)^;
!!el|i^mouse_item^=12/i^mouse_item^=32;
  !!IF:M0/(MSG_TYPE_POPUP)/^%T(Diff_Mod.Hench_Option_8_Info)^;
!!en;

!?FU(OnCustomDialogEvent)&i^dlg_id^=168/i^mouse_item^=30721/i^mouse_action^=(MOUSE_LMB_RELEASED);      [When clicking on the close Button in Neutral Dif Exit and reset all options]
!!DL:C1;
!!FU(hench_LoadGlobalConfig):P;           [Load Default settings from json again]

!?FU(OnCustomDialogEvent)&i^dlg_id^=168/i^mouse_item^=30722/i^mouse_action^=(MOUSE_LMB_RELEASED);      [Close Dialogue with Confirm Button Save all Settings]
!!DL:C1;

// Cosmetics
!?FU(diff_SetUpHenchDlgRandomMons);
!!VR(phoenixCadre:y):S(MON_PHOENIX) +2;
!!VR(arcticCadre:y):S(MON_ARCTIC_SHARPSHOOTER) +2;
!!VR(hellCadre:y):S(MON_HELL_BARON) +2;

; Set up Perry, Archer and Daemon
!!DL168:A82/(DLG_CMD_SET_DEF_FRAME)/(phoenixCadre);
!!DL168:A83/(DLG_CMD_SET_DEF_FRAME)/(arcticCadre);
!!DL168:A84/(DLG_CMD_SET_DEF_FRAME)/(hellCadre);

!!DL168:A80/(DLG_CMD_SET_DEF_FRAME)/0;
!!DL168:A81/(DLG_CMD_SET_DEF_FRAME)/0;
!!DL168:A85/(DLG_CMD_SET_DEF_FRAME)/0;
!!DL168:A86/(DLG_CMD_SET_DEF_FRAME)/0;

; Fill the other slots with extra monsters - if there is any
!!FU(GetMaxMonsterId):P?(lastMon:y);

!!if&(lastMon)>=200;
  !!VR(size:y):S(lastMon) -(MON_LAST_WOG);
  !!FU(NewIntArray):P(size)/?(randomNewMonsList:y);
  !!FU(Array_Fill):P(randomNewMonsList)/197/1;
  !!FU(Array_Shuffle):P(randomNewMonsList);

  !!FU(Array_Pop):P(randomNewMonsList)/?(randomMon:y);
  !!FU(diff_SetUpHenchDlgRandomMon):P(randomNewMonsList)/80 P(randomNewMonsList)/81 P(randomNewMonsList)/85 P(randomNewMonsList)/86;
!!en;

!?FU(diff_SetUpHenchDlgRandomMon);
!#VA(randomNewMonsList:x) (item:x);

!!FU(Array_Pop):P(randomNewMonsList)/?(randomMon:y);

; Set up cadre
!!VR(randomCadre:y):S(randomMon) +2;
!!DL168:A(item)/(DLG_CMD_SET_DEF_FRAME)/(randomCadre);

; Set up Hints
; Here we use z65 to z68
!!if&(item)=80;
  !!SN:H^monname^/(randomMon)/0/?z65;
  !!DL168:H80/65;
!!el&(item)=81;
  !!SN:H^monname^/(randomMon)/0/?z66;
  !!DL168:H81/66;
!!el&(item)=85;
  !!SN:H^monname^/(randomMon)/0/?z67;
  !!DL168:H85/67;
!!el&(item)=86;
  !!SN:H^monname^/(randomMon)/0/?z68;
  !!DL168:H86/68;
!!en;

; Set up monster index to be used for right-clicking
!!VRi^diff_henchDlg_mon_%(item)^:S(randomMon);

; Right-clicking event
!?FU(OnCustomDialogEvent)&i^dlg_id^=168/i^mouse_item^>=80/i^mouse_item^<=86/i^mouse_action^=(MOUSE_RMB_PRESSED);
!!if&i^mouse_item^=82;
  !!VR(mon:y):S(MON_PHOENIX);
!!el&i^mouse_item^=83;
  !!VR(mon):S(MON_ARCTIC_SHARPSHOOTER);
!!el&i^mouse_item^=84;
  !!VR(mon):S(MON_HELL_BARON);
!!el;
  !!VR(mon):Si^diff_henchDlg_mon_%i(mouse_item)^;
!!en;

!!FU(diff_ShowMonDlgRecruiting)&(mon):P(mon)/(TRUE);

!?FU(diff_ShowMonDlgRecruiting);        [by Hawaiing]
!#VA(mon:x) (isPopup:x);

!!FU&(mon)<=(NO_MON):E;

!!VR(value:y):S9990 *4 +8943204;
!!VR(value2:y):S(isPopup)* -1 +1;       [Get negative value]
!!SN:E6243760/2/(value)/(mon)/119/32/(value2);[Create the dialogue]
!!SN:E6289408/2/(value)/-1/-1;
!!UN:C7998936/4/(mon);                  [For stack Exp Panel]

!!if&(isPopup);
  !!SN:E6245264/2/(value);              [Popup]
!!el;
  !!SN:E6245280/2/(value);              [Normal]
!!en;

!!SN:E6244736/2/(value);                [Destroy the dialogue]
