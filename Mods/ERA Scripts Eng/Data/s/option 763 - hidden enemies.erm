ZVSE2
** Author orig.  : Algor
** Name          : hidden enemies
** Name rus.     : скрытые враги
** Options       : 763
** Dialogs       : -
** Variables     : -
** Tmp variables : v1-v2
** Timers        : 
** Functions     : FU7945-FU7951
** PO-values     : -


!?FU(OnEveryDay)&i^timerMonth^>1/i^timerWeekday^=1/i^timerOnce^;
!!UN:P763/?y1;
!!FU&y1=0:E;             // выход, если опция не включена

!!VRv1:S-1;                   [инициализация v1]
!!DO7945/0/7/1:P;             [перебираем игроков. v1 - последний неактивный игрок]
!!FU&v1=-1:E;                 [выход, если нет неактивного ирока]

!!VRy1:Sv1;                   [y1 - номер нового игрока]
!!VRv1:C0/0;                  [инициализация v1, v2]
!!DO7946/(HERO_FIRST)/(HERO_LAST_WOG)/1:P0;          [перебираем героев. v1 - количество ненанятых героев (x1=0)]
!!VRy2:Sv1;                   [y2 - кол-во ненанятых героев]
!!UN:U(OBJ_TOWN)/(ANY_OBJ)/?y3;              [y3 - количество городов на карте]
!!VRy3:-1;                    [y3 - максимальный номер города на карте]
!!UN:J2/?y4;                  [y4 - уровень сложности (0..4)]
!!VRy4:+1 *5;                 [y4 - шанс спавна для городов человека (5%/10%/15%/20%/25%)]
!!VRy5:S10;                   [y4 - шанс спавна для городов ИИ (10%)]
!!DO7947/0/y3/1:Py1/y2/y4/y5; [спавны у городов: х1 - неактивный игрок, x2 - кол-во ненанятых героев, x3 - шанс спавна для человека, x4 шанс спавна для ИИ]

!?FU7945;                     [проверка статуса игрока x16 и возврат его номера в v1, если игрок "мертв"]
!!OW:Ix16/d/?y1;              [y1 - статус игрока x16 (жив 0 / мертв 1)]
!!VRv1&y1=1:Sx16;             [v1 - номер неактивного игрока]

!?FU7946;                     [подсчет ненанятых героев в v1 если x1=0 или возврат в v1 номера x1-го ненанятого героя]
!!HEx16:O?y1 P?y2/?y3/?y4;    [y1 - хозяин героя, y2-y4 =-1, если герой не на карте (не в тюрьме, например)]
!!FU|y1>=0/y2>-1:E;           [выход, если герой имеет хозяина или на карте]

!!re i/(PLAYER_FIRST)/(PLAYER_LAST);                    [перебор всех игроков]
  !!OW:Vi/?y11/?y12;          [y11/y12 - герои в таверне игрока]
  !!FU|x16=y11/x16=y12:E;     [выход, если проверяемый герой в таверне одного из игроков]
!!en; 

!!VRv1:+1;                    [инкремент счетчика ненанятых героев]

!!if&x1>0/v1=x1;              [если искомый герой найден]
  !!VRv1:Sx16;                [v1 - номер искомого героя]
  !!VRx16:S200;               [прерывание цикла]
!!en; 

!?FU7947;                     [х1 - неактивный игрок, x2 - кол-во ненанятых героев, x3 - шанс спавна для человека, x4 шанс спавна для ИИ, x16 - номер города на карте]
!!FU&v2>=8:E;                 [выход, если уже спавнилось 8 новых героев]

!!CA0/x16:O?y1 B3/7;          [y1 - владелец города, флаг 1 - отстроен ли форт]
!!VRy2&1:S1;                  [y2=0/1 - деревня/город]
!!OW&y1>=0:Iy1/?y3;           [y3=0/1 - город человека/ИИ]
!!VRx4&y3=0:Sx3;              [x4 - шанс спавна]
!!VRx4&y2=0:*2;               [x4 - для деревень шанс спавна удвоен]
!!VRy3:S0 R99;                [y3 - случайное число от 0 до 99]
!!FU&x4<=y3:E;                [выход, еcли спавн не выпал]

*** поиск места для спавна перед городом (площадка 5х3)
!!CA0/x16:P?y1/?y2/?y3;       [y1-y3 - координаты города]
!!UN:X?y4/?y11;               [y4 - размер карты]
!!VRy11:Sy1 -2;               [первая абсцисса "площадки"]
!!VRy11&y11<0:S0;             [...]
!!VRy12:Sy1 +2;               [вторая абсцисса "площадки"]
!!VRy12&y12>=y4:Sy4 -1;       [...]
!!VRy13:Sy2 +1;               [первая ордината "площадки"]
!!FU&y13>=y4:E;               [выход, если город расположен некорректно (у нижней кромки карты без возможности выхода)]

!!VRy14:Sy2 +3;               [вторая ордината "площадки"]
!!VRy14&y14>=y4:Sy4 -1;       [...]
!!VRv1:S-1;                   [инициализация v1]
!!VRy5:Sy12 -y11 +1;          [x5 - горизонтальный размер площадки]
!!VRy4:Sy14 -y13 +1 *y5 -1;   [y4 - количество квадратов для поиска]
!!DO7948/0/y4/1:Py11/y13/y3;  [поиск свободного места перед городом для спавна. v1 - номер свободной позиции]
!!FU&v1=-1:E;                 [выход, если нет позиции для размещения героя]

!!VRy1:Sv1 %5 +y11;           [y1/y2/y3 - координаты спавна]
!!VRy2:Sv1 :5 +y13;           [...]
!!VRv1:S-1;                   [инициализация v1]
!!VRy4:Sx2 -v2 -1;            [y4 - количество ненанятых героев -1]
!!VRy5:S1 Ry4;                [y5 - случайный номер свободноо героя для спавна]
!!DO7946/0/155/1:Py5;         [перебираем героев. v1 - номер героя для спавна]
!!FU&v1=-1:E;                 [выход, если герой не найден]

!!VRy4:Sv1;                   [y4 - номер героя]
*** спавн героя y4 в координатах y1/y2/y3
!!OW:Ix1/d/?y5;               [y5=1, если игрок еще не активирован]

!!if&y5=1;
  !!OW:Ix1/1/0 Dx1/128; Tx1/6; [активируем нового ИИ-игрока в 6м союзе,без ограничений на жизнь без городов]
  !!FU(ES_763_GetHiddenEnemiesAlliance):Px1/?y30;
  !!OW:Tx1/y30;
  !!OW:Rx1/0/30 Rx1/2/30 Rx1/1/10 Rx1/3/10 Rx1/4/10 Rx1/5/10 Rx1/6/30000; [даем новому игроку ресурсы на развитие(30/30/10/10/10/10/30000)]
!!en;

!!HEy4:Py1/y2/y3 Ox1;         [помещаем героя на карту и меняем владельца]
!!OW:Ax1/y4;                  [активируем героя]
!!VRv2:+1;                    [инкремент счетчика размещенных героев]
!!VRy5:Sc *250;               [y5 - бонус опыта герою 250*день недели]
!!VRv1:S-1;                   [инициализация v1]
!!DO7951/10/69/1:P0;          [подсчет количества доступных заклинаний в v1]
!!VRy6:S1 Rv1;                [случайный номер доступного заклинания]
!!VRy7:S1 Rv1;                [случайный номер доступного заклинания]
!!VRy8:S1 Rv1;                [случайный номер доступного заклинания]
!!VRy9:S1 Rv1;                [случайный номер доступного заклинания]
!!VRv1:S0;                    [инициализация v1]
!!DO7951/10/69/1:Py6;         [выбор 1-го заклинания в v1]
!!VRy6:Sv1;                   [y6 - 1-е заклинание]
!!VRv1:S0;                    [инициализация v1]
!!DO7951/10/69/1:Py7;         [выбор 2-го заклинания в v1]
!!VRy7:Sv1;                   [y6 - 2-е заклинание]
!!VRv1:S0;                    [инициализация v1]
!!DO7951/10/69/1:Py8;         [выбор 3-го заклинания в v1]
!!VRy8:Sv1;                   [y6 - 3-е заклинание]
!!VRv1:S0;                    [инициализация v1]
!!DO7951/10/69/1:Py9;         [выбор 4-го заклинания в v1]
!!VRy9:Sv1;                   [y6 - 4-е заклинание]
!!VRv1:S-1;                   [инициализация v1]
!!DO7950/7/122/1:P0;          [подсчет количества доступных артефактов в v1]
!!VRy10:S1 Rv1;               [случайный номер доступного артефакта]
!!VRv1:S0;                    [инициализация v1]
!!DO7950/7/122/1:Py10;        [выбор артефакта в v1]
!!VRy10:Sv1;                  [y10 - выбранный артефакт]
!!HEy4:Edy5 A1/0/17 My6/1 My7/1 My8/1 My9/1 A4/y10; [даем герою бонус опыта, книгу магии, 4 случайных боевых заклинания и случайный артефакт]
!!HEy4:Fd/d/d/?y5 S24/?y6;    [y5/y6 - Знание/Интеллект героя]
!!VRy6:*25 +100;              [y6 - повышение маны от Интеллекта]
!!VRy6&y6=175:S200;           [...]
!!VRy5:*y6 :10;               [y5 - запас маны героя]
!!HEy4:Iy5 B2/?y1;            [даем запас маны герою, y1 - класс героя]
!!IF:Wy4;                     [для совместимости с "новой системой восполнения маны" ...]
!!VRw99:Sy5;                  [сохранение текущей маны героя в w99]
!!VRy1::2;                    [y1 - фракция героя]
!!VRy2:Sc +1 :7 +1;           [y2 - номер недели]
!!DO7949/0/6/1:Py4/y1/y2;     [заполнение армии героя x1. x2 - фракция, x3 - неделя]

!?FU7948;                     [поиск позиции для размещения героя перед городом]
!!VRy1:Sx16 %5 +x1;           [абсцисса]
!!VRy2:Sx16 :5 +x2;           [ордината]
!!TRy1/y2/x3:P?y3 T?y4/d/d/d/d/d/d/d; [y3=0, если квадрат не проходим, y4 - тип почвы]
!!OBy1/y2/x3:T?y5;            [y5 - тип объекта]
!!VRv1&y3=1/y4<8/y5=0:Sx16;   [возврат номера свободной позиции в v1]

!?FU7949;                     [заполнение слота x16 армии героя x1. x2 - фракция, x3 - неделя]
!!FU&x16>x3:E;                [2я неделя - 3 слота, 3я неделя - 4 слота и т.д. до 7 слотов в армии героя]

!!VRy1:S0 R1;                 [y1 = 0/1 - downgrade/upgrade вариант существа]
!!UN:Tx2/x16/y1/?y2;          [y2 - тип существа для слота]
!!MA:Gy2/?y3;                 [y3 - базовый прирост существа]
!!VRy3:*x3;                   [y3 - макс. кол-во существ в слоте (баз. прирост * номер недели)]
!!VRy4:S0 Ry3;                [y4 - количество существ в слоте]
!!HEx1:C0/x16/y2/y4;          [установка количества существ в слоте]

!?FU7950;                     [подсчет в v1 (х1=0) или выбор x1-го артефакта (x1>0) классов "treasure/minor", исключая запрещенные на карте]
!!UN:Ax16/?y1 Ax16/3/?y2 Ax16/4/?y3; [y1/y2/y3 - запрет/класс/номер сборника артефакта]
!!FU|y1=1/y2<2/y2>4/y3>-1:E;  [выход, если артефакт неподходящий: запрещен на карте, не подходящего класса или сборный]

!!VRv1:+1;                    [инкремент v1]

!!if&x1>0/v1=x1;              [если найден искомый артефакт для выбора]
  !!VRv1:Sx16;                [сохраняем номер выбранного артефакта в v1]
  !!VRx16:S1000;              [прерываем цикл]
!!en; 

!?FU7951;                     [подсчет в v1 (х1=0) или выбор x1-го боевого заклинания (x1>0) 1-3 уровня, исключая запрещенные на карте]
!!UN:J0/x16/?y1;              [y1 - запрет заклинания]
!!SSx16:L?y2;                 [y2 - уровень заклинания]
!!FU|y1=1/y2>3:E;             [выход, если заклинание запрещено или выше 3го уровня]

!!VRv1:+1;                    [инкремент v1]

!!if&x1>0/v1=x1;              [если найдено искомое заклинание для выбора]
  !!VRv1:Sx16;                [сохраняем номер выбранного заклинания в v1]
  !!VRx16:S1000;              [прерываем цикл]
!!en;

!?FU(ES_763_GetHiddenEnemiesAlliance);
!#VA(excludePlayer:x) (result:x);

; If the alliance has been set, set it
!!if&i^es_763_hiddenAlliancePlusOne^;
  !!VR(result):Si^es_763_hiddenAlliancePlusOne^ -1;

  !!FU:E;
!!en;

; If the alliance hasn't been set yet, get a new value
!!FU(NewIntArray):P?(occupiedAlliancesList:y);

!!re i/(PLAYER_FIRST)/(PLAYER_LAST);
  !!co&i=(excludePlayer);

  ; Get all the occupied team ID
  !!OW:Ii/?(isAi:y)/?(hasLost:y);

  !!if&(hasLost)<>(TRUE);
    !!OW:Ti/?(team:y);
    !!FU(Array_Push):P(occupiedAlliancesList)/(team);
  !!en;
!!en;

!!FU(Array_SortedUnique):P(occupiedAlliancesList);

; Get all the unoccupied team ID
; If all the team has been occupied, set the hidden enemy as no alliance
!!SN:M(occupiedAlliancesList)/?(size:y);

; failed safe: set the team as 8 - usually won't be occupied and is available
; This should not happen though
!!if&(size)>=7;
  !!VR(result):S8;

  !!FU:E;
!!en;

; Get an unoccupied team
!!re i/7/0/-1;                          [reverse order]
  !!re j/0/(size)/1/-1;
    !!SN:M(occupiedAlliancesList)/j/?(team:y);

    !!br&(team)=i;
  !!en;

  !!if&j<(size);
    !!co;
  !!el;
    !!br;
  !!en;
!!en;

!!VR(result):Si;
!!VRi^es_763_hiddenAlliancePlusOne^:S(result) +1;

** end
