ZVSE

** ver 1.0

!?FU(Fun_SetDescriptionFromERT);        // установка описаний из ERT-переменных
; позволяет экономить z-переменные, устанавливая в качестве описаний навыков, монстров и артефактов
; значения z-переменных из ers-файлов. Описания артефактов нужно обновлять также и после загрузки старой игры
; Вернуть стандартные описания можно штатным функционалом ресивера UN:G.
; Параметры функции:
; x1=0 - тексты вторичных навыков
;   x2 - номер навыка [0...27]
;   x3 - название (0) или уровень навыка (1..3)
; x1=1 тексты существ
;   x2 - номер существа (0...196)
;   x3 - название ед.ч. (0), название мн.ч. (1), спец.текст/описание (2)
; x1=2 - специализация героя
;   x2 - номер героя (0...155)
;   x3 - название (0), короткое описание (1) или полное описание (2)
; x1=3 - артефакт
;   x2 - номер артефакта (0...170)
;   x3 - название (0), описание (1)
; x4 - номер z переменной из ert файла
; Пример:
; ?FU(OnAfterErmInstructions);  !FU(Fun_SetDescriptionFromERT):P3/1/21/234567; // артефакт (3) Шлем Хаоса (21) имеет описание (1) указанное в ert-переменной z234567
!!SN:E7824928/1/x4; !!VRy1:Sv1;         // 
!!if&x1=0/x2>=0/x2<=27/x3>=0/x3<=3:;    // вторичный навык
  !!VRz1:Szx4;                          // 
  !!SN:H^secskill^/x2/x3/^%Z1^;         // 
!!en:;                                  // 
!!if&x1=1/x2>=0/x2<=196/x3>=0/x3<=2:;   // существо
  !!VRz1:Szx4;                          // 
  !!SN:H^monname^/x2/x3/^%Z1^;          // 
!!en:;                                  // 
!!if&x1=2/x2>=0/x2<=155/x3>=0/x3<=2:;   // специализация
  !!VRz1:Szx4;                          // 
  !!SN:H^spec^/x2/x3/^%Z1^;             // 
!!en:;                                  // 
!!if&x1=3/x2>=0/x2<=170/x3>=0/x3<=1:;   // артефакт
  !!VRy4&x3<>0:S16;                     // 
  !!UN:C6687592/4/?y2;                  // 
  !!VRy3:Sx2 *32 +y2 +y4;               // 
  !!UN:Cy3/4/y1;                        // 
!!en:;                                  // 

** end