ZVSE2
** Author        : Algor [Idea: Ivor]
** Updated by    : Archer30
** Name          : Field repairs
** Name rus.     : Ремонт в полевых условиях
** Options       : 744

** Герой, имеющий навыки управления боевыми машинами после сразу после боя может починить разрушенную машину.
** Базовая стоимость починки (с одним уровнем навыка) равна удвоенной стоимости машины, базовое время починки - 400 очков движения.
** Каждый уровень каждого навыка управления боевыми машинами, а также специализация героя на таком навыке или боевой машине снижают стоимость и время ремонта на 10%.
** ИИ всегда чинит машины, если это возможно.


!?FU(OnAfterErmInited);
!!UN:P744/?(wogOption:y);               [проверяем включена ли опция 744]
!!FU&(wogOption)<>(TRUE):E;             [выход если опция не включена]

; Set Up new war machine skills descriptions
; Ballista
!!SN:H^monname^/(MON_BALLISTA)/0/?(name:z);
!!SN:T^es.744.desc^/?(newDesc:z)/^mon^/(name);

!!re i/(SKILL_BASIC)/(SKILL_EXPERT);
  !!SN:H^secskill^/(SKILL_ARTILLERY)/i/?(desc:z);
  !!SN:H^secskill^/(SKILL_ARTILLERY)/i/^%(desc)%(newDesc)^;
!!en;

; First Aid Tent
!!SN:H^monname^/(MON_FIRST_AID_TENT)/0/?(name);
!!SN:T^es.744.desc^/?(newDesc)/^mon^/(name);

!!re i/(SKILL_BASIC)/(SKILL_EXPERT);
  !!SN:H^secskill^/(SKILL_FIRST_AID)/i/?(desc:z);
  !!SN:H^secskill^/(SKILL_FIRST_AID)/i/^%(desc)%(newDesc)^;
!!en;

; Ammo Cart
!!SN:H^monname^/(MON_AMMO_CART)/0/?(name);
!!SN:T^es.744.desc^/?(newDesc)/^mon^/(name);

!!re i/(SKILL_BASIC)/(SKILL_EXPERT);
  !!SN:H^secskill^/(SKILL_BALLISTICS)/i/?(desc:z);
  !!SN:H^secskill^/(SKILL_BALLISTICS)/i/^%(desc)%(newDesc)^;
!!en;

!?FU(OnBeforeBattleUniversal);[перед боем]
!!UN:P744/?y1;                [проверяем включена ли опция 744 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]

!!BA:H0/?y1 H1/?y2;           [y1/y2 - герои участвующие в битве]

!!if&y1>=0;
  !!HEy1:A2/3/d/?y3 A2/4/d/?y4 A2/5/d/?y5 A2/6/d/?y6; [y3-y6 - боевые машины героя]
  !!SN:W^opt744.H%Y1.3^/y3 W^opt744.H%Y1.4^/y4 W^opt744.H%Y1.5^/y5 W^opt744.H%Y1.6^/y6; [сохраняем кол-во машин перед боем для атакующего героя]
!!en;

!!if&y2>=0;
  !!HEy2:A2/3/d/?y3 A2/4/d/?y4 A2/5/d/?y5 A2/6/d/?y6; [y3-y6 - боевые машины героя]
  !!SN:W^opt744.H%Y2.3^/y3 W^opt744.H%Y2.4^/y4 W^opt744.H%Y2.5^/y5 W^opt744.H%Y2.6^/y6; [сохраняем кол-во машин перед боем для защищающегося героя]
!!en;

!?FU(OnAfterBattleUniversal); [после боя]
!!UN:P744/?y1;                [проверяем включена ли опция 744 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]

!!re i/(BATTLE_LEFT)/i^battle_hero_vs_hero^;
  !!HEi^battle_hero_%i^:O?y30;

  !!if&y30>(NO_OWNER);
    !!VRy1:Si^battle_hero_%i^;

    !!br;
  !!en;
!!en;

!!SN:W^opt744.H%Y1.3^/?y3 W^opt744.H%Y1.4^/?y4 W^opt744.H%Y1.5^/?y5 W^opt744.H%Y1.6^/?y6; [кол-во машин перед боем]
!!HEy1:A2/3/d/?y13 A2/4/d/?y14 A2/5/d/?y15 A2/6/d/?y16; [y13-y16 - боевые машины героя после боя]
!!HEy1:S(SKILL_BALLISTICS)/?y7 S(SKILL_ARTILLERY)/?y8 S(SKILL_FIRST_AID)/?y9 X?y10/?y11/d/d/d/d/d; [y7-y9 "машинные навыки героя", y10/y11 - специальность героя]
!!VRy12:Sy7 +y8 +y9;          [y12 - сумма уровней "машинных" навыков и "машинной" специализации героя]
!!VRy12&y10=0/y11=10:+1;      [...]
!!VRy12&y10=0/y11=20:+1;      [...]
!!VRy12&y10=0/y11=27:+1;      [...]
!!VRy12&y10=1/y11=145:+1;     [...]
!!VRy12&y10=1/y11=146:+1;     [...]
!!VRy12&y10=1/y11=147:+1;     [...]
!!VRy12&y10=1/y11=148:+1;     [...]
!!VRy12&y10=4/y11=145:+1;     [...]
!!VRy12&y10=4/y11=146:+1;     [...]
!!VRy12&y10=4/y11=147:+1;     [...]
!!VRy12&y10=4/y11=148:+1;     [...]
!!VRy12&y12>10:S10;           [...]
!!FU&y12<1:E;                 [выход, если у героя нет "машинных" навыков/специализации]

!!VRy12:*-20 +200 +20;        [y12 - стоимость починки в % (200%..20%)]
!!HEy1:O?y17;                 [y17 - хозяин героя]
!!FU&y17<0:E;                 [выход, если у героя нет хозяина]

!!OW:Iy17/?y18/?y19;          [y18=1 для ИИ героя]
!!FU&y19=1:E;                 [выход, если игрок мертв]

!!VRy21:S200 *y12 :100;       [y21 - время ремонта машины 40..400 очков движения]
!!HEy1:B0/?z2;
!!IF:Wy1;                     [доступ к w-переменным героя]

** Катапульта
; In most cases this is not used since Catapults would be repaired automatically
!!if&y13<y3/y13<1;            [если есть разрушенные Катапульты и нет ни одной целой]
  !!MA:C(MON_CATAPULT)/(RES_GOLD)/?y20;           [y20 - цена машины]
  !!VRy20:*y12 :100 *-1;      [y20 - цена ремонта с учетом навыков героя (отрицательное значение)]
  !!OW:Ry17/6/?y19;
  !!VRy19:*-1;                [y19 - золото героя, отрицательное значение]

  !!if&y20>=y19;              [если золота достаточно]
    !!SN:T^es.744.dlg^/?z1/^hero^/z2; [z1 - текст вопроса о починке]     
    !!VRy22:Sy20 -100000;     [y22 - цена для показа в диалоге]
    !!IF&y18=0:Q1/(PIC_TYPE_MONSTER)/(MON_CATAPULT)/(PIC_TYPE_RES_GOLD)/y22/(MSG_TYPE_QUESTION)/1; [вопрос о починке машины для игрока человека]

    !!if|y18=1/1;             [если ответ положительный или ИИ (ИИ всегда восстанавливает машины)]
      !!HEy1:A1/3/16 W?y23/1; [даем герою Катапульту, y23 - текущие ОД]
      !!VRy23:-y21;           [снижаем запас ОД на время ремонта]
      !!VRy23&y23<0:S0;       [...]
      !!HEy1:Wy23/1;          [...]
      !!OW:Ry17/6/dy20;       [Уменьшаем количество золота игрока]

      !!if&y18=0;
        !!SN:D;
        !!UN:R2;
      !!en;
    !!en; 
  !!en; 
!!en; 

** Баллиста
!!if&y14<y4/y14<1;            [если есть разрушенные Баллисты и нет ни одной целой]
  !!MA:C(MON_BALLISTA)/(RES_GOLD)/?y20;           [y20 - цена машины]
  !!VRy20:*y12 :100 *-1;      [y20 - цена ремонта с учетом навыков героя (отрицательное значение)]
  !!OW:Ry17/6/?y19;
  !!VRy19:*-1;                [y19 - золото героя, отрицательное значение]

  !!if&y20>=y19;              [если золота достаточно]
    !!SN:T^es.744.dlg^/?z1/^hero^/z2; [z1 - текст вопроса о починке]   
    !!VRy22:Sy20 -100000;     [y22 - цена для показа в диалоге]
    !!IF&y18=0:Q1/(PIC_TYPE_MONSTER)/(MON_BALLISTA)/(PIC_TYPE_RES_GOLD)/y22/(MSG_TYPE_QUESTION)/1; [вопрос о починке машины для игрока человека]

    !!if|y18=1/1;             [если ответ положительный или ИИ (ИИ всегда восстанавливает машины)]
      !!HEy1:A1/4/13 W?y23/1; [даем герою Баллисту, y23 - текущие ОД]
      !!VRy23:-y21;           [снижаем запас ОД на время ремонта]
      !!VRy23&y23<0:S0;       [...]
      !!HEy1:Wy23/1;          [...]
      !!OW:Ry17/6/dy20;       [Уменьшаем количество золота игрока]

      !!if&y18=0;
        !!SN:D;
        !!UN:R2;
      !!en;
    !!en; 
  !!en; 
!!en; 

** Подвода
!!if&y15<y5/y15<1;            [если есть разрушенные Подводы и нет ни одной целой]
  !!MA:C(MON_AMMO_CART)/(RES_GOLD)/?y20;           [y20 - цена машины]
  !!VRy20:*y12 :100 *-1;      [y20 - цена ремонта с учетом навыков героя (отрицательное значение)]
  !!OW:Ry17/6/?y19;
  !!VRy19:*-1;                [y19 - золото героя, отрицательное значение]

  !!if&y20>=y19;              [если золота достаточно]
    !!SN:T^es.744.dlg^/?z1/^hero^/z2; [z1 - текст вопроса о починке]   
    !!VRy22:Sy20 -100000;     [y22 - цена для показа в диалоге]
    !!IF&y18=0:Q1/(PIC_TYPE_MONSTER)/(MON_AMMO_CART)/(PIC_TYPE_RES_GOLD)/y22/(MSG_TYPE_QUESTION)/1; [вопрос о починке машины для игрока человека]

    !!if|y18=1/1;             [если ответ положительный или ИИ (ИИ всегда восстанавливает машины)]
      !!HEy1:A1/5/14 W?y23/1; [даем герою Подводу, y23 - текущие ОД]
      !!VRy23:-y21;           [снижаем запас ОД на время ремонта]
      !!VRy23&y23<0:S0;       [...]
      !!HEy1:Wy23/1;          [...]
      !!OW:Ry17/6/dy20;       [Уменьшаем количество золота игрока]

      !!if&y18=0;
        !!SN:D;
        !!UN:R2;
      !!en;
    !!en; 
  !!en; 
!!en; 

** Палатка
!!if&y16<y6/y16<1;            [если есть разрушенные Палатки и нет ни одной целой]
  !!MA:C(MON_FIRST_AID_TENT)/(RES_GOLD)/?y20;           [y20 - цена машины]
  !!VRy20:*y12 :100 *-1;      [y20 - цена ремонта с учетом навыков героя (отрицательное значение)]
  !!OW:Ry17/6/?y19;
  !!VRy19:*-1;                [y19 - золото героя, отрицательное значение]

  !!if&y20>=y19;              [если золота достаточно]
    !!SN:T^es.744.dlg^/?z1/^hero^/z2; [z1 - текст вопроса о починке]   
    !!VRy22:Sy20 -100000;     [y22 - цена для показа в диалоге]
    !!IF&y18=0:Q1/(PIC_TYPE_MONSTER)/(MON_FIRST_AID_TENT)/(PIC_TYPE_RES_GOLD)/y22/(MSG_TYPE_QUESTION)/1; [вопрос о починке машины для игрока человека]

    !!if|y18=1/1;             [если ответ положительный или ИИ (ИИ всегда восстанавливает машины)]
      !!HEy1:A1/6/15 W?y23/1; [даем герою Катапульту, y23 - текущие ОД]
      !!VRy23:-y21;           [снижаем запас ОД на время ремонта]
      !!VRy23&y23<0:S0;       [...]
      !!HEy1:Wy23/1;          [...]
      !!OW:Ry17/6/dy20;       [Уменьшаем количество золота игрока]

      !!if&y18=0;
        !!SN:D;
        !!UN:R2;
      !!en;
    !!en; 
  !!en; 
!!en; 

!!FU(WOG_73_Func_CorrectHeroVariablesW)&i^WOG_73_enabled^:Py1; совместимость с боевыми машинами III

**end
