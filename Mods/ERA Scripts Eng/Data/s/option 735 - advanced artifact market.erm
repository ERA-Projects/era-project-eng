ZVSE2
** Author orig.  : igrik, Algor
** Name          : Advanced artifact market
** Name rus.     : Улучшенный рынок артефактов
** Options       : 735
** Dialogs       : -
** Variables     : -
** Tmp variables : -
** Timers        : -
** Functions     : FU7893
** PO-values     : -

** Торговцы артефактами в городах и на карте ежедневно вместо выкупленных товаров выкладывают на продажу новые, иногда более редкие артефакты.
; 80% treasue level, 20% minor level
; This script refills the shop, doesn't replace the existing ones


!?FU(OnEveryDay)&i^timerOnce^/i^timerWeekday^=1;
!!UN:P735/?(wogOption:y);               [проверяем включена ли опция 735 в y1]
!!FU&(wogOption)<>(TRUE):E;             [выход если опция не включена]

!!UN:C(GAME_MANAGER)/(UNC_INT)/?(gameMgr:y);

!!re i/128612/128636/(UNC_INT);
  !!UN:C(gameMgr)/i/(UNC_INT)/?(art:y);

  !!if&(art)<=(NO_ART);                 [если артефакт выкуплен]
    !!FU(ES_735_GetRandomArt):P(gameMgr)/?(randArt:y);
    !!UN:C(gameMgr)/i/(UNC_INT)/(randArt); [установка артефакта в слот рынка]
  !!en;
!!en;

** обновление артефактов у торговцев на карте
!!UN:C(gameMgr)/128644/(UNC_INT)/?(startPtr:y); [_ptr_ первого черного рынка на карте]
!!UN:C(gameMgr)/128648/(UNC_INT)/?(endPtr:y); [_ptr_ последнего черного рынка на карте]
!!VR(endPtr):-1;                        ["последнего черного рынка" нет]

!!DO7893/(startPtr)/(endPtr)/28&(endPtr)>(startPtr):P(gameMgr);            [циклическая функция прохода по торговцам]

!?FU7893;
!#VA(gameMgr:x);

; x16 - адрес текущего в цикле черного рынка на карте
!!re i/0/24/4;
  !!UN:Cx16/i/4/?(art:y);

  !!if&(art)<=(NO_ART);                 [если артефакт выкуплен]
    !!FU(ES_735_GetRandomArt):P(gameMgr)/?(randArt:y);
    !!UN:Cx16/i/(UNC_INT)/(randArt);    [установка артефакта в слот рынка]
  !!en;
!!en;

!?FU(ES_735_GetRandomArt);
!#VA(gameMgr:x) (art:x);

!!VR(level:y):R0/0/4 :4 *2 +2;          [качество нового артефакта (2, в 20% случаев - 4)]

!!re j;
  !!SN:E5018000/2/(gameMgr)/(level);    [генерация нового артефакта требуемого качества]
  !!UN:Av1/1/?(price:y);

  !!if&(price)>0;
    !!VR(art):Sv1;

    !!br;
  !!en;
!!en;

** end
