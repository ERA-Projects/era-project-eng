ZVSE2
** Author orig.  : Algor
** Name          : Team AI
** Name rus.     : Командный ИИ
** Options       : 734
** Dialogs       : -
** Variables     : -
** Tmp variables : -
** Timers        : TM2 (standard)
** Functions     : FU7897
** PO-values     : -

** В конце каждого хода каждый ИИ игрок передает все свои ресурсы следующему ИИ-игроку своей команды, если таковой имеется
** (наличие у ИИ-игрока рынка не проверяется, т.к. рынок доступен в подавляющем большинстве случаев)


!#SN:W^opt734pp^/-1;                 [инициализация предыдущего игрока]

!?FU(OnEveryDay);
!!UN:P734/?y1;                       [проверяем включена ли опция 734 в y1]
!!FU&y1=0:E;                         [Выход, если опция не включена]

!!OW:C?y1;                           [y1 - текущий игрок]
!!SN:W^opt734pp^/?y2 W^opt734pp^/y1; [y2 - предыдущий игрок, обновление предыдущего игрока текущим]
!!OW:Iy2/?y3/?y4 Ty2/?y5;            [y3/y4/y5 - ИИ статус(1=ИИ)/активность(0=Жив)/Команда (-1=Нет)
!!FU|y2<0/y3=0/y4=1/y5<0:E;          [выход, если предыдущего игрока нет, он - человек, мертв или не в команде]

!!VRy6:Sy2 +1;
!!VRy7:Sy2 +7;                       [y6-y7 - диапазон поиска игроков]
!!DO7897/y6/y7/1:Py2/y5/?y8;         [поиск следующего командного ИИ-игрока]
!!FU&y8<0:E;                         [выход, если следующий командный ИИ-игрок не найден]

!!OW:Ry2/0/?y10 Ry2/1/?y11 Ry2/2/?y12 Ry2/3/?y13 Ry2/4/?y14 Ry2/5/?y15 Ry2/6/?y16 Ry2/7/?y17; [получение ресурсов предыдущего игрока]
!!OW:Ry2/0/0 Ry2/1/0 Ry2/2/0 Ry2/3/0 Ry2/4/0 Ry2/5/0 Ry2/6/0 Ry2/7/0;                         [обнуление ресурсов предыдущего игрока]
!!OW:Ry8/0/dy10 Ry8/1/dy11 Ry8/2/dy12 Ry8/3/dy13 Ry8/4/dy14 Ry8/5/dy15 Ry8/6/dy16 Ry8/7/dy17; [добавление ресурсов следующему игроку в команде]
!_!IF:M^Debug: игрок %Y2 передал игроку %Y8 ресурсы: %Y11/%Y12/%Y13/%Y14/%Y15/%Y16/%Y17^;

!?FU7897;                            [возврат в x3 следующего игрока в команде x2 игрока x1]
!!VRy1:Sx16;
!!VRy1&y1>7:-8;                      [y1 - проверяемый игрок]
!!VRx3:S-1;                          [...]
!!OW:Ty1/?y2 Iy1/?y3/?y4;            [y2/y3/y4 - команда/ИИ статус/активность игрока]
!!FU|y2<>x2/y3=0/y4=1:E;             [выход, если игрок не в нужной команде, человек или мертв]

!!VRx3:Sy1;
!!VRx16:S100500;                     [возвращаем в x3 найденного игрока и прерываем цикл]

** end
