ZVSE2
** Author orig.  : Fnord
** Rewritten by  : Archer30
** Name          : Monsters drop artefacts
** Name rus.     : Монстры с артефактами
** Changes rus.  : [Algor] вынос опции в отдельный файл для мода ERA, построчные комментарии
** Options       : 769
** PO-values     : V0 (neutral stacks)

** Approximately 25% of Monster Stacks will have artifacts unless the stack is
** set to always join or already has an artifact. The artifact class depends on
** the initial gold value of the stack as follows:
**
**           under 1000 = none
**            1000-2999 = Treasure
**           3000-11999 = Minor
**          12000-39999 = Major
**          40000+      = Relic
**
** Only enabled artifacts will be given (meaning no assembled combos unless
** specifically enabled) and WoG Artifacts may be given if they're not banned.
** Boots of Speed, Equestrian Gloves, Boosts of Levitation and Angel Wings
** will never be given, even if not banned.


** [Before show map]
!?FU(ES_OnIterateAllMapObjects);
!#VA(x:x) (y:x) (z:x);
!#VA(objType:x);
!#VA(objSubtype:x);
!#VA(isYellowSquare:x);                 [boolean]
!#VA(isPassable:x);                     [boolean]

; exit if option isn't enabled
!!UN:P769/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; check objects
!!FU&(objType)<>(OBJ_MONSTER):E;

; Set up monster artifacts
!!VR(random:y):R0/1/100;                [Random number: 1-100]
!!FU&(random)>25:E;                     [Only a 25% chance of monster having an artifact]

!!MO(x)/(y)/(z):R?(aggression:y)/1;     [Check monster's aggression]
!!FU&(aggression)=0:E;                  [Skip if monster is set to always join]

; Check if the monster has already got an artifact
!!OB(x)/(y)/(z):C?(controlWords:y);
!!VR(hasSetup:y):S(controlWords) &(BIT_31); [Bit 31 for checking additional settings]

!!if&(hasSetup);
  !!MO(x)/(y)/(z):A?(art:y);
  !!FU&(art)>(NO_ART):E;                [skip if monster already has an artifact]
!!en;

!!MO(x)/(y)/(z):G?(num:y);
; Determine Class from 1 to 5 based on stack's gold value
!!MA:C(objSubtype)/(RES_GOLD)/?(cost:y);[Gold cost of 1 monster: y9]
!!VR(totalCost:y):S(cost) *(num);       [Gold cost of monster stack]
!!VR(class:y)&(totalCost)<=999:S0;      [Stack is Class 0 (No Artifact)]
!!VR(class)&(totalCost)>=1000/(totalCost)<=2999:S(ART_LEVEL_TREASURE); [Stack is Class 1 (Treasure)]
!!VR(class)&(totalCost)>=3000/(totalCost)<=11999:S(ART_LEVEL_MINOR);  [Stack is Class 2 (Minor)]
!!VR(class)&(totalCost)>=12000/(totalCost)<=39999:S(ART_LEVEL_MAJOR); [Stack is Class 3 (Major)]
!!VR(class)&(totalCost)>=40000:S(ART_LEVEL_RELIC); [Stack is Class 4 (Relic)]
!!FU&(class)=0:E;                       [Exit if Class 0]

; Get a random artifact
!!FU(ES_769_GetRandomArtifactWithClass):P(class)/?(art:y);

; Set the artifact number as the PO:V0 value
!!PO(x)/(y)/(z):V0/(art);

** [Monster stack is trigger]
!?OB(OBJ_MONSTER)&1000;
!!UN:P769/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;             [exit if option isn't enabled]

!!PO998:V0/?(art:y);

!!if&(art)>0;
  !!MO998:G?(num:y);                    [Number of monsters in stack]
  !!OB998:U?(type:y);                   [Monster type]
  !!SN:H^art^/(art)/0/?(artName:z);

  !!if&(num)=1;
    !!SN:H^monname^/(type)/0/?(monName:z) T^es.769.singEncounter^/?(desc:z)/^mon^/(monName)/^art^/(artName);
  !!el;
    !!SN:H^monname^/(type)/1/?(monName) T^es.769.plurEncounter^/?(desc)/^mon^/(monName)/^art^/(artName);
  !!en;

  !!IF:Q1/(PIC_TYPE_MONSTER)/(type)/(PIC_TYPE_ART)/(art)/(MSG_TYPE_MES)^%(desc)^;
!!en;

** [Post-Battle Trigger]
!$OB(OBJ_MONSTER);
!!UN:P769/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;             [exit if option isn't enabled]

; Give artifact if the number is valid
!!PO998:V0/?(art:y) V0/0;

!!if&(art)>=(ART_CENTAUR_AXE)/(art)<=(ART_META_SPELLBOOK);
  !!HE(CURRENT_HERO):O?(owner:y);

  !!if&(owner)>(NO_PLAYER);
    !!if&1000;
      !!HE(CURRENT_HERO):O?(owner:y) B0/?(heroName:z);
      !!SN:T^es.769.leave^/?(desc:z)/^hero^/(heroName);
      !!IF:Q1/(PIC_TYPE_ART)/(art)/(MSG_TYPE_MES)^%(desc)^;
    !!en;

    !!HE(CURRENT_HERO):A4/(art);
  !!en;
!!en;

!?FU(ES_769_GetRandomArtifactWithClass);
!#VA(class:x) (art:x);

!!re i/0/99;
  !!UN:J6/(class)/?(art);

  ; Break the loop if the found artifact is not any of the following
  !!VR(foundArt:y):S(TRUE);
  !!VR(foundArt)|(art)=(ART_BOOTS_OF_SPEED)/(art)=(ART_EQUESTRIANS_GLOVES)/(art)=(ART_BOOTS_OF_LEVITATION)/(art)=(ART_ANGEL_WINGS):S(FALSE);

  ; Set more art to be invalid if Land Navigation is in land mode
  !!if&i^es_773_landMode^;
    !!VR(foundArt)|(art)=(ART_NECKLACE_OF_OCEAN_GUIDANCE)/(art)=(ART_SEA_CAPTAINS_HAT)/(art)=(ART_ADMIRALS_HAT):S(FALSE);
  !!en;

  !!br&(foundArt);
!!en;

** end
