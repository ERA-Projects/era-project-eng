ZVSE2
** Author orig.  : Algor
** Name          : Medicine
** Name rus.     : Медицина
** Options       : 782

** Fully Rewritten by daemon_n;
** +hp now placed in the "option 743 - health artefacts.erm"

!?FU(OnAfterErmInstructions);           [перед началом новой игры]
!!UN:P782/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;             [выход если опция не включена]

; Set up new name
!!SN:H^secskill^/(SKILL_FIRST_AID)/0/^%T(es.782.name)^;

!!UN:P204/?(firstAidI:y);
!!UN:P190/?(firstAidII:y);

!!re i/1/3;                                                             [(end_value);]
  !!SN:T^es.782.desc%i^/?(desc:z);                                      [; Set up new skill description]

  !!if&(firstAidI);
    !!VR(zErtInd:y):Si+175257 -1;
    !!VR(desc):+z(zErtInd);                                             [; Add in First Aid I description if enabled]
  !!en;

  !!if&(firstAidII);
    !!VR(zErtInd:y):Si +175260 -1;
    !!VR(desc):+z(zErtInd);                                             [; Add in First Aid II description if enabled]
  !!en;
  !!SN:H^secskill^/(SKILL_FIRST_AID)/i/(desc);                          [; Change the descriptions]

!!en;
!!re i/(HERO_FIRST)/(HERO_LAST_WOG);                                    [; Set up new spec description]
  !!HEi:X?(spec:y)/?(spec2:y);
  !!SN&(spec)=0/(spec2)=(SKILL_FIRST_AID):H^spec^/i/2/^%T(es.782.spec)^;
!!en;

!?FU(ES_CreateERMHook);
!!UN:P782/?(isEnabled:y);
!!if&(isEnabled:y);
  !!SN:Ex1/1/4687155/(ES_782_BeforeTentRandomHeal); [установка хука на событие]
!!en;

!?FU(ES_782_BeforeTentRandomHeal);
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_ECX)/4/?(minHeal:y) Cx1/(STRUCT_HOOK_CONTEXT_EAX)/4/?(maxHeal:y);
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_EDI)/4/?(tentStack:y);
!!UN:C(tentStack)/76/4/?(tentNum:y) C(tentStack)/244/4/?(side:y);
!!if&i^battle_hero_%(side)^<>(NO_HERO);
  !!HEi^battle_hero_%(side)^:S(SKILL_FIRST_AID)/?(skillLvl:y); ;              [y13 - уровень навыка Медицина]
  !!if&(skillLvl);
    !!HEi^battle_hero_%(side)^:X?(spec:y)/?(skill:y);
    !!VR(skillLvl)&(spec)=0/(skill)=(SKILL_FIRST_AID):+1;
  !!en;

  !!VR(minHeal):S(skillLvl) *25 +1 *(tentNum);
  !!VR(maxHeal):S(skillLvl) *25 +25 *(tentNum);
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_ECX)/4/(minHeal:y) Cx1/(STRUCT_HOOK_CONTEXT_EAX)/4/(maxHeal:y);
!!en;
