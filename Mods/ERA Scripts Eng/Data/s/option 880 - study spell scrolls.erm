ZVSE2
** Author orig.  : DracoLich
** Rewritten by  : Archer30
** Name          : Study spell scrolls
** Name rus.     : Учеба свитков заклинаний
** Changes rus.  : [Algor] вынос опции в отдельный файл для мода ERA, построчные комментарии
**                 [Algor] вынос текстов в ert-файл
**                 [Algor] уровень заклинания определяется через SS:L, а не "hardcoded"
**                 [Algor] проверка то, что герой уже изучил заклинание
**                 [Algor] сообщение о том, что гильдия недостаточно высокого уровня не выводится
**                 [Algor] цена за обучение привязана к базовой стоимости заклинания S (S^2 * 5)
**                 [Algor] перенумерация функций/переменных и пр.
** Options       : 880


!?FU(ES_880_WriteSpellToSpellBook);
!!CM:R0;

; Exit if no visiting hero
!!CA(CURRENT_TOWN):H1/?(hero:y);

!!if&(hero)=(NO_HERO);
  !!IF:M^%T(es.880.noHero)^;
  !!FU:E;
!!en;

; Exit if the hero has no spell book
!!HE(hero):A2/(ART_SPELL_BOOK)/?(has:y)/?(equipped:y);

!!if&(has)<=0;
  !!IF:M^%T(es.880.noBook)^;
  !!FU:E;
!!en;

; Get the guild level of the town, exit if the town has no guild
!!re (building:y)/0/4;
  !!CA(CURRENT_TOWN):B3/(building);

  !!br&-1;
!!en;

!!VR(guildLv:y):S(building);

; Set the varaible for whether a suitable scroll has been found - use it for IF msg later
!!VR(scrollFound:y):S(FALSE);

; Loop through all the spells and try to find a scroll brought by the hero
!!re i/(SPELL_FIRST)/(SPELL_LAST_WOG);
  !!VR(scroll:y):Si +1001;
  !!HE(hero):A2/(scroll)/?(has)/?(equipped);
  ; Chck if the scroll level is adequate and whether the hero has learned that spell
  !!if&(has)>0;
    !!HE(hero):M=i/1;
    !!SSi:L?(lv:y);

    ; Set scroll as found if all the criteria has been meet, offer to write
    !!if&-1/(lv)<=(guildLv);
      !!VR(scrollFound):S(TRUE);

      !!SSi:C0/?(spCost:y);
      !!VR(cost:y):S(spCost) *(spCost) *5;
      !!SN:T^es.880.ask^/?(ask:z)/^gold^/(cost);
      !!IF:Q1/(PIC_TYPE_SPELL)/i/(MSG_TYPE_QUESTION)^%(ask)^;

      ; Check if gold is enough, proceed to write if positve
      !!if&1;
        !!OW:R(CURRENT_PLAYER)/(RES_GOLD)/?(gold:y);

        !!if&(gold)<(cost);  
          !!IF:M^%T(es.880.cantAfford)^;
        !!el;
          !!OW:R(CURRENT_PLAYER)/(RES_GOLD)/d-(cost);
          !!HE(hero):Mi/1 A3/(scroll)/1/1;

          ; Update the res bar
          !!SN:D;
        !!en;
      !!en;
    !!en;
  !!en;
!!en;

; Show the msg if no adequate spell scroll found
!!IF&(scrollFound)=(FALSE):M^%T(es.880.noScroll)^;
