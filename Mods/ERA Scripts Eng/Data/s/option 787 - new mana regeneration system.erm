ZVSE2
** Author orig.  : Algor
** Updated by    : Archer30
** Fully Rewritten by  : daemon_n
** Name          : New mana regeneration system
** Name rus.     : Новая система восполнения маны
** Options       : 787
** Dialogs       : -
** Variables     :


!?FU(ES_787_Initialization);
!!UN:P787/?(manaSys:y);
!!FU&(manaSys)<>(TRUE):E;               [выход если опция не включена]

!!UN:A(ART_CHARM_OF_MANA)/1/1000;
!!UN:A(ART_TALISMAN_OF_MANA)/1/2000;    [цена Амулета маны = 1000, Талисмана маны = 2000]
!!UN:A(ART_MYSTIC_ORB_OF_MANA)/1/3000 A(ART_MYSTIC_ORB_OF_MANA)/3/(ART_LEVEL_MINOR);      [цена Медали маны = 3000, класс = Minor]

!#FU(ES_787_Initialization):P;

!?FU(OnAfterErmInstructions);           [перед началом игры]
!!UN:P787/?(manaSys:y);
!!FU&(manaSys)<>(TRUE):E;               [выход если опция не включена]

; Disable the additional spell points regeneration from Mysticism I
!!VRi^wog_35_extraManaRegenEnabled^:S(FALSE);

; Set up new skill description
!!SN:H^secskill^/(SKILL_MYSTICISM)/0/?(skillName:z);
!!UN:P35/?(mysticismI:y);
!!UN:P207/?(mysticismII:y);

!!re i/1/3;
  !!VR(percent1:y):Si *3;
  !!VR(percent2:y):Si *20;
  !!SN:T^es.787.desc^/?(desc:z)/^percent1^/(percent1)/^min1^/i/^percent2^/(percent2:y)/^min2^/(percent2:y);
  !!VR(rowNum:y):Si -1;
  !!FU(GetTextFileString):P^skilllev^/(rowNum)/?(skillLevel:z);
  !!VR(skillDesc:z):S^{%(skillLevel) %(skillName)}%(desc)^;

  ; Add in Mysticism I description if enabled (the mana regeneration part is not added)
  !!if&(mysticismI);
    !!VR(skillDesc):+^%T(es.endl)^;

    !!re j/1/i;
      !!SN:T^wog.35.desc%j^/?(mysticismIDesc:z);
      !!VR(skillDesc):+^%(mysticismIDesc)^;
    !!en;
  !!en;

  ; Add in Mysticism II description if enabled
  !!if&(mysticismII);
    !!VR(ertIndex:y):S175152 + i -1;
    !!VR(skillDesc):+^%T(es.endl)^ +z(ertIndex);
  !!en;

  !!SN:H^secskill^/(SKILL_MYSTICISM)/i/(skillDesc); [; Change the descriptions]
  !!VR(artId:y):S(ART_CHARM_OF_MANA) +i -1; [; Set Up new artifact descriptions]
  !!SN:H^art^/(artId)/0/?(artName:z) H^art^/(artId)/1/^{%(artName)}%(desc)^;
!!en;

; Set up new spec description
!!re i/(HERO_FIRST)/(HERO_LAST_WOG);
  !!HEi:X?(spec:y)/?(spec2:y);
  !!SN&(spec)=0/(spec2)=(SKILL_MYSTICISM):H^spec^/i/2/^%T(es.787.spec)^;
!!en;


!?FU(ES_CreateERMHook);
!!UN:P787/?(wogOption:y);               [проверяем включена ли опция 7871]
!!FU&(wogOption)<>(TRUE):E;             [выход если опция не включена]

!!SN:Ex1/1/4863098/(ES_Hero_OnWellCheckingReportMsg);  [att well mana count]
!!SN:Ex1/1/4863148/(ES_Hero_OnWellCheckingSpellPoints);  [att well mana count]

!!SN:Ex1/1/5013369/(ES_Hero_BeforeManaAddedOutsideTown); [adding mana in main heores loop]

!!SN:Ex1/1/5013627/(ES_Hero_HeroRegeneratesManaInTown);  [DOWN HERO]
!!SN:Ex1/1/5013731/(ES_Hero_HeroRegeneratesManaInTown);  [UP HERO]

!!SN:Ex1/1/5128688/(ES_Hero_BeforeMistSpecBonusCount);  [myst spec just adds +1 to myst power]


!?FU(ES_Hero_BeforeMistSpecBonusCount);
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_ECX)/4/0; ecx is Level in v2 = ((Level * 0.050000001 + 1.0) * v14) + 1;

!?FU(ES_Hero_HeroRegeneratesManaInTown);
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_ESI)/4/?(hero:y) C(hero)/26/4/?(heroId:y);
  !!UN:C(hero)/24/2/?(currentSp:y) Cx1/(STRUCT_HOOK_CONTEXT_EAX)/4/?(maxSp:y);
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_ECX)/4/?(currentSp:y) Cx1/(STRUCT_HOOK_CONTEXT_EAX)/4/?(maxSp:y);

  !!if&(currentSp)<(maxSp);
    
    !!FU(ES_Hero_GetSpRestoreCoeff):P(heroId)/?(coeff:y);

    !!FU&(coeff)>4:E;                   [allow 100% restore]

    !!HE(heroId):P?(x:y)/?(y:y)/?(z:y);
    !!re i/1/4;
      !!CA(x:y)/(y:y)/(z:y):B3/i;
      !!VR(coeff)&1:+1;                 [add 20% per each MG level]
    !!en;

    !!if&(coeff)<5;                     [< 5 * 20% allow 100% restore]
      !!FU(ES_787_RestoreMana):Px1/(STRUCT_HOOK_CONTEXT_EAX)/(coeff)/(currentSp)/(maxSp);
    !!en;
  !!en;

!?FU(ES_Hero_OnWellCheckingReportMsg);
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_ESI)/4/?(hero:y) C(hero)/26/4/?(heroId:y);
!!FU(ES_Hero_GetSpRestoreCoeff):P(heroId)/?(coeff:y);

!!if&(coeff)<5;                         [allow 100% restore]
  !!UN:C(hero)/24/2/?(currentSp:y) Cx1/(STRUCT_HOOK_CONTEXT_EDI)/4/?(maxSp:y);
  !!VR(savedCurrExp:y):S(currentSp);
  !!FU(ES_787_RestoreMana):Px1/(STRUCT_HOOK_CONTEXT_EDI)/(coeff)/?(currentSp)/(maxSp);
  !!VR(newHeroSp:y):S(maxSp) -(currentSp) +(savedCurrExp:y);
  !!if&(newHeroSp)<(maxSp);
    !!VRi^es_787_sp_to_add^:S(currentSp);
    !!UN:C(hero)/24/2/(newHeroSp:y);
  !!en;
!!en;

!?FU(ES_Hero_OnWellCheckingSpellPoints)&i^es_787_sp_to_add^;
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_ESI)/4/?(hero:y) C(hero)/26/4/?(heroId:y);
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_EDI)/4/i^es_787_sp_to_add^;
!!VRi^es_787_sp_to_add^:S(NULL);

!?FU(ES_787_RestoreMana);
!#VA(hookContext:x) (hookContextOffset:x) (coeff:x) (currentSp:x) (maxSp:x);
!!VR(needToRestore:y):S(maxSp) -(currentSp);
!!VR(baseRestore:y):S20 *(coeff);
!!if&(baseRestore)<(needToRestore);/;
  !!VR(float:e):S(maxSp) :100 *(baseRestore);
  !!VR(restoredSp:y):S(float) F(baseRestore)/(needToRestore);
  !!if&(restoredSp)<(needToRestore);
    !!VR(currentSp):+(restoredSp);
    !!UN:C(hookContext)/(hookContextOffset)/4/(currentSp);
  !!en;
!!en;

!?FU(ES_Hero_GetSpRestoreCoeff);
!#VA(heroId:x) (result:x);
!!HE(heroId):Z?(hero:y) O?(heroOwnerId:y) A2/(ART_WIZARDS_WELL)/?y99/?(isWell:y);
!!if&(isWell);
  !!VR(result):S100;
!!el;
  !!SN:E5128624/(CALLCONV_THISCALL)/(hero); [get myst power]
  !!VR(result):Sv1;
  !!VR(difficulty:y):S0;
  !!if&(heroOwnerId)<>(NO_OWNER);
    !!OW:I(heroOwnerId)/?(isAi:y);
    !!UN:J2/?(difficulty:y);
    !!VR(difficulty):*(isAi);
    !!VR(result):+(difficulty);
  !!en;
!!en;
	
!?FU(ES_Hero_BeforeManaAddedOutsideTown);
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_EDI)/4/?(hero:y) C(hero)/26/4/?(heroId:y);
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBX)/4/?(currentSp:y) C(hero)/34/1/?(heroOwnerId:y);
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_EAX)/4/?(coeff:y); C(hero)/26/4/?(heroId:y);
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBP)/4/?(ebp:y) C(ebp)/-8/4/?(maxSp:y);


!!FU(ES_Hero_GetTownId):P(heroId)/?(townId:y);
!!if&(townId)<>(NO_TOWN);
  !!CA0/(townId):B3/0;
  !!if&1;
    !!UN:Cx1/(STRUCT_HOOK_CONTEXT_EAX)/4/0;
    !!FU:E;
  !!en;
!!en;

!!VR(difficulty:y):S0;
!!if&(heroOwnerId)<>(NO_OWNER);
  !!OW:I(heroOwnerId)/?(isAi:y);
  !!UN:J2/?(difficulty:y);
  !!VR(difficulty):*(isAi);
!!en;

!!VR(coeff):+(difficulty);

!!VR(percent:y):S(coeff) *3;

!!VR(bonus:e):S(maxSp) :100 *(percent);
!!VR(newSp:y):S(bonus) F(coeff)/(maxSp);

!!UN:Cx1/(STRUCT_HOOK_CONTEXT_EAX)/4/(newSp:y);


!?FU(OnHeroScreenMouseClick)|i^mouse_item^=109/i^mouse_item^=113/i^mouse_item^=120; [клик в окне героя]
!!UN:P787/?y1; !!FU&y1=0:E;   [выход если опция не включена]
!!if&i^mouse_action^<>(MOUSE_LMB_RELEASED);
  !!FU(ES_787_GetRestoreDescription):Pi^mouse_hero_0^; [text would be returned to s^es_787_desc^]
  !!VR(msgType:y):Si^mouse_action^ -12 *2 F(MSG_TYPE_MES)/(MSG_TYPE_POPUP);
  !!IF:Q1/-1/-1/(msgType)/s^es_787_desc^;
  !!VRs^es_787_desc^:S^^;
  !!CM:R0;
!!en;


!?FU(ES_787_GetSpRestoreAtPlace);
!#VA(heroId:x) (maxSp:x) (restore:x);

!!FU(ES_Hero_GetSpRestoreCoeff):P(heroId)/?(coeff:y);
!!if&(coeff)=100;
  !!VR(restore):S(maxSp);
!!el;
  !!VR(percent:y):S3;
  !!VR(baseRestore:y):S1;
  !!FU(ES_Hero_GetTownId):P(heroId)/?(townId:y);
  !!if&(townId)<>(NO_TOWN);
    !!CA0/(townId):B3/0;
    !!if&1;
      !!re i/1/4;
        !!CA0/(townId):B3/i;
        !!VR(coeff)&1:+1;
      !!en;
      !!VR(percent):S20;
      !!VR(baseRestore):S20;
    !!en;
  !!en;

  !!VR(percent):*(coeff);
  !!VR(baseRestore):*(coeff);

  !!VR(bonus:e):S(maxSp) :100 *(percent);
  !!VR(restore):S(bonus) F(baseRestore)/(maxSp);
!!en;

!?FU(ES_787_GetRestoreDescription);
!#VA(heroId:x);
  !!HE(heroId):B0/?(heroName:z) I?(currentSp:y)/1;
  !!FU(ES_Hero_GetFullSpellPoints):P(heroId)/?(maxSp:y);
  !!FU(GetTextFileString):P^genrltxt^/206/?z1;
  !!SN:E6388190/(CALLCONV_CDECL)/?z2/z1/(heroName:z)/(currentSp)/(maxSp);
  !!FU(ES_787_GetSpRestoreAtPlace):P(heroId)/(maxSp)/?(restore:y);

  !!SN:T^es.787.heroScr^/?z1/^hero^/(heroName:z)/^regenSp^/(restore:y);
  !!VRs^es_787_desc^:S^%z2%z1^;         [Here we use global variable as the text usually exceed 512 characters]

!?FU(OnHeroesMeetScreenMouseClick)|i^mouse_item^=113/i^mouse_item^=83/i^mouse_item^=114/i^mouse_item^=84;
!!UN:P787/?(wogOption:y); !!FU&(wogOption)<>(TRUE):E;             [выход если опция не включена]
!!if&i^mouse_action^<>(MOUSE_LMB_RELEASED);
  !!VR(side:y):Si^mouse_item^ -113 F0/1;
  !!FU(ES_787_GetRestoreDescription):Pi^mouse_hero_%(side)^; [text would be returned to s^es_787_desc^]
  !!VR(msgType:y):Si^mouse_action^ -12 *2 F(MSG_TYPE_MES)/(MSG_TYPE_POPUP);
  !!IF:Q1/-1/-1/(msgType)/s^es_787_desc^;
  !!VRs^es_787_desc^:S^^;
  !!CM:R0;                                [отключаем стандартное действие по клику]
!!en;

!?FU(OnAdventureMapRightMouseClick)&i^mouse_item^=2010; [клик на карте приключений]
!!UN:P787/?y1;                [проверяем включена ли опция 787 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]

!!OW:A-1/?(heroId:y);
!!if&(heroId)<>(NO_HERO);
  !!HE(heroId):B0/?(heroName:z) I?(currentSp:y)/1;         [z1/y7 - имя/мана героя]
  !!FU(ES_Hero_GetFullSpellPoints):P(heroId)/?(maxSp:y);
  !!FU(ES_787_GetSpRestoreAtPlace):P(heroId)/(maxSp)/?(restore:y);

  !!SN:T^es.787.advMap^/?z2/^hero^/(heroName:z)/^currSp^/(currentSp:y)/^maxSp^/(maxSp)/^regenSp^/(restore:y);
  !!IF:Q1/(PIC_TYPE_SPELL_POINTS)/(restore:y)/4/^%z2^;              [вывод сообщения]
  !!CM:R0;                      [отключаем стандартное действие по клику]

!!en;
