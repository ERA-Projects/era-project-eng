ZVSE2
** Author orig.  : Algor
** Rewritten by  : Archer30
** Name          : Cutthroats
** Name rus.     : Головорезы
** Options       : 758

** Воры становятся хладнокровными головорезами не зависящими от боевого духа,
** но каждый раз после убийства отряда воодушевляются получая возможность совершить еще одно действие.

; Assassin from Night Scouting cannot trigger cutthroat ability


// Initialization
!?FU(OnAfterErmInstructions);
!!UN:P758/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; Set up new names and description
!!SN:H^monname^/(MON_ROGUE)/0/^%T(es.758.singName)^;
!!SN:H^monname^/(MON_ROGUE)/1/^%T(es.758.plurName)^;
!!SN:H^monname^/(MON_ROGUE)/2/?(desc:z);
!!SN:H^monname^/(MON_ROGUE)/2/^%(desc)%T(es.758.desc)^;
; Give No Morale monster flag and increase the fight value
!!MA:F(MON_ROGUE)/?(fv:y) X(MON_ROGUE)/d|(MON_FLAG_NO_MORALE);
!!VR(fv):*105 :100;
!!MA:F(MON_ROGUE)/(fv);

!?FU(OnSetupBattlefield);
; Initialise variable - This must to be executed regardless of the option
!!VRi^es_758_defStack^:S(NO_STACK);

// Store defending stack ID if it is Rogue attacking another stack
; Skipping Assassin stack (option 795) and stack that has granted morale once this turn
!?FU(OnBeforeBattleAction)&i^battle_acting_stack^<>i^opt795.11.as^;
!!UN:P758/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!BG:A?(action:y);

!!if&(action)=(BATTLE_ACTION_WALK_AND_ATTACK);
  !!BMi^battle_acting_stack^:T?(type:y);
  !!FU(ES_758_CheckIfMonIsEligible):P(type)/?(result:y);

  !!if&(result);
    !!BMi^battle_acting_stack^:F?(flags:y);
    !!VR(moraled:y):S(flags) &(MON_FLAG_MORALE);

    !!if&(moraled)=(FALSE);
      !!BG:E?(defStack:y);
      !!VRi^es_758_defStack^&(defStack)>(NO_STACK):S(defStack);
    !!en;
  !!en;
!!en;

// Check if Rogue is eligible to attack again
!?FU(OnBattleActionEnd)&i^es_758_defStack^>(NO_STACK);
; Check if the defending stack is dead
!!BMi^es_758_defStack^:N?(defNum:y);

!!if&(defNum)<=0;
  ; Remove the acted flag so cutthroat can act again
  !!BMi^battle_acting_stack^:Fd~(MON_FLAG_ACTED) Fd|(MON_FLAG_MORALE);

  ; Show battle log and play animation/sound
  !!if&i^battle_isVisible^;
    !!BMi^battle_acting_stack^:N?(cutthroatNum:y) T?(type:y);
    !!VR(isPlural:y):S(cutthroatNum) -1 B;
    !!SN:H^monname^/(type)/(isPlural)/?(cutthroatName:z);
    !!SN:T^es.758.battleLog^?(battleLog:z)/^cutthroat^/(cutthroatName);
    !!MM:S(battleLog);
    
    !!SN:P^MIRTH^;                      [вывод звука морали]
    !!BMi^battle_acting_stack^:V20;     [анимация морали на отряде]
  !!en;
!!el;
  !!VRi^es_758_defStack^:S(NO_STACK);
!!en;

// Restore the turn to Rogue's turn
!?FU(OnBeforeBattleStackTurn)&i^es_758_defStack^>(NO_STACK);
!#VA(stack:x);

!!VR(stack):Si^battle_acting_stack^;
!!VRi^es_758_defStack^:S(NO_STACK);

// Funciton to check if the targeted monster is eligible for cutting throat
!?FU(ES_758_CheckIfMonIsEligible);
!#VA(mon:x) (result:x);

!!VR(result):S(FALSE);
!!VR(result)&(mon)=(MON_ROGUE):S(TRUE);

** end
