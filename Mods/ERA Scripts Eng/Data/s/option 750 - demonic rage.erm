ZVSE2
** Author orig.  : Algor
** Rewritten by  : Archer30
** Name          : Demonic rage
** Name rus.     : ƒемоническа€ €рость
** Options       : 750

** –огатые демоны получают способность перед атакой впадать в €рость,
** тер€€ защиту (-1) и значительно увеличива€ силу атаки (+3). Ёффект кумул€тивен.
** FV –огатых демонов повышено на 10%.


!?FU(OnAfterErmInstructions);
!!UN:P750/?(wogOption:y);               [провер€ем включена ли опци€ 750]
!!FU&(wogOption)<>(TRUE):E;             [выход если опци€ не включена]

; Set up new description
!!SN:H^monname^/(MON_HORNED_DEMON)/2/?(desc:z);
!!SN:H^monname^/(MON_HORNED_DEMON)/2/^%(desc)%T(es.750.desc)^;

; Raise fight value
!!MA:F(MON_HORNED_DEMON)/?(fv:y);       [FV рогатых демонов]
!!VR(fv):*110 :100;                     [увеличиваем FV на 10%]
!!MA:F(MON_HORNED_DEMON)/(fv);          [обновл€ем FV]

!?FU(OnBeforeBattleAction);
!!UN:P750/?(wogOption:y);               [провер€ем включена ли опци€ 750]
!!FU&(wogOption)<>(TRUE):E;             [выход если опци€ не включена]

!!BG:N?(stack:y) A?(action:y);
!!BM(stack):T?(type:y) D?(def:y);

!!if&(action)=(BATTLE_ACTION_WALK_AND_ATTACK)/(def)>0;
  !!FU(ES_750_CheckIfMonIsEligible):P(type)/?(result:y);

  !!if&(result);
    ; Show battle log and animation
    !!if&i^battle_isVisible^;
      !!BM(stack):N?(num:y);
      !!VR(isPlural:y):S(num) -1 B;
      !!SN:H^monname^/(type)/(isPlural)/?(monName:z);
      !!SN:T^es.750.battleLog%(isPlural)^?(battleLog:z)/^mon^/(monName);
      !!MM:S(battleLog);                  [вывод текста в лог битвы]
      
      !!SN:P^FRENZY^;                     [вывод звука бешенства]
      !!BM(stack):V17;
    !!en;

    !!BM(stack):Dd-1 Ad3;                 [снижение защиты, повышение атаки]
  !!en;
!!en;

// Funciton to check if the targeted monster is eligible for rage
!?FU(ES_750_CheckIfMonIsEligible);
!#VA(mon:x) (result:x);

!!VR(result):S(FALSE);
!!VR(result)&(mon)=(MON_HORNED_DEMON):S(TRUE);

**end
