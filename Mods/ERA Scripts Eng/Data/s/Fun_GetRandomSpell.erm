ZVSE

** ver 1.0

!?FU(Fun_GetRandomSpell);               // получение по критериям отбора случайного незапрещенного заклинания в v1 (-1 если нет)
; Допустимые значения критериев отбора 0/1/2 (прочие считаются как 0)
; 0 - критерий отбора не учитывается (аналог "или" для нескольких критериев)
; 1 - заклинание должно соответствовать критерию (аналог "и" для нескольких критериев)
; 2 - заклинание должно НЕ соответствовать критерию (аналог "и не" для нескольких критериев)
; Критерии отбора:
; x1 - Уровень 1
; x2 - Уровень 2
; x3 - Уровень 3
; x4 - Уровень 4
; x5 - Уровень 5
; x6 - Школа Воздуха
; x7 - Школа Воды
; x8 - Школа Огня
; x9 - Школа Земли
; x10 - Боевое заклинание
; x11 - конкретный набор флагов, например: 528 (512+16) - Заклинание с уроном, цель - 1 отряд (0 - не учитывать флаги)
; Пример: найти случайное заклинание (1 или 3 или 5 уровня), принадлежащее (Воздуху И Земле), являющееся (Боевым), набор флагов не имеет значения
; FU(Fun_GetRandomSpell):P0/2/0/2/0/1/0/0/1/1/0;
; В стандартной магической раскладке этим критериям соответствует только "Волшебная стрела"
!!SN:M-1/70/0/0 W^fun.grs^/v1;          // Mi^es.fun.grs^ - временный массив заклинаний
!!VRy1:S-1;                             // y1 - количество подходящих заклинаний
!!re y2/0/69:;                          // y2 - проверяемое заклинание
  !!UN:J0/y2/?y3; !!co&y3=1;            // пропуск запрещенных заклинаний
  !!SSy2:L?y3 S?y4 F?y5;                // y3/y4/y5 - уровень/школы/флаги заклинания
  !!VRy6:Sy4 &1; !!VRy7:Sy4 &4; !!VRy8:Sy4 &2; !!VRy9:Sy4 &8; // y6..y8 - принадлежность Школам Воздуха..Земли
  !!VRy10:Sy5 &1;                       // y10 - боевое заклинание
  !!co&x1=1/y3<>1:;  !!co&x1=2/y3=1:;   // пропуск заклинания не подходящего по критериям уровня
  !!co&x2=1/y3<>2:;  !!co&x2=2/y3=2:;   // ...
  !!co&x3=1/y3<>3:;  !!co&x3=2/y3=3:;   // ...
  !!co&x4=1/y3<>4:;  !!co&x4=2/y3=4:;   // ...
  !!co&x5=1/y3<>5:;  !!co&x5=2/y3=5:;   // ...
  !!co&x6=1/y6=0:;   !!co&x6=2/y6>0:;   // пропуск заклинания не подходящего по критериям Школы
  !!co&x7=1/y7=0:;   !!co&x7=2/y7>0:;   // ...
  !!co&x8=1/y8=0:;   !!co&x8=2/y8>0:;   // ...
  !!co&x9=1/y9=0:;   !!co&x9=2/y9>0:;   // ...
  !!co&x10=1/y10=0:; !!co&x10=2/y10>0:; // пропуск заклинания не подходящего по критерию Боевое/Походное
  !!co&x11>0/x1<>y5:;                   // пропуск заклинания не подходящего по флагам
  !!VRy1:+1; !!SN:Mi^fun.grs^/y1/y2;    // добавление заклинания в массив подходящих
!!en:;                                  // ...
!!VRv1:S-1;                             // инициализация v1 значением по-умолчанию (подходящее заклинание не найдено)
!!if&y1>=0:;                            // если найдено хотя бы одно подходящее заклинание
  !!VRy2:S0 Ry1;                        // выбор случайного заклинания из найденных
  !!SN:Mi^fun.grs^/y2/?v1;              // в переменную v1
!!en:;                                  // ...
!!SN:Mi^fun.grs^;                       // удаление временного массива

** end