ZVSE2
** Author orig.  : Algor
** Rewritten by  : daemon_n and Archer30
** Name          : Diplomatic bargaining
** Name rus.     : Дипломатические торги
** Options       : 786
** Dialogs       : -
** Variables     : v7900-v7906, v7910-v7916


!?FU(OnAfterErmInstructions);           [пост-инструкция]
!!UN:P786/?(bargaining:y);              [проверяем включена ли опция 786]
!!FU&(bargaining)<>(TRUE):E;            [выход если опция не включена]

; Set Up new Diplomacy descriptions
!!SN:T^es.786.descA^/?(desc1:z)/^percent1^/10;
!!SN:T^es.786.descA^/?(desc2:z)/^percent1^/20;
!!SN:T^es.786.descA^/?(desc3:z)/^percent1^/30;

!!SN:T^es.786.descB^/?(desc1Ext:z)/^percent2^/20;
!!SN:T^es.786.descB^/?(desc2Ext:z)/^percent2^/40;
!!SN:T^es.786.descB^/?(desc3Ext:z)/^percent2^/60;

!!VR(desc1):+^%T(es.endl)%(desc1Ext)^;
!!VR(desc2):+^%T(es.endl)%(desc2Ext)^;
!!VR(desc3):+^%T(es.endl)%(desc3Ext)^;

; Add in scholar description if Obelisk replaced by Scholars is enabled
!!UN:P783/?(scholar:y);

!!if&(scholar);
  !!SN:T^es.786.scholar^/?(scholarDesc:z)/^percent^/10;
  !!VR(desc1):+^%T(es.period)%(scholarDesc)^;
  !!SN:T^es.786.scholar^/?(scholarDesc)/^percent^/20;
  !!VR(desc2):+^%T(es.period)%(scholarDesc)^;
  !!SN:T^es.786.scholar^/?(scholarDesc)/^percent^/30;
  !!VR(desc3):+^%T(es.period)%(scholarDesc)^;
!!en;

!!SN:H^secskill^/(SKILL_DIPLOMACY)/0/?(name:z);
!!SN:H^secskill^/(SKILL_DIPLOMACY)/(SKILL_BASIC)/^{%T(es.basic)%(name)}%(desc1)^;
!!SN:H^secskill^/(SKILL_DIPLOMACY)/(SKILL_ADVANCED)/^{%T(es.advanced)%(name)}%(desc2)^;
!!SN:H^secskill^/(SKILL_DIPLOMACY)/(SKILL_EXPERT)/^{%T(es.expert)%(name)}%(desc3)^;

; Set up new descriptions for diplomacy artifact
; Must be in Erm Instruction to be cmopatible with Enhanced Artifacts I/II (WoG Scripts)
!?FU(ES_786_SetArtDescriptions);
!!UN:P786/?(bargaining:y);              [проверяем включена ли опция 786]
!!FU&(bargaining)<>(TRUE):E;            [выход если опция не включена]

!!SN:T^es.786.descA^/?(artDesc:z)/^percent1^/10;
!!SN:T^es.786.descB^/?(artDescExt:z)/^percent2^/10;
!!VR(artDesc):+^%T(es.period)%(artDescExt)%T(es.period)^;

; Add in scholar description if Obelisk replaced by Scholars is enabled
!!UN:P783/?(scholar:y);

!!if&(scholar);
  !!SN:T^es.786.scholar^/?(scholarDesc:z)/^percent^/10;
  !!VR(artDesc):+^%(scholarDesc)%T(es.period)^;
!!en;

; Remove space at the end
!!SN:K(artDesc)/?(size:y);
!!VR(lastInd:y):S(size) -1;
!!SN:K(artDesc)/(lastInd)/?(char:z);
!!FU(Substr)&(char)=^ ^:P(artDesc)/0/-1/?(artDesc);

!!re i/(ART_STATESMANS_MEDAL)/(ART_AMBASSADORS_SASH);
  !!SN:H^art^/i/0/?(artName:z);
  !!SN:H^art^/i/1/^{%(artName)}%(artDesc)^;
!!en;

!#FU(ES_786_SetArtDescriptions):P;

!?FU(ES_786_RestoreMonsterCosts);       [установка оригинальных цен существ]
  !!re (monId:y)/7900/7906;
    !!if&v(monId)<>(NO_MON);
      !!VR(vPrice:y):S(monId) +10;
      !!MA:Cv(monId)/(RES_GOLD)/v(vPrice:y); [восстанавливаем оригинальную стоимость монстра 1го типа]
      !!VRv(monId:y):S(NO_MON);         [v, устанавливаем признак оригинальной цены монстрам 1го-7го типов]
    !!en;
  !!en;

  !!UN:P786/1;                          [устанавливаем опцию 786 в 1 (цены не снижены)]

!?OB(OBJ_MONSTER);                      [при атаке монстра]
  !!UN:P786/?(wogOption:y);             [проверяем включена ли опция 786]
  !!FU&(wogOption)=(FALSE):E;           [выход если опция не включена]

  !!MO998:R?(aggression:y)/1;
  !!MO998&(aggression)>0:R10/1;         [устанавливаем монстру максимальную агрессию]

!?OB(OBJ_CREATURE_GENERATOR_1);         [посещение внешнего жилища тип 17]
  !!UN:P786/?(wogOption:y);             [проверяем включена ли опция 786]
  !!FU&(wogOption)=(FALSE):E;           [выход если опция не включена]

  !!FU(ES_786_RestoreMonsterCosts)&(wogOption)=2:P; [если цены изменены, устанавливаем оригинальные]

  !!FU(ES_786_SetDiscountMonsterCosts):P; [устанавливаем скидку]

!?FU(ES_786_GetDiscountLevel);
!#VA(discount:x);

  !!HE(CURRENT_HERO):S(SKILL_DIPLOMACY)/?(discount);

  !!re i/66/68;
    !!HE(CURRENT_HERO):A2/i/d/?(isArt:y);[навык дипломатии, кол-во надетых артефактов дипломата медаль/кольцо/лента]
    !!VR(discount)&(isArt):+1;          [учитываем только одно кольцо]
  !!en;

!?FU(ES_786_SetDiscountMonsterCosts);

  !!FU(ES_786_GetDiscountLevel):P?(discount:y);
  !!VR(discount):*-1 +10;               [коэффициент снижения стоимости]

  !!if&(discount)>0;

    !!VRv7900:C-1/-1/-1/-1/-1/-1/-1;    [инициализация переменных с номерами обиталетей]

    !!re i/0/3;
      !!VR(monId:y):Si +7900;
      !!DW998:Mi/?v(monId:y)/d;

      !!if&v(monId)<>(NO_MON);
        !!VR(vPrice:y):Si +7910;
        !!MA:Cv(monId)/(RES_GOLD)/?v(vPrice:y);   [store old price into v7900-7093]
        !!VR(newPrice:y):Sv(vPrice) *(discount) :10; [set discount]
        !!MA:Cv(monId)/(RES_GOLD)/(newPrice:y);   [set new price]
      !!en;
    !!en;

    !!UN:P786/2;                        [выставляем флаг сниженной цены (опция 786=2)]
  !!en;

!$OB(OBJ_CREATURE_GENERATOR_1);         [посещение внешнего жилища тип 17, посттриггер]
  !!UN:P786/?(wogOption:y);             [проверяем включена ли опция 786]
  !!FU(ES_786_RestoreMonsterCosts)&(wogOption)=2:P;

!?OB(OBJ_CREATURE_GENERATOR_4);         [посещение внешнего жилища тип 20]
  !!UN:P786/?(wogOption:y);             [проверяем включена ли опция 786]
  !!FU&(wogOption)=(FALSE):E;           [выход если опция не включена]

  !!FU(ES_786_RestoreMonsterCosts)&(wogOption)=2:P; [если цены изменены, устанавливаем оригинальные]
  !!FU(ES_786_SetDiscountMonsterCosts):P;

!$OB(OBJ_CREATURE_GENERATOR_4);         [посещение внешнего жилища тип 20, посттриггер]
  !!UN:P786/?(wogOption:y);             [проверяем включена ли опция 786]
  !!FU(ES_786_RestoreMonsterCosts)&(wogOption)=2:P;

!?FU(ES_OnGetHillFortCreatureLevel)&i^es_786_hillFortDiscount^>0;
!#VA(address:x);

  !!UN:C(address)/(STRUCT_HOOK_CONTEXT_EDX)/4/?(currentMonId:y);

  !!if&(currentMonId)<>(NO_MON);
    !!MA:L(currentMonId)/?(monLvl:y);

    !!if&(monLvl);

      !!VR(mult:y):S10 -i^es_786_hillFortDiscount^ *10;
      !!VR(percent:e):S(mult) :100;

      !!if&(monLvl)<4;
        !!VR(monFloat:e):S(monLvl)*25 :100;
        !!VR(percent:e):*(monFloat);
      !!en;

      !!VR(offset:y):S(@percent) -1 *4;
      !!UN:C10784536/(offset)/(UNC_INT)/?(multFloatConverted:y); [10784536 - address for e1]
      !!UN:C6548300/24/(UNC_INT)/(multFloatConverted);

      !!UN&(monLvl)<6:C(address)/(STRUCT_HOOK_CONTEXT_EDX)/(UNC_INT8)/(MON_ANGEL);
    !!en;
  !!en;

!?OB(OBJ_HILL_FORT);                    [посещение форта-на-холме]
  !!UN:P786/?(wogOption:y);             [проверяем включена ли опция 786]
  !!FU&(wogOption)=(FALSE):E;           [выход если опция не включена]
  !!UN:C6548300/24/(UNC_INT)/?i^es_786_originalCoefficient^;

  !!FU(ES_786_GetDiscountLevel):P?i^es_786_hillFortDiscount^;

!$OB(OBJ_HILL_FORT)&i^es_786_hillFortDiscount^;          [посещение форта-на-холме, посттриггер]

  !!UN:C6548300/24/(UNC_INT)/i^es_786_originalCoefficient^;
  !!VRi^es_786_hillFortDiscount^:S(FALSE);
  !!VRi^es_786_originalCoefficient^:S(FALSE);

!?OB(OBJ_REFUGEE_CAMP);                 [посещение лагеря беженцев]
!!UN:P786/?(wogOption:y);               [проверяем включена ли опция 786]
!!FU&(wogOption)=(FALSE):E;             [выход если опция не включена]

!!FU(ES_786_RestoreMonsterCosts)&y1=2:P; [если цены изменены, устанавливаем оригинальные]
!!FU(ES_786_GetDiscountLevel):P?(discount:y);

!!if&(discount);
  !!VR(discount):*-1 +10;               [коэффициент снижения стоимости]

  !!VRv7900:C-1/-1/-1/-1/-1/-1/-1;      [v7900, инициализация переменных с номерами обиталетей]
  !!OB998:U?v7900;                      [v7900 - тип нанимаемого монстра]
  !!MA&v7900>-1:Cv7900/(RES_GOLD)/?v7910;[v7910 - оригинальная стоимость монстра]
  !!VR(newPrice:y):Sv7910 *(discount) :10; [сниженная на 10%*уровень Дипломатии стоимость монстра 1го типа]
  !!MA&v7900>-1:Cv7900/(RES_GOLD)/(newPrice); [устанавливаем стоимость монстра 1го типа]
  !!UN:P786/2;                          [выставляем флаг сниженной цены (опция 786=2)]
!!en;

!$OB(OBJ_REFUGEE_CAMP);                 [посещение лагеря беженцев, посттриггер]
!!UN:P786/?(wogOption:y);               [проверяем включена ли опция 786]
!!FU&(wogOption)<2:E;                   [выход если опция не включена или цены не снижались]

!!FU(ES_786_RestoreMonsterCosts):P;     [если цены изменены, устанавливаем оригинальные]

// Change the aggression of neutral stacks on mouse click (so you won't see wrong info with Vision)
!?FU(OnAdventureMapRightMouseClick);
!!UN:P786/?(wogOption:y);               [проверяем включена ли опция 786]
!!FU&(wogOption)=(FALSE):E;             [выход если опция не включена]
  
!!CM:P?(x:y)/?(y:y)/?(z:y);
!!OB(x)/(y)/(z):T?(type:y);

!!if&(type)=(OBJ_MONSTER);
  !!MO(x)/(y)/(z):R?(aggression:y)/1;
  !!MO(x)/(y)/(z)&(aggression)>0:R10/1;
!!en;

** end
