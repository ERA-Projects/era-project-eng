ZVSE2
** Author orig.  : Дьякон (идея и код), Algor (оформление)
** Updated by    : Archer30
** Name          : Necromancy
** Name rus.     : Некромантия
** Options       : 751

** Некромантия восстанавливает после боя до 15/30/50% существ
** в виде нежити, но только из своих павших существ.


!?FU(OnAfterErmInstructions);
!!UN:P751/?(wogOption:y);               [проверяем включена ли опция 751]
!!FU&(wogOption)<>(TRUE):E;             [выход если опция не включена]

; Change secondary skill description
!!SN:H^secskill^/(SKILL_NECROMANCY)/(SKILL_BASIC)/^%T(es.751.desc1)^;
!!SN:H^secskill^/(SKILL_NECROMANCY)/(SKILL_ADVANCED)/^%T(es.751.desc2)^;
!!SN:H^secskill^/(SKILL_NECROMANCY)/(SKILL_EXPERT)/^%T(es.751.desc3)^;

!?FU(OnGameEnter);
!!UN:P751/?(wogOption:y);               [проверяем включена ли опция 751]
!!FU&(wogOption)<>(TRUE):E;             [выход если опция не включена]

!!UN:C6547900/(UNC_INT)/?i^es_751_patch1^ C6547904/(UNC_INT)/?i^es_751_patch2^ C6547908/(UNC_INT)/?i^es_751_patch3^;
!!UN:C4681501/(UNC_INT8)/?i^es_751_patch4^;

!!VRe1:S15 :100;                        [% Базовой Некромантии (15%)]
!!VRe2:S30 :100;                        [% Продвинутой Некромантии (30%)]
!!VRe3:S50 :100;                        [% Экспертной Некромантии (50%)]
!!SN:Xe1 X?(value1:y) Xe2 X?(value2:y) Xe3 X?(value3:y); [конвертация величин для записи в память]
!!UN:C6547900/(UNC_INT)/(value1) C6547904/(UNC_INT)/(value2) C6547908/(UNC_INT)/(value3); [установка %%]
!!UN:C4681501/(UNC_INT8)/8;             [gamecreator: некромантия работает с потерями только собственной армии]

!?FU(OnGameLeave);
!!UN:P751/?(wogOption:y);               [проверяем включена ли опция 751]
!!FU&(wogOption)<>(TRUE):E;             [выход если опция не включена]

!!UN:C6547900/(UNC_INT)/i^es_751_patch1^ C6547904/(UNC_INT)/i^es_751_patch2^ C6547908/(UNC_INT)/i^es_751_patch3^;
!!UN:C4681501/(UNC_INT8)/i^es_751_patch4^;

**end
