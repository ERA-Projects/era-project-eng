ZVSE2
** Author orig.  : DracoLich
** Updated by    : Archer30
** Name          : Custom primary skills
** Name rus.     : Измененная прокачка героев
** Changes rus.  : [Algor] вынос опции в отдельный файл для мода ERA, построчные комментарии
**                 [Algor] вынос текстов в ert-файл
**                 [Algor] исправлен бонус Грамотности в соответствии с описанием (было 0,5/0,75/1, стало 0,25/0,5/1)
**                 [Algor] убраны сообщения об ошибочном вводе значений. Теперь некорректный ввод молча игнорируется.
**                 [Algor] перенумерация функций/переменных и пр.
** Options       : 881
** Dialogs       : 51
** Variables     : w150


!?FU(OnTransferHero);
!#VA(hero:x);

!!UN:P881/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!VRi^es_881_transferred_%(hero)^:S(TRUE); [Mark for PS points initialization]

!?FU(OnAfterErmInstructions);           [пост-инструкция]
!!UN:P881/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;             [выход, если опция не включена]

!!rei/(HERO_FIRST)/(HERO_LAST_WOG);
  ; Skip initialization if there are PS points from the previous scenario
  !!if&i^es_881_transferred_%i^;
    !!VRi^es_881_transferred_%i^:S(FALSE);
    !!co;
  !!en;

  !!IF:Wi;
  !!VRw150:S0;                          [обнуляем w150 для всех героев]
!!en;

!!VRi^es_881_mapStarted^:S(TRUE);

!?FU(OnHeroGainLevel)&1000/i^es_881_mapStarted^; [Получение героем-человеком уровня]
!!UN:P881/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;             [выход, если опция не включена]

!!HL:S?(primSkill:y)/-2/-2;             [получаемый первичный навык]
!!HE(CURRENT_HERO)&(primSkill)=(SKILL_ATTACK):Fd-1/d/d/d; [отменяем увеличение навыка]
!!HE(CURRENT_HERO)&(primSkill)=(SKILL_DEFENSE):Fd/d-1/d/d; [...]
!!HE(CURRENT_HERO)&(primSkill)=(SKILL_POWER):Fd/d/d-1/d; [...]
!!HE(CURRENT_HERO)&(primSkill)=(SKILL_KNOWLEDGE):Fd/d/d/d-1; [...]
!!IF:W(CURRENT_HERO);                   [переменные героя]
!!VRw150:+100;                          [увеличиваем запас нераспределенных навыков (w150)]
!!HE(CURRENT_HERO):S(SKILL_SCHOLAR)/?(scholarLv:y);[уровень Грамотности героя]
!!VR(scholarLv)&(scholarLv)=(SKILL_EXPERT):+1; [Добавляем бонус Грамотности]

!!if&(scholarLv)>(SKILL_NOT_LEARNED);
  !!VR(scholarBonus:y):S(scholarLv)*25; [...]
  !!VRw150:+(scholarBonus);             [...]
!!en;

!!VRi^es_881_during_hero_screen^:S(TRUE);

!?FU(OnAfterHeroGainLevel)&i^es_881_during_hero_screen^;
!!VRi^es_881_during_hero_screen^:S(FALSE);

!?FU(OnHeroScreenMouseClick)&i^mouse_action^=(MOUSE_LMB_PRESSED)/i^mouse_item^>=50/i^mouse_item^<=53/i^key_shift^/i^es_881_during_hero_screen^<>(TRUE); [клик мышкой в окне героя]
!!UN:P881/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;             [выход, если опция не включена]

!!CM:R0;                                [отключаем стандартное действие мыши]

!!IF:W(CURRENT_HERO);                   [переменные героя]

!!if&w150<100;
  !!IF:M^%T(es.881.noPoint)^;           [сообщение "нет свободных очков навыка"]
  !!FU:E;                               [выход, если нет свободных очков навыка]
!!en;

!!VR(picIndex:y):Si^mouse_item^ -19;    [картинка-навык для IF:Q]
!!VR(avail:y):Sw150 :100;               [количество свободных навыков]
!!SN:T^es.881.avail^/?z2/^points^/(avail); [сообщение "Доступно оков:"]
!!VRz3:S^%T(es.881.ask1)
%z2^;                                   [сообщение "хотите добавить в этот навык?"]
!!IF:Q1/(picIndex)/(avail)/(MSG_TYPE_QUESTION)/^%z3^; [...]
!!FU&-1:E;                              [выход, если ответ отрицателен]

!!SN:T^es.881.ask2^/?z4;                [сообщение "Сколько очков навыка добавить?"]
!!IF:D51/4/2/^^/^^/^^/^^/^^/^^/^^/^^/^^/^^/^^/^^/^^ E1/51;[показ диалога]

!!if&v1>-1;
  !!VR(input:y):Vz1 F0/(avail);         [введенное число]
  !!FU&(input)=0:E;

  !!VR(spentPoints:y):S(input) *100;    [распределяемые очки *100]
  !!VRw150:-(spentPoints);              [уменьшаем свободные очки героя]
  !!HE(CURRENT_HERO)&i^mouse_item^=50:Fd(input)/d/d/d;  [повышаем первичные параметры]
  !!HE(CURRENT_HERO)&i^mouse_item^=51:Fd/d(input)/d/d;  [...]
  !!HE(CURRENT_HERO)&i^mouse_item^=52:Fd/d/d(input)/d;  [...]
  !!HE(CURRENT_HERO)&i^mouse_item^=53:Fd/d/d/d(input);  [...]
  !!SN:D;                               [обновляем экран героя]
!!en;

** end
