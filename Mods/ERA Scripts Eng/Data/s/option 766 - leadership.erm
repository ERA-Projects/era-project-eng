ZVSE2
** Author orig.  : Algor (по мотивам опции 880 от Alex)
** Name          : Leadership
** Name rus.     : Ћидерство
** Options       : 766
** Dialogs       : -
** Variables     : v7917-v7919, z799-z801
** Tmp variables : z1-z5
** Timers        : TM51(standard)
** Functions     : FU7940-FU7941
** PO-values     : -


!?FU(OnAfterErmInstructions); [пост-инструкци€: установка описаний]
!!UN:P766/?(wogOption:y);                [провер€ем включена ли опци€ 766 в y1]
!!FU&(wogOption)<>(TRUE):E;                  [выход если опци€ не включена]

!!VRz1:S^Data\s\option 766 - leadership.ini^; [z1 - им€ ini-файла]
!!VRz2:S^option 766^;         [z2 - секци€ ini-файла]
!!UN:N6/3/0/2/1 N6/4/1/2/1 N6/5/2/2/1; [z3-z5 - текстровые значени€ опций 0..2]
!!VRv7917:Vz3;                [v7917 - числовое значение опции 0]
!!VRv7918:Vz4;                [v7918 - числовое значение опции 1]
!!VRv7919:Vz5;                [v7919 - числовое значение опции 2]

; Set up new secondary skill descriptions
!!SN:T^es.766.desc^/?(desc1:z)/^percent^/v7917;
!!SN:T^es.766.desc^/?(desc2:z)/^percent^/v7918;
!!SN:T^es.766.desc^/?(desc3:z)/^percent^/v7919;

!!SN:H^secskill^/(SKILL_LEADERSHIP)/(SKILL_BASIC)/?(basicDesc:z);
!!SN:H^secskill^/(SKILL_LEADERSHIP)/(SKILL_BASIC)/^%(basicDesc)%(desc1)^;

!!SN:H^secskill^/(SKILL_LEADERSHIP)/(SKILL_ADVANCED)/?(advancedDesc:z);
!!SN:H^secskill^/(SKILL_LEADERSHIP)/(SKILL_ADVANCED)/^%(advancedDesc)%(desc2)^;

!!SN:H^secskill^/(SKILL_LEADERSHIP)/(SKILL_EXPERT)/?(expertDesc:z);
!!SN:H^secskill^/(SKILL_LEADERSHIP)/(SKILL_EXPERT)/^%(expertDesc)%(desc3)^;

!?FU(OnEveryDay)&i^timerWeekday^=1/i^timerDay^>1;
!!UN:P766/?y1;                [провер€ем включена ли опци€ 766 в y1]
!!FU&y1=0:E;                  [выход если опци€ не включена]

!!OW:C?y1;                    [y1 - текущий игрок]
!!DO7940/(HERO_FIRST)/(HERO_LAST_WOG)/1:Py1; [увеличиваем живые армии героев с навыком Ћидерство]

!?FU7940;                     [x16 - номер геро€, x1 - текущий игрок]
!!HEx16:O?y1;                 [y1 - игрок-владелец геро€]
!!FU&y1<>x1:E;                [выход, если герой не текущего игрока]

!!HEx16:S(SKILL_LEADERSHIP)/?y1; [y1 - уровень Ћидерства геро€]
!!DO7941/0/6/1&y1>0:Px16/y1;  [прирост численности отр€да]

!?FU7941;                     [x16 - номер отр€да, x1 - номер геро€, x2 - уровень Ћидерства]
!!HEx1:C0/x16/?y1/?y2;        [y1/y2 - тип/количество существ в отр€де]
!!FU|y2<1/y1<0:E;             [выход, если отр€да нет]

!!MA:Xy1/?y3;                 [y3 - флаги существа]
!!VRy3:&(MON_FLAG_ALIVE);     [y3=16, если существа живые]
!!FU&y3=0:E;                  [выход, если существа не живые]

!!VRy3:Sx2 +7916;             [y3 - индекс v-переменной с коэффициентом прироста дл€ нужного уровн€ Ћидерства]
!!VRy3:Svy3 *y2 :100;         [y3 - прирост отр€да]
!!HEx1:C0/x16/d/dy3/0/0;      [добавл€ем в стек новобранцев с нулевым опытом]

** end
