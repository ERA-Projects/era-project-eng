ZVSE2
** Author orig.  : Algor
** Rewritten by  : Archer30
** Name          : Not a place for people
** Name rus.     : Не место для людей
** Options       : 792


!?FU(OnAfterLoadGame);        [при загрузке игры]
!!UN:P792/?(wogOption:y);     [проверяем включена ли опция 792]
!!FU&(wogOption)<>(TRUE):E;   [выход, если опция не включена]

!!UN:J4/64000;                [Восстанавливаем удвоенный SoD'овский "радиус раздумий" ИИ]

!?FU(OnEveryDay)&i^timerOnce^/i^timerDay^=1;[В первый день]
!!UN:P792/?(wogOption:y);     [проверяем включена ли опция 792]
!!FU&(wogOption)<>(TRUE):E;   [выход, если опция не включена]

!!UN:J4/64000;                [Восстанавливаем удвоенный SoD'овский "радиус раздумий" ИИ]

!!UN:J2/?(diff:y);            [0..4 уровень сложности выбранный игроком-человеком]
!!VR(bonusExp:y):S(diff) *(diff) *1000; [0,1000,4000,9000,16000 - бонус опыта ИИ-героям]

!!re i/(HERO_FIRST)/(HERO_LAST_WOG);
  !!HEi:O?(owner:y);

  !!if&(owner)>(NO_OWNER);
    !!OW:I(owner)/?(isAi:y);

    !!if&(isAi);
      !!HEi:E?(exp:y);
      !!VR(newExp:y):S(exp) +(bonusExp);
      !!HEi:E(newExp)/d/1;
    !!en;
  !!en;
!!en;

!?FU(OnEveryDay)&i^timerWeekday^=1/i^timerDay^>1/i^timerOnce^; [каждую неделю начиная со второй]
!!UN:P792/?(wogOption:y);     [проверяем включена ли опция 792]
!!FU&(wogOption)<>(TRUE):E;   [выход, если опция не включена]

!!AI:D-1/-1/-1;               [Удаление всех точек назначения]
!!VR(x:y):S-1;

; loop through all the towns
!!re i;
  !!UN:U(OBJ_TOWN)/(ANY_OBJ)/-1/(x)/(y:y)/(z:y);
  !!br&(x)<0;

  !!CA(x)/(y)/(z):O?(owner:y);

  ; Set the team as -1 and the town as valuable if it has no owner
  !!if&(owner)=(NO_OWNER);
    !!VR(team:y):S-1;
    !!VR(importance:y):S500000;         [Valuable]
  !!el;
    !!OW:I(owner)/?(isAi:y);
    ; Skip to the next town if the town is an AI's town
    !!co&(isAi);

    ; Set the town as very important if it is a human's town
    !!OW:T(owner)/?(team);
    !!VR(importance):S5000000;          [Very Important]
  !!en;

  !!CA(x)/(y)/(z):U?(townId:y);

  ; Loop through all the AI players and set importance of the town
  !!re i/(PLAYER_FIRST)/(PLAYER_LAST);
    !!OW:Ii/?(isAi:y)/?(hasLost:y) Ti/?(aiTeam:y);

    !!if&(isAi)/(hasLost)<>(TRUE)/(aiTeam)<>(team);
      !!AI:S-1/3/(townId)/(x)/(y)/(z)/(importance)/0; [Установка значимой точки для несоюзного ИИ]
      !!UN:S(x)/(y)/(z)/3/5; [открываем область вокруг значимой точки для несоюзного ИИ]
    !!en;
  !!en;
!!en;

** end
