ZVSE2
** Author orig.  : Algor, igrik
** Name          : Health artefacts
** Name rus.     : Артефакты здоровья
** Options       : 743

** Fully Rewritten by daemon_n

!?FU(OnAfterErmInstructions);
  !!UN:P743/?(wogOption:y);     [проверяем включена ли опция 743]
  !!FU&(wogOption)<>(TRUE):E;   [выход если опция не включена]

  !!SN:H^art^/(ART_RING_OF_VITALITY)/1/^%T(es.743.desc1)^;
  !!SN:H^art^/(ART_RING_OF_LIFE)/1/^%T(es.743.desc2)^;
  !!SN:H^art^/(ART_VIAL_OF_LIFEBLOOD)/1/^%T(es.743.desc3)^;

!?FU(ES_CreateERMHook);
!!UN:P743/?i^ES_health_artifacts_enabled^ P782/?i^ES_medicine_enabled^;?(medicine:y);     [проверяем включена ли опция 743]

!!if|i^ES_health_artifacts_enabled^/i^ES_medicine_enabled^;
  !!SN:Ex1/1/5138208/(ES_AfterVitalityArtifactsBonusesCalculation); [установка хука на событие]
!!en;

!?FU(ES_AfterVitalityArtifactsBonusesCalculation);            [вызов функции из хука]
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBP)/4/?(ebp:y) C(ebp)/-4/4/?(hero:y);
  ec
  !!UN:C(hero)/26/4/?(heroId:y) C(ebp)/8/4/?(monID:y);
  !!MA:X(monID)/?f;
  !!VRf:&(MON_FLAG_ALIVE);
  !!if&f;
    !!if&i^ES_medicine_enabled^;
      !!HE(heroId:y):S(SKILL_FIRST_AID)/?(skillLvl:y) X?(spec:y)/?(skill:y);              [y11 - уровень Медицины героя [0..3]]
      !!if&(skillLvl)/(spec)=0/(skill)=(SKILL_FIRST_AID);
        !!VR(skillLvl):+1;
      !!en;
      !!UN:Cx1/(STRUCT_HOOK_CONTEXT_EAX)/4/d(skillLvl);
    !!en;

    !!if&i^ES_health_artifacts_enabled^;
      !!UN:Cx1/(STRUCT_HOOK_CONTEXT_EAX)/4/?h;

      !!HE(heroId):A2/131/?y99/?(elix:y);

      !!if&(elix);
        !!VR(bonus:y):S5;
      !!el;
        !!HE(heroId):A2/(ART_RING_OF_VITALITY)/?y99/?(ringA:y) A2/(ART_RING_OF_LIFE)/?y99/?(ringB:y) A2/(ART_VIAL_OF_LIFEBLOOD)/?y99/?(vial:y);
        !!VR(ringA):F0/1;
        !!VR(ringB):F0/1;
        !!VR(vial):F0/1;
        !!VR(bonus:y):S(vial) *2 +(ringA) +(ringB);
      !!en;

      !!if&(bonus)>0;
        !!VR(bonus):*5;
        !!VR(newHp:e):Sh :100 *(bonus);
        !!VR(newHpY:y):S(newHp);
        !!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBX)/4/(newHpY);
      !!en;
    !!en;
  !!el;
    !!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBX)/4/0;
  !!en;
