ZVSE2
** Author orig.  : Algor
** Rewritten by  : Archer30
** Name          : Open map
** Name rus.     : ќткрыта€ карта
** Options       : 794

** ѕеред началом игры выдаетс€ диалог выбора дл€ каких игроков
** открывать карту (по умолчанию - только красному).


!?FU(OnAfterErmInstructions);           [пост-инструкци€]
!!UN:P794/?(wogOption:y);               [провер€ем включена ли опци€ 794 в y1]
!!FU&(wogOption)<>(TRUE):E;             [выход если опци€ не включена]

!!VR(defaultChoiceBits:y):S0;

!!re i/(PLAYER_FIRST)/(PLAYER_LAST);
  ; Initialize string for options
  !!VR(ptr:y):Si +2;
  !!VRz(ptr):S^^;

  ; Set up the string when a player exist in the game
  !!OW:Ii/?(isAi:y)/?(hasLost:y);

  !!if&(hasLost)<>(TRUE);
    ; Get the player's name
    !!UN:C(GAME_MANAGER)/(UNC_INT)/?(gameMgr:y);
    !!VR(playerNameAddr:y):Si *360 +(gameMgr) +134044;
    !!SN:B(playerNameAddr)/d/?(playerName:z);

    ; Get the string of colour
    !!FU(GetTextFileString):P^plcolors^/i/?(colourStr:z);

    ; Set up text for the option
    !!SN:T^es.794.opt^/?z(ptr)/^player^/(playerName)/^colour^/(colourStr);

    ; Add to the default choice if is a human player
    !!if&(isAi)<>(TRUE);
      !!VR(playerBits:y):S1 Sd<<i;
      !!VR(defaultChoiceBits:y):+(playerBits);
    !!en;
  !!en;
!!en;

; Open the dialogue
!!SN:T^es.794.title^/?z1;
!!IF:G0/2/(defaultChoiceBits)/1/2/3/4/5/6/7/8/9;

; Reveal the map for the selected player(s)
!!re i/(PLAYER_FIRST)/(PLAYER_LAST);
  !!VR(playerBits:y):S1 Sd<<i;
  !!VR(wasSelected:y):Sv2 &(playerBits);

  !!if&(wasSelected);
    !!UN:X?(size:y)/?(isDoubleLv:y) S0/0/0/i/356;
    !!UN&(isDoubleLv):S0/0/1/i/356;
  !!en;
!!en;

** end
