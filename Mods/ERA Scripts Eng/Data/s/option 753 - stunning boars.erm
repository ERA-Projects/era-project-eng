ZVSE2
** Author orig.  : Algor
** Rewritten by  : Archer30
** Name          : Stunning boars
** Name rus.     : Ошеломляющие кабаны
** Options       : 753

** Орки на кабанах при атаке получают способность ошеломить противника (действует только на живых существ).
** Ошеломленный отряд теряет способность ходить и контратаковать в текущем раунде.
** Шанс ошеломления тем больше, чем сильнее отряд орков на кабанах и слабее их противник.
** FV орков на кабанах повышено на 10%.


!?FU(OnAfterErmInstructions);
!!UN:P753/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;             [выход если опция 753 не включена]

; Set up new description
!!SN:H^monname^/(MON_BOAR)/2/?(desc:z);
!!VR(desc):+^%T(es.753.desc)^;
!!SN:H^monname^/(MON_BOAR)/2/(desc);

; Raise fight value
!!MA:F(MON_BOAR)/?(fv:y);               [FV кабанов]
!!VR(fv):*110 :100;                     [увеличиваем FV на 10%]
!!MA:F(MON_BOAR)/(fv);                  [обновляем FV]

!?FU(ES_OnAfterMelee);
!#VA(atkStack:x) (defStack:x);

!!UN:P753/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;             [выход если опция 753 не включена]

; Exit if it is a retaliation
!!BG:N?(currStack:y);
!!FU&(currStack)<>(atkStack):E;

!!BM(atkStack):T?(type:y) N?(atkNum:y) H?(atkHp:y) L?(atkLostHp:y) R?(retals:y); [тип/численность/макс./потерянное здоровье активного отряда]
!!FU(ES_753_CheckIfMonIsEligible):P(type)/?(result:y);
!!FU&(result)<>(TRUE):E;                [выход, если атакуют не кабаны]

!!BM(defStack):F?(monFlags:y) N?(defNum:y) H?(defHp:y) L?(defLostHp:y); [флаги/численность/макс./потерянное здоровье целевого отряда]
!!FU&(defNum)<=0:E;

; Exit the function if the defending monster has acted this turn + has no retaliation or is not a living creature
!!VR(isAlive:y):S(monFlags) &(MON_FLAG_ALIVE);
!!FU&(isAlive)=(FALSE):E;

!!VR(hasActed:y):S(monFlags) &(MON_FLAG_ACTED);
!!FU&(hasActed)/(retals)=0:E;

; Calculate total HP of the two stacks
!!VR(totalAtkHp:y):S(atkHp) *(atkNum) -(atkLostHp); [суммарное здоровье отряда Кабанов]
!!VR(totalDefHp:y):S(defNum) *(defHp) -(defLostHp); [суммарное здоровье целевого отряда]
!!FU&(totalDefHp)<1:E;                  [выход, если целевой отряд уже мертв]

!!VR(ratio:y):S(totalAtkHp) *100 :(totalDefHp); [шанс срабатывания оглушения (в %)]

!!VR(random:y):R0/0/99;                 [случайное число (1..100)]
!!FU&(ratio)<(random):E;                [выход, если оглушение не сработало]

!!VR(monFlags):|(MON_FLAG_ACTED);       [+флаг отряд уже ходил в этом раунде]
!!BM(defStack):R0 F(monFlags);          [цель теряет возможность контратаковать и ходить в текущем раунде]

; Show msg in the battle log and animation
!!if&i^battle_isVisible^;  
  !!VR(isPlural:y):S(defNum) -1 B;
  !!SN:H^monname^/(type)/(isPlural)/?(monName:z);
  !!SN:T^es.753.battleLog^?(battleLog:z)/^mon^/(monName);
  !!MM:S(battleLog);

  !!SN:P^Disruptr^;
  !!BM(defStack):V49;                   [анимация на отряде кабанов]
!!en;

// Funciton to check if the targeted monster is eligible to stun
!?FU(ES_753_CheckIfMonIsEligible);
!#VA(mon:x) (result:x);

!!VR(result):S(FALSE);
!!VR(result)&(mon)=(MON_BOAR):S(TRUE);
