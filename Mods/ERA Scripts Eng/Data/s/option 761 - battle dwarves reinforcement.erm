ZVSE2
** Author orig.  : Algor
** Rewritten by  : Archer30
** Name          : battle dwarves reinforcement
** Name rus.     : укрепление боевых гномов
** Options       : 761


!?FU(OnAfterErmInstructions);           [пост-инструкция: установка описаний]
!!UN:P761/?(wogOption:y);               [проверяем включена ли опция 761]
!!FU&(wogOption)<>(TRUE):E;             [выход если опция не включена]

; Set up new description
!!SN:H^monname^/(MON_BATTLE_DWARF)/2/?(desc:z);
!!SN:H^monname^/(MON_BATTLE_DWARF)/2/^%(desc)%T(es.761.desc)^;

; Raise fight value
!!MA:F(MON_BATTLE_DWARF)/?(fv:y);       [FV боевых гномов]
!!VR(fv):*105 :100;                     [увеличиваем FV на 5%]
!!MA:F(MON_BATTLE_DWARF)/(fv);          [обновляем FV боевых гномов]

; Set up global var
!?FU(OnSetupBattlefield);
!!UN:P761/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!re i/(BATTLE_STACK_FIRST)/(BATTLE_STACK_LAST);
  !!VRi^es_761_dwarfDefIncreased_%i^:S(FALSE);
!!en;

!?FU(OnBattleRound)&i^battle_round^>=1;
!!re i/(BATTLE_STACK_FIRST)/(BATTLE_STACK_LAST);
  !!VRi^es_761_dwarfDefIncreased_%i^:S(FALSE);
!!en;

; Trigger after being attacked
!?FU(ES_OnAfterAttack);
!#VA(atkStack:x) (defStack:x);

!!UN:P761/?(wogOption:y);               [проверяем включена ли опция 761]
!!FU&(wogOption)<>(TRUE):E;             [выход если опция не включена]

; Exit if it is a retaliation
!!BG:N?(currStack:y);
!!FU&(currStack)<>(atkStack):E;

; Increase the defense of Battle Dwarves if they are hit
!!BM(defStack):T?(type:y) N?(num:y);
!!FU(ES_761_CheckIfMonIsEligible):P(type)/?(result:y);

!!if&(result)/(num)>0/i^es_761_dwarfDefIncreased_%(defStack)^=(FALSE);
  ; Increase defence by 2
  !!BM(defStack):Dd2;                   [укрепление отряда]

  ; Change the global var
  !!VRi^es_761_dwarfDefIncreased_%(defStack)^:S(TRUE);
  
  ; Show battle log and play animation/sound
  !!if&i^battle_isVisible^;
    !!VR(isPlural:y):S(num) -1 B;
    !!SN:H^monname^/(type)/(isPlural)/?(monName:z);
    !!SN:T^es.761.battleLog^?(battleLog:z)/^mon^/(monName);
    !!MM:S(battleLog);
    
    !!SN:P^SHIELD^;                     [вывод звука щита]
    !!BM(defStack):V27;                 [анимация]
  !!en;
!!en;

// Funciton to check if the targeted monster is eligible for reinforcement
!?FU(ES_761_CheckIfMonIsEligible);
!#VA(mon:x) (result:x);

!!VR(result):S(FALSE);
!!VR(result)&(mon)=(MON_BATTLE_DWARF):S(TRUE);

** end
