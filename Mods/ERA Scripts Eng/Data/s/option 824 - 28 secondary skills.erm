ZVSE2
** Author        : igrik
** Name          : 28 secondary skills
** Name rus.     : максимум вторичных навыков
** Options       : 824

** Опция устанавливает максимальное количество вторичных навыков, доступных героям
** По-умолчанию количество доступных навыков повышено до 10, значение настраивается в ini-файле

!?FU(ES_824_SetSecSkillLimit);
!!UN:P824/?(wogOption:y);               [включен ли скрипт]

!!if&(wogOption);
  !!VRz1:S^Data\s\option 824 - 28 secondary skills.ini^; [z1 - имя ini-файла]
  !!VRz2:S^option 824^;                 [z2 - секция ini-файла]
  !!UN:N6/3/0/2/1;
  !!VR(value:y):Vz3 F8/28;              [кол-во втор.навыков из ini-файла]

  ; Check if the current skill limit is lower than config  
  !!UN:C4881872/(UNC_INT8)/?(currValue:y);

  !!if&(currValue)<(value);
    ; Store the current value
    !!VRi^es_824_patch^:S(currValue);

    ; Set up new values
    !!UN:C4881872/(UNC_INT8)/(value) C5091278/(UNC_INT8)/(value) C5121386/(UNC_INT8)/(value) C5718399/(UNC_INT8)/(value);
    !!UN:C6229127/(UNC_INT8)/(value) C6229840/(UNC_INT8)/(value) C7621300/(UNC_INT8)/(value) C4847883/(UNC_INT8)/(value);
  !!en;
!!en;

!#FU(ES_824_SetSecSkillLimit):P;
!#VRi^es_824_isStartGame^:S(TRUE);

; Prevent OnGameEnter lines to be executed again at the start of the game, only loading
!?FU(OnGameEnter);
!!if&i^es_824_isStartGame^;
  !!VRi^es_824_isStartGame^:S(FALSE);
!!el;
  !!FU(ES_824_SetSecSkillLimit):P;
!!en;

!?FU(OnGameLeave);
!!UN:P824/?(wogOption:y);               [включен ли скрипт]

!!if&(wogOption);
  ; restore default values
  !!UN:C4881872/(UNC_INT8)/i^es_824_patch^ C5091278/(UNC_INT8)/i^es_824_patch^ C5121386/(UNC_INT8)/i^es_824_patch^ C5718399/(UNC_INT8)/i^es_824_patch^;
  !!UN:C6229127/(UNC_INT8)/i^es_824_patch^ C6229840/(UNC_INT8)/i^es_824_patch^ C7621300/(UNC_INT8)/i^es_824_patch^ C4847883/(UNC_INT8)/i^es_824_patch^;

  !!VRi^es_824_patch^:S0;
!!en;

** end
