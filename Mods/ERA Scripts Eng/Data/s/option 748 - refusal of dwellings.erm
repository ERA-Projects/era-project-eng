ZVSE2
** Author        : Algor (Idea: Antib)
** Rewritten by  : Archer30
** Name          : Refusal of dwellings
** Name rus.     : Отказ от жилищ
** Options       : 748

** ПКМ на принадлежащем герою внешнем жилище вызывает диалог,в котором от данного жилища можно отказаться,
** сделав его нейтральным и переведя всех невыкупленных существ в охрану (существа 1-4 уровней только до конца недели).
** Отказ от жилищ существ "8го уровня" (3й грейд замковых существ 7го уровня) запрещен,
** если жилища 8го уровня функционируют не как обычные.

; This script dsiables the dwelling feature of option 192 transfer owner.


!?FU(OnAfterErmInstructions);
!!UN:P748/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!VRi^wog_192_dwellingEnabled^:S(FALSE);

!?FU(OnAdventureMapLeftMouseClick)&i^key_shift^/i^mouse_item^=37;
!!UN:P748/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!CM:P?(x:y)/?(y:y)/?(z:y);

; Exit if not clicking on a dwelling
!!OB(x)/(y)/(z):T?(type:y) U?(subtype:y);
!!FU&(type)<>(OBJ_CREATURE_GENERATOR_1)/(type)<>(OBJ_CREATURE_GENERATOR_4):E;

; Exit if the interacting player is not the owner of the dwelling
!!DW(x)/(y)/(z):O?(owner:y);
!!OW:C?(player:y)/?(interactPlayer:y);
!!FU|(owner)<>(player)/(owner)<>(interactPlayer):E;

; Exit if regular level 8 dwelling is disabled and clicking on a level 8 dwelling
!!UN:P(WOG_OPT_REGULAR_LEVEL8_DWELLINGS)/?(normalLv8:y);
!!FU&(normalLv8)<>(TRUE)/(type)=(OBJ_CREATURE_GENERATOR_1)/(subtype)>=80/(subtype)<=88:E;

!!CM:R0;

; Set up the dialogue with monster and numbers
!!FU(NewIntArray):P?(availMons:y);

!!re (slot:y)/0/3;
  !!DW(x)/(y)/(z):M(slot)/?(mon:y)/?(num:y);

  !!if&(mon)>(NO_MON);
    !!VR(num):F0/32767;
    !!VR(monAndNum:y):S65536 *(num) +(mon);
    !!FU(Array_Push):P(availMons)/(PIC_TYPE_MONSTER)/(monAndNum);
  !!en;
!!en;

!!FU(PrepareMultiPicDialog):P(availMons);

; Get the string for the title
!!if&(type)=(OBJ_CREATURE_GENERATOR_1);
  !!FU(GetTextFileString):P^crgen1^/(subtype)/?(dwellName:z);
!!el&(type)=(OBJ_CREATURE_GENERATOR_4);
  !!FU(GetTextFileString):P^crgen4^/(subtype)/?(dwellName);
!!en;

!!VR(ownerStrIndex:y):S(owner) +23;
!!FU(GetTextFileString):P^arraytxt^/(ownerStrIndex)/?(ownerStr:z);

; Open the dialogue
!!IF:N(MSG_TYPE_QUESTION)/^{%(dwellName)}

%(ownerStr)

%T(es.748.dlg)^/?(choice:y);

; Set the dwelling to no owner and convert all the available monsters to guards
!!if&(choice);
  !!DW(x)/(y)/(z):O(NO_OWNER)/1;

  !!re (slot)/0/3;
    !!DW(x)/(y)/(z):M(slot)/?(availMon:y)/?(availNum:y);
    !!DW(x)/(y)/(z):G(slot)/?(guardMon:y)/?(guardNum:y);

    !!if|(guardMon)=(availMon)/(guardMon)=(NO_MON);
      !!VR(guardNum):+(availNum);

      !!if&(guardNum)>0;
        !!DW(x)/(y)/(z):G(slot)/(availMon)/(guardNum);
      !!el;
        !!DW(x)/(y)/(z):G(slot)/(NO_MON)/0;
      !!en;
      
      !!DW(x)/(y)/(z):M(slot)/(availMon)/0;
    !!en;
  !!en;
!!en;

**end
