ZVSE

** ver 1.0

!?FU(Fun_SetSecSkillValue);             // установка числового значения вторичного навыка
; x1 - вторичный навык (0..27)
; x2 - уровень навыка (0..3)
; x3 - числовое значение навыка
; x4 - дополнительное значение, специфайное для навыка
;
; Не для всех навыков возможно установить числовое значение, неподдерживаемые комбинации не дадут эффекта
; Пример:
; ?FU(OnAfterLoadGame); !FU(Fun_SetSecSkillValue):P23/2/33; // Продвинутая (2) Защита (23) снижает урон на 33%
!!FU|x2<0/x2>3:E;                       // выход, если уровень навыка указан неверно
!!FU&x1=0:E;                            // Поиск пути - не применимо
!!FU(Fun_SetSecSkillValuePercent)&x1=1:P6547944/x2/x3;  // Стрельба - процентное значение
!!FU(Fun_SetSecSkillValuePercent)&x1=2:P6548072/x2/x3;  // Логистика
!!FU(Fun_SetSecSkillValuePercent)&x1=3:P6547928/x2/x3;  // Разведка
!!FU(Fun_SetSecSkillValuePercent)&x1=4:P6548024/x2/x3;  // Дипломатия
!!FU&x1=5:E;                            // Навигация - не применимо
!!FU(Fun_SetSecSkillValuePercent)&x1=6:P6547880/x2/x3;  // Лидерство
!!FU&x1=7:E;                            // Мудрость - не применимо
!!FU(Fun_SetSecSkillValuePercent)&x1=8:P6547912/x2/x3;  // Мистицизм
!!FU(Fun_SetSecSkillValuePercent)&x1=9:P6547864/x2/x3;  // Удача
!!FU&x1=10:E;                           // Баллистика - не применимо. Количество выстрелов и урон?
!!FU(Fun_SetSecSkillValuePercent)&x1=11:P6548008/x2/x3; // Орлиный глаз 
!!FU(Fun_SetSecSkillValuePercent)&x1=12:P6547896/x2/x3; // Некромантия 
!!FU(Fun_SetSecSkillValuePercent)&x1=13:P6547992/x2/x3; // Имущество
!!FU&x1=14:E;                           // Магия огня - не применимо
!!FU&x1=15:E;                           // Магия воздуха - не применимо
!!FU&x1=16:E;                           // Магия воды - не применимо
!!FU&x1=17:E;                           // Магия земли - не применимо
!!FU&x1=18:E;                           // Грамотность - не применимо
!!FU&x1=19:E;                           // Тактика - не применимо
!!FU&x1=20:E;                           // Артиллерия - не применимо. Количество выстрелов и шансы двойного выстрела?
!!FU(Fun_SetSecSkillValuePercent)&x1=21:P6548056/x2/x3; // Обучение
!!FU(Fun_SetSecSkillValuePercent)&x1=22:P6547960/x2/x3; // Атака
!!FU(Fun_SetSecSkillValuePercent)&x1=23:P6547976/x2/x3; // Защита
!!FU(Fun_SetSecSkillValuePercent)&x1=24:P6548104/x2/x3; // Интеллект 
!!FU(Fun_SetSecSkillValuePercent)&x1=25:P6548088/x2/x3; // Волшебство
!!FU(Fun_SetSecSkillValuePercent)&x1=26:P6548040/x2/x3; // Сопротивление
!!FU&x1=27:E;                           // Первая помощь - не применимо. Сила лечения?

!?FU(Fun_SetSecSkillValuePercent);      // установка процентного значения вторичного навыка
; x1 - адрес вторичного навыка
; x2 - уровень навыка (0..3)
; x3 - значение %
!!VRy1:Sx2 *4 +x1;                      // адрес требуемого ранга вторичного навыка
!!VRe1:Sx3:100; !!SN:X?y2 Xe1 X?y3 Xy2; // конвертация целой величины в процентную
!!UN:Cy1/4/y3;                          // установка значения

** end