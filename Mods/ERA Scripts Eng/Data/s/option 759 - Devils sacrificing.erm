ZVSE2
** Author orig.  : Algor
** Rewritten by  : Archer30
** Name          : Devils sacrificing
** Name rus.     : Дьявольское пожертвование
** Options       : 759

*** Архидьяволы и Бароны Ада получают способность 1 раз за бой (Бароны Ада - 2 раза)принести в жертву дружественный отряд
*** живых непризванных существ восстановив свое здоровье/численность на сумму здоровья принесенного в жертву отряда.
*** Количество Архидьяволов/Баронов Ада после восстановления не может превышать начальной численности отряда.

*** ИИ при возможности применяет новую способность если отряд Архидьяволов/Баронов Ада потерял не менее 30% численности.
*** При этом выбирается жертвенный отряд, здоровье которого не более чем на 10% превышает максимально необходимое здоровье
*** для восстановления всех Архидьяволов/Баронов Ада и не менее необходимого для восстановления хотя бы одного Архидьявола/Барона.
*** Если таких отрядов у ИИ несколько, выбирается отряд с наименьшим значением FV/восстанавливаемое существо.


// Initialization
!?FU(OnAfterErmInstructions);
!!UN:P759/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!MA:B(MON_ARCH_DEVIL)/d1 B(MON_HELL_BARON)/d2; 
!!SN:H^monname^/(MON_ARCH_DEVIL)/2/?(desc:z);
!!SN:H^monname^/(MON_ARCH_DEVIL)/2/^%(desc)%T(es.759.desc)^;

!!SN:H^monname^/(MON_HELL_BARON)/2/?(desc);
!!SN:H^monname^/(MON_HELL_BARON)/2/^%(desc)%T(es.759.desc)^;

!?FU(OnSetupBattlefield);
; Initialise variable - This must to be executed regardless of the option
!!VRi^es_759_victimStack^:S(NO_STACK);

// Change the cursor and show hint when hovering over victims
!?FU(OnBattleMouseHint)&i^battle_round^>=0;
!!UN:P759/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!VRi^es_759_victimStack^:S(NO_STACK);

; Exit if not hovering one of the hex
!!MM:D?(pos:y);                            [позиция курсора]
!!FU|(pos)<0/(pos)>186:E;                  [выход, если курсор за полем]

!!BU:E(pos)/?(stack:y);                    [номер живого отряда в позиции]
!!FU&(stack)=(NO_STACK):E;

; Exit if not a devil with spell (and not being controlled)
!!FU(ES_759_CheckSacrificeConditions):Pi^battle_current_stack^/?(result:y);
!!FU&(result)<>(TRUE):E;

; Exit if the pointed stack is not from the same side with the devil/is cloned/not living
!!FU(ES_759_CheckIfVictimIsValid):P(stack)/?(result);
!!FU&(result)<>(TRUE):E;

!!VRi^es_759_victimStack^:S(stack);

; Show hint of the sacrificing event
!!BM(stack):T?(type:y) N?(num:y);          [флаги/тип/количество существ отряда под курсором]

!!VRi^es_759_victimStack^:S(stack);        [установка номера жертвенного отряда]
!!UN:R5/2/18;                              [установка вида курсора "Жертва"]

!!if&(num)=1;
  !!SN:H^monname^/(type)/0/?(victimName:z);
!!el;
  !!SN:H^monname^/(type)/1/?(victimName);
!!en;

!!SN:T^es.759.hint^/?(hint:z)/^victim^/(victimName);
!!MM:M(hint);                              [подмена стандартной подсказки]

!?FU(ES_759_CheckSacrificeConditions);
!#VA(stack:x) (result:x);

!!VR(result):S(FALSE);
!!BM(stack):T?(type:y);
!!FU(ES_759_CheckIfMonIsEligible):P(type)/?(isDevil:y);

!!if&(isDevil);
  !!BM(stack):E?(spell:y) N?(num:y) B?(startNum:y) G(SPELL_BERSERK)/?(isBerserked:y)/d G(SPELL_HYPNOTIZE)/?(isHypotized:y)/d;
  !!VR(result)&(spell)>0/(num)<(startNum)/(isBerserked)<>(TRUE)/(isHypotized)<>(TRUE):S(TRUE);
!!en;

!?FU(ES_759_CheckIfVictimIsValid);
!#VA(stack:x) (result:x);

!!VR(result):S(FALSE);

!!if&(stack)>(NO_STACK)/(stack)<>i^battle_current_stack^;
  ; Exit if the side doesn't match
  !!BM(stack):I?(side:y);
  !!FU&(side)<>i^battle_current_side^:E;

  ; Exit if the victim is not a living creature/is summoned
  !!BM(stack):F?(monFlags:y);
  !!VR(isAlive:y):S(monFlags) &(MON_FLAG_ALIVE); [если существа отряда под курсором не живые]
  !!VR(isSummoned:y):S(monFlags) &(MON_FLAG_SUMMONED); [если существа отряда под курсором призванные]
  !!VR(result)&(isAlive)/(isSummoned)<>(TRUE):S(TRUE);
!!en;

// Sacrifce the target and revive the devil when clicking on it
!?FU(OnBattleScreenMouseClick)&i^es_759_victimStack^>(NO_STACK)/i^mouse_action^=(MOUSE_LMB_PRESSED)/i^battle_round^>=0;
!!FU(ES_759_ReviveDevils):Pi^battle_current_stack^/i^es_759_victimStack^;

!?FU(ES_759_ReviveDevils);                 [Восстановление Архидьяволов за счет жертвенного отряда: номер отряда Архидьяволов,Баронов/жертвенного отряда]
!#VA(devilStack:x) (victimStack:x);

; Calculate sacrificed HP (restoring HP for the devils)
!!BM(victimStack):N?(num:y) L?(lostHp:y) H?(hp:y) T?(victimType:y); [текущая численность/потерянное здоровье/макс.здоровье/тип существ в жертвенном отряде]
!!VR(sacrificedHp:y):S(num) *(hp) -(lostHp); ["пожертвованное" здоровье]

; Calculate the new HP/number of the devil
!!BM(devilStack):N?(num) L?(lostHp) H?(hp) B?(startNum:y) T?(devilType:y); [текущая численность/потерянное здоровье/макс.здоровье/начальная численность/тип существ в отряде Архидьяволов/Баронов]
!!VR(newTotalHp:y):S(num) *(hp) -(lostHp) +(sacrificedHp); [максимально возможное новое здоровье отряда Архидьяволов/Баронов]
!!VR(newNum:y):S(newTotalHp) :(hp);        [максимально возможное новое количество Архидьяволов/Баронов в отряде]
!!VR(lastUnitHp:y):S(newTotalHp) %(hp);    [количество здоровья у раненного Архидьявола/Барона]
!!VR(lastUnitHp)&(newNum)>=(startNum):S(hp); [...]
!!VR(newNum)&(lastUnitHp)>0:+1 F1/(startNum); [...]
!!VR(newLostHp:y):S(hp) -(lastUnitHp);     [потерянное здоровье нового отряда Архидьяволов/Баронов]

; Sacrifce the target, manage flags and memory
!!BM(victimStack):Fd(MON_FLAG_SACRIFICED) Fd(MON_FLAG_NO_COLORING) Fd(MON_FLAG_CLONE); [добавление флагов "Принесен в жертву", "Клон", "Не меняет цвет при клонироании" жертвенному отряду]
!!BM(devilStack):Z?(devilStruct:y);
!!BM(victimStack):Z?(victimStruct:y);

; Play sound and lure attack animation
!!UN:C4462626/1/1;                         [анимациия атаки "подманивание"]
!!SN:P^SACRIF1.wav^;                       [воспроизведение звука]
!!BM(victimStack):V51;                     [анимация "Жертва" на жертвенном отряде]
!!SN:E4461360/2/(devilStruct)/(victimStruct)/2; [Атака Архидьяволов/Баронов]

; Show battle log
!!SN:H^monname^/(victimType)/1/?(victimName:z);
!!SN:H^monname^/(devilType)/1/?(devilName:z);
!!SN:T^es.759.battleLog^/?(battleLog:z)/^devil^/(devilName)/^victim^/(victimName);
!!MM:S(battleLog);

!!SN:P^RESURECT.wav^;                      [воспроизведение звука]
!!BM(devilStack):V50;                      [анимация на отряде Архидьяволов]

; Set up new number/hp/spell of the devil
!!BM(devilStack):N(newNum) L(newLostHp) Ed-1; [восстановление Архидьяволов с уменьшением количества заклинаний отряда]

; restore attack animation
!!UN:C4462626/1/13;                        [анимациия атаки по умолчанию]

; Skip this turn
!!BG:A(BATTLE_ACTION_SKIP);                [пропуск хода отрядом]

; Update the next stack turn (in case the next stack is sacrificed and turn order messes up)
; Seems not required? Not sure
!!UN:C(COMBAT_MANAGER)/4/?(cmbMgr:y);
!!SN:E4607072/(CALLCONV_THISCALL)/(cmbMgr)/(TRUE);

** AI

// Revive devil for AI if the living number is lower than 70%
!?FU(OnBeforeBattleAction)&i^battle_isVisible^/i^battle_round^>=0/i^battle_ai_%i(battle_current_side)^;
!!UN:P759/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; Exit if not a devil with spell (and not being controlled)
!!FU(ES_759_CheckSacrificeConditions):Pi^battle_acting_stack^/?(result:y);
!!FU&(result)<>(TRUE):E;

; Exit if the devil is more than 70% of the starting number
!!BMi^battle_acting_stack^:N?(num:y) B?(startNum:y);
!!VR(percent:y):S(num) *100 :(startNum);
!!FU&(percent)>70:E; 

; Get a valid victim depending on the lost hp of the devil
!!BMi^battle_acting_stack^:L?(lostHp:y) H?(hp:y); [текущее количество/начальное количество/потерянное здоровье/макс.здоровье отряда]
!!VR(totalLostHp:y):S(startNum) -(num) *(hp) +(lostHp) *11 :10; [здоровье отряда, которое можно восстановить "жертвой" (+10% допустимый "перебор")]
!!FU(ES_759_GetAiVictim):Pi^battle_acting_stack^/(totalLostHp)/?(victimStack:y);

; Revive the devil if the victim is found
!!FU(ES_759_ReviveDevils)&(victimStack)>(NO_STACK):Pi^battle_acting_stack^/(victimStack);

!?FU(ES_759_GetAiVictim);
!#VA(devilStack:x) (totalLostHp:x) (victimStack:x);

!!VR(victimStack):S(NO_STACK);
!!VR(maxFvHpRatio:y):S(INT_MAX);
!!BM(devilStack):H?(devilHp:y) I?(side:y);

!!VR(firstStack:y):S(side) *(BATTLE_STACKS_PER_SIDE);
!!VR(lastStack:y):S(firstStack) +20;

!!re i/(firstStack)/(lastStack);
  ; Next mon if the target is not alive or summoned
  !!BMi:T?(type:y) N?(num:y);
  !!co|i=(devilStack)/(type)<=(NO_MON)/(num)<0;

  !!BMi:F?(monFlags:y) H?(hp:y) L?(lostHp:y);
  !!VR(isAlive:y):S(monFlags) &(MON_FLAG_ALIVE);
  !!VR(isSummoned:y):S(monFlags) &(MON_FLAG_SUMMONED);
  !!co|(isAlive)=(FALSE)/(isSummoned);

  ; Check the totla Hp of the victim and see if it can revive at least one devil but not over heal it
  !!VR(totalHp:y):S(num) *(hp) -(lostHp);

  !!if&(totalHp)>=(devilHp)/(totalHp)<(totalLostHp);
    ; Calculate Hp/Fv Ratio and decide how to proceed
    !!MA:F(type)/?(fv:y) L(type)/?(lv:y);
    !!VR(fvHpRatio:y):S(fv) :(hp:y);

    ; Set the target if it has lower fv/hp ratip and is weaker than level 7
    !!if&(fvHpRatio)<(maxFvHpRatio)/(lv)<6;  
      !!VR(victimStack):Si;
      !!VR(maxFvHpRatio):S(fvHpRatio);
    !!en;
  !!en;
!!en;

// Funciton to check if the targeted monster is eligible for devil sacrificing
!?FU(ES_759_CheckIfMonIsEligible);
!#VA(mon:x) (result:x);

!!VR(result):S(FALSE);
!!VR(result)|(mon)=(MON_ARCH_DEVIL)/(mon)=(MON_HELL_BARON):S(TRUE);

** end
