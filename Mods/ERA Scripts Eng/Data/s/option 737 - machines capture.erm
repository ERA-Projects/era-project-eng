ZVSE2
** Author orig.  : Algor
** Rewritten by  : Archer30
** Fully Rewritten by  : daemon_n
now it is simple as life 

** Name          : Machines capture
** Options       : 737

** After the battle, the victorious hero receives the surviving enemy vehicles (cart/tent/ballista)
** if he has free space for them. If there is no space, the cars remain with the vanquished.
** In case of capitulation, the vehicles remain with the vanquished.
** Unnecessary machines can be destroyed.

!?FU(ES_CreateERMHook);
!!UN:P737/?(isEnabled:y);
!!VRi^ES_737_enabled^:S(isEnabled); // needed for Hero Screen LMB proc
!!if&(isEnabled);
  !!SN:Ex1/(CALLCONV_CDECL)/4627621/(ES_737_CombatManager_OnLoserHeroEquippedArtIteration); 469CA5
!!en;


!?FU(ES_737_CombatManager_OnLoserHeroEquippedArtIteration);469CA5
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_EAX)/4/?(artId:y); // get art id from stack

!!if&(artId)>=(ART_BALLISTA)/(artId)<=(ART_FIRST_AID_TENT);
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBP)/4/?(ebp:y) C(ebp)/-8/4/?(heroWinner:y);// get hero ptr from stack
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBX)/4/?(artPtr:y); // get art ptr from stack
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_ECX)/4/?(artMod:y); // get art mod from stack
  !!UN:C(ebp)/-28/4/(artId:y) C(ebp)/-24/4/(artMod:y); restore original code
  !!SN:E5124832/(CALLCONV_THISCALL)/(heroWinner)/(artPtr)/(TRUE)/(FALSE); // add art to hero. ask for combo. don't check winconditions
  !!VRv1:&1;
  !!if&v1; // if equipp success
    !!VR(returnAddress:y):S4627685; //return to adding art into "remove/add" list
  !!el;
    !!VR(returnAddress:y):S4627711; //return to next loop it
  !!en;
  !!SN:X?y99/0;// indicate that function return address is custom 
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_RET)/4/(returnAddress);// set new return address
!!en;


!?FU(OnHeroScreenMouseClick)&i^mouse_action^=(MOUSE_LMB_PRESSED)/i^mouse_item^>=15/i^mouse_item^<=17/i^mouse_flags^=(MOUSE_FLAG_CTRL)/999/i^ES_737_enabled^; [клик в окне геро€]
  ; Set up variables
  !!VR(warMachine:y):Si^mouse_item^ -11;

  ; Check if the hero has a war machine at the clicked slot
  !!HEi^mouse_hero_0^:A2/(warMachine)/d/?(hasWarMachine:y);

  ; Ask to destroy the war machine if positive
  !!if&(hasWarMachine);
    !!IF:Q1/(PIC_TYPE_ART)/(warMachine)/(MSG_TYPE_QUESTION)^%T(es.737.heroScr)^;
    !!CM:R0;

    ; Remove the war machine
    !!if&1;

      !!HEi^mouse_hero_0^:A3/(warMachine)/1/1;

      !!if&i^WOG_73_enabled^;                 [WM III comatibility]
        !!IF:Wi^mouse_hero_0^; 
        !!VR(wVar:y):S(warMachine) +77;       [remove all WM]
        !!VRw(wVar):S0;
      !!en;

      !!SN:D;
    !!en;
  !!en;

** end
