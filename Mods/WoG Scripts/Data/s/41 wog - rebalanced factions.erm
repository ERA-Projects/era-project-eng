ZVSE2

** Rebalanced Factions v1.3 by Steven Lynch
** WoGify Name: script37.erm
** Last Edited: September 23, 2004
** Updated by Archer30 on Apr 2022


[MAIN FUNCTION]
!?FU(WOG_Rebalance_Initialization);

!!UN:P37/?i^wog_37_enabled^; [Rebalanced Creatures]
!!UN:P198/?i^wog_198_enabled^; [Rebalanced Hero Abilities]
!!UN:P199/?i^wog_199_enabled^; [Rebalanced Starting Armies]

!!UN:P202/?i^wog_202_enabled^; [Eagle Eye I]
!!UN:P103/?i^wog_103_enabled^; [Eagle Eye II]
!!UN:P216/?i^wog_216_enabled^; [Resistance I]
!!UN:P210/?i^wog_210_enabled^; [Resistance II]
!!UN:P203/?i^wog_203_enabled^; [Estates I]
!!UN:P191/?i^wog_191_enabled^; [Estates II]

!!UN:P39/?i^wog_39_enabled^; [Hero Specialization Boost]
!!UN:P50/?i^wog_50_enabled^; [Enhanced Monsters]

!!UN:P67/?i^wog_67_enabled^; [Neutral Town]
!!UN:P205/?i^wog_205_enabled^; [Learning II]

!!if&i^wog_199_enabled^;                [Rebalanced Starting Armies]
  !!FU(WOG_CheckRandomMap):P?(isRandom:y);
  !!FU(WOG_199_RebalancStartArmies1)&(isRandom):P; [HE:C]
  !!FU(WOG_199_RebalancStartArmies2):P; [HE:H]
!!en;

!!FU(WOG_198_RebalanceHeroes)&i^wog_198_enabled^:P;

!?FU(OnAfterErmInited);
!!FU(WOG_37_RebalanceMonsters)&i^wog_37_enabled^:P;

!?FU(OnHeroScreenMouseClick)&i^wog_198_enabled^=1/i^mouse_item^=45/i^mouse_action^=(MOUSE_RMB_PRESSED);
!!CM:R0;

!!HE(CURRENT_HERO):N?(hero:y);
!!VRz-1:Sz137000;
!!VRz-1&(hero)=3:Sz137001;
!!VRz-1&(hero)=4/i^wog_203_enabled^<>1/i^wog_191_enabled^<>1:Sz137002;
!!VRz-1&(hero)=5/i^wog_39_enabled^<>1:Sz137003;
!!VRz-1&(hero)=5/i^wog_39_enabled^=1:Sz137004;
!!VRz-1&(hero)=6:Sz137005;
!!VRz-1&(hero)=6/i^wog_39_enabled^<>1:+z137006;
!!VRz-1&(hero)=7/i^wog_39_enabled^<>1:Sz137007;
!!VRz-1&(hero)=7/i^wog_39_enabled^=1:Sz137008;
!!VRz-1&(hero)=8:Sz137009;
!!VRz-1&(hero)=9:Sz137010;
!!VRz-1&(hero)=10:Sz137011;
!!VRz-1&(hero)=10/i^wog_39_enabled^<>1:+z137012;
!!VRz-1&(hero)=11:Sz137013;
!!VRz-1&(hero)=11/i^wog_39_enabled^<>1:+z137014;
!!VRz-1&(hero)=12/i^wog_39_enabled^<>1:Sz137015;
!!VRz-1&(hero)=12/i^wog_39_enabled^=1:Sz137016;
!!VRz-1&(hero)=13/i^wog_202_enabled^<>1/i^wog_103_enabled^<>1/i^wog_39_enabled^<>1:Sz137017;
!!VRz-1&(hero)=14:Sz137018;
!!VRz-1&(hero)=17/i^wog_216_enabled^<>1/i^wog_210_enabled^<>1/i^wog_39_enabled^<>1:Sz137019;
!!VRz-1&(hero)=17/i^wog_216_enabled^<>1/i^wog_210_enabled^<>1/i^wog_39_enabled^=1:Sz137020;
!!VRz-1&(hero)=19:Sz137021;
!!VRz-1&(hero)=20/i^wog_216_enabled^<>1/i^wog_210_enabled^<>1:Sz137022;
!!VRz-1&(hero)=22/i^wog_216_enabled^<>1/i^wog_210_enabled^<>1:Sz137023;
!!VRz-1&(hero)=23:Sz137024;
!!VRz-1&(hero)=24:Sz137025;
!!VRz-1&(hero)=25:Sz137026;
!!VRz-1&(hero)=27:Sz137027;
!!VRz-1&(hero)=28/i^wog_202_enabled^<>1/i^wog_103_enabled^<>1:Sz137028;
!!VRz-1&(hero)=29:Sz137029;
!!VRz-1&(hero)=30:Sz137030;
!!VRz-1&(hero)=31/i^wog_39_enabled^<>1:Sz137031;
!!VRz-1&(hero)=32:Sz137032;
!!VRz-1&(hero)=33/i^wog_39_enabled^<>1:Sz137033;
!!VRz-1&(hero)=34/i^wog_39_enabled^<>1:Sz137034;
!!VRz-1&(hero)=34/i^wog_39_enabled^=1:Sz137035;
!!VRz-1&(hero)=36:Sz137036;
!!VRz-1&(hero)=37/i^wog_39_enabled^<>1:Sz137037;
!!VRz-1&(hero)=37/i^wog_39_enabled^=1:Sz137038;
!!VRz-1&(hero)=39/i^wog_39_enabled^<>1:Sz137039;
!!VRz-1&(hero)=39/i^wog_39_enabled^=1:Sz137040;
!!VRz-1&(hero)=40:Sz137041;
!!VRz-1&(hero)=45:Sz137042;
!!VRz-1&(hero)=46:Sz137043;
!!VRz-1&(hero)=49:Sz137044;
!!VRz-1&(hero)=54:Sz137045;
!!VRz-1&(hero)=57:Sz137046;
!!VRz-1&(hero)=59:Sz137047;
!!VRz-1&(hero)=63/i^wog_39_enabled^<>1:Sz137048;
!!VRz-1&(hero)=64/i^wog_39_enabled^<>1:Sz137049;
!!VRz-1&(hero)=64/i^wog_39_enabled^=1:Sz137116;
!!VRz-1&(hero)=65/i^wog_39_enabled^=1:Sz137050;
!!VRz-1&(hero)=65/i^wog_39_enabled^<>1:Sz137051;
!!VRz-1&(hero)=67/i^wog_39_enabled^<>1:Sz137052;
!!VRz-1&(hero)=67/i^wog_39_enabled^=1:Sz137053;
!!VRz-1&(hero)=68/i^wog_39_enabled^<>1:Sz137054;
!!VRz-1&(hero)=68/i^wog_39_enabled^=1:Sz137055;
!!VRz-1&(hero)=72/i^wog_39_enabled^<>1:Sz137056;
!!VRz-1&(hero)=72/i^wog_39_enabled^=1:Sz137057;
!!VRz-1&(hero)=73:Sz137058;
!!VRz-1&(hero)=76:Sz137059;
!!VRz-1&(hero)=77/i^wog_39_enabled^<>1:Sz137061;
!!VRz-1&(hero)=77/i^wog_39_enabled^=1:Sz137062;
!!VRz-1&(hero)=81:Sz137060;
!!VRz-1&(hero)=85:Sz137063;
!!VRz-1&(hero)=86/i^wog_39_enabled^<>1:Sz137064;
!!VRz-1&(hero)=86/i^wog_39_enabled^=1:Sz137065;
!!VRz-1&(hero)=88:Sz137066;
!!VRz-1&(hero)=91:Sz137067;
!!VRz-1&(hero)=93:Sz137068;
!!VRz-1&(hero)=95/i^wog_39_enabled^<>1:Sz137069;
!!VRz-1&(hero)=96/i^wog_39_enabled^<>1:Sz137070;
!!VRz-1&(hero)=97:Sz137071;
!!VRz-1&(hero)=99/i^wog_39_enabled^<>1:Sz137072;
!!VRz-1&(hero)=99/i^wog_39_enabled^=1:Sz137073;
!!VRz-1&(hero)=101/i^wog_39_enabled^<>1:Sz137074;
!!VRz-1&(hero)=105/i^wog_39_enabled^<>1:Sz137075;
!!VRz-1&(hero)=106:Sz137076;
!!VRz-1&(hero)=108/i^wog_39_enabled^<>1:Sz137077;
!!VRz-1&(hero)=118:Sz137078;
!!VRz-1&(hero)=119/i^wog_39_enabled^<>1:Sz137079;
!!VRz-1&(hero)=119/i^wog_39_enabled^=1:Sz137080;
!!VRz-1&(hero)=120/i^wog_39_enabled^<>1:Sz137081;
!!VRz-1&(hero)=121/i^wog_39_enabled^=1:Sz137082;
!!VRz-1&(hero)=121/i^wog_39_enabled^<>1:Sz137083;
!!VRz-1&(hero)=122:Sz137084;
!!VRz-1&(hero)=123:Sz137085;
!!VRz-1&(hero)=124/i^wog_39_enabled^<>1:Sz137086;
!!VRz-1&(hero)=124/i^wog_39_enabled^=1:Sz137087;.
!!VRz-1&(hero)=128:Sz137088;
!!VRz-1&(hero)=129/i^wog_203_enabled^<>1/i^wog_191_enabled^<>1/i^wog_67_enabled^<>1:Sz137089;
!!VRz-1&(hero)=130:Sz137090;
!!VRz-1&(hero)=131/i^wog_67_enabled^=1:Sz137118;
!!VRz-1&(hero)=132/i^wog_39_enabled^<>1:Sz137091;
!!VRz-1&(hero)=132/i^wog_39_enabled^=1:Sz137092;
!!VRz-1&(hero)=133/i^wog_203_enabled^<>1/i^wog_191_enabled^<>1/i^wog_67_enabled^<>1:Sz137093;

!!if&i^wog_39_enabled^<>1/i^wog_67_enabled^<>1;
  !!VRz-1&(hero)=133:Sz137094;
  !!VRz-1&(hero)=134:Sz137095;
  !!VRz-1&(hero)=135/i^wog_205_enabled^<>1:Sz137096;
  !!VRz-1&(hero)=135/i^wog_205_enabled^=1:Sz137097;
!!en;

!!VRz-1&(hero)>=136/(hero)<=143:Sz137098;
!!VRz-1&(hero)=136:+z137099;
!!VRz-1&(hero)=139:+z137100;
!!VRz-1&(hero)=140/i^wog_39_enabled^<>1:+z137101;
!!VRz-1&(hero)=143:+z137102;
!!VRz-1&(hero)=144:Sz137103;
!!IF:Q1/-1/-1/4^%Z-1^;

[CREATURE TWEAKS]
!?FU(WOG_37_RebalanceMonsters);

!!if&i^wog_67_enabled^<>1;
  !!MA:D(MON_HALFLING)/d2;              [Halfling Defense increased by 2]
  !!MA:N(MON_HALFLING)/d-10;            [Halfling Shots decreased by 10]
  !!MA:S(MON_HALFLING)/d1;              [Halfling Speed increased by 1]
  !!MA:E(MON_HALFLING)/d-1;             [Halfling Maximum Damage decreased by 1]

  ; New upgrade chain must be executed later than scripts give additional upgrade to Master Gremlins
  !!MA:U(MON_MASTER_GREMLIN)/(MON_HALFLING); [Master Gremlins/Halflings Switch]
  !!MA:U(MON_HALFLING)/(MON_MASTER_GREMLIN);
!!en;

*** Inferno ***
!!if&i^wog_50_enabled^<>1;
  !!MA:G(MON_IMP)/d5;                   [Imp Growth increased by 5]
  !!MA:G(MON_FAMILIAR)/d5;              [Familiar Growth increased by 5]
  !!MA:D(MON_GOG)/d1;                   [Gog Defense increased by 1]
  !!MA:P(MON_GOG)/d2;                   [Gog HitPoints increased by 2]
  !!MA:C(MON_GOG)/(RES_GOLD)/d10;       [Gog Cost increased by 10 gold]
  !!MA:D(MON_MAGOG)/d1;                 [Magog Defense increased by 1]
  !!MA:P(MON_MAGOG)/d2;                 [Magog Health increased by 2]
  !!MA:E(MON_MAGOG)/d1;                 [Magog Maximum Damage increased by 1]

  !!MA:X(MON_MAGOG)/d|(MON_FLAG_ATTACKS_TWICE);
  !!SN:H^monname^/(MON_MAGOG)/2/?(desc:z) H^monname^/(MON_MAGOG)/2/^%(desc)%T(wog.37.monDesc)^;
!!en;

!!MA:G(MON_HELL_HOUND)/d1;              [Hell Hound Growth increased by 1]
!!MA:G(MON_CERBERUS)/d1;                [Cerberus Growth increased by 1]

!!if&i^wog_50_enabled^<>1;
  !!MA:P(MON_HORNED_DEMON)/d5;          [Horned Demon HitPoints increased by 5]
  !!MA:E(MON_HORNED_DEMON)/d1;          [Horned Demon Maximum Damage increased by 1]
  !!MA:C(MON_HORNED_DEMON)/(RES_GOLD)/d20; [Horned Demon Cost increased by 20 gold]
  !!MA:C(MON_PIT_LORD)/(RES_GOLD)/d-25; [Pit Lord Cost decreased by 25 gold]
!!en;

!!MA:D(MON_EFREET_SULTAN)/d1;           [Efreet Sultan Defense increased by 1]
!!MA:P(MON_EFREET_SULTAN)/d10;          [Efreet Sultan HitPoints increased by 10]
!!MA:C(MON_EFREET_SULTAN)/(RES_GOLD)/d50;[Efreet Sultan Cost increased by 50 gold]
!!MA&i^wog_50_enabled^<>1:C(MON_ARCH_DEVIL)/(RES_GOLD)/d-600;[Arch Devil Gold Cost decreased by 600]

*** Fortress ***
!!MA:X(MON_LIZARD_WARRIOR)/d|(MON_FLAG_ATTACKS_TWICE); [Lizard Warrior gets Double Shot]
!!MA:A(MON_LIZARD_WARRIOR)/d-1;         [Lizard Warrior Attack decreased by 1]
!!MA:D(MON_LIZARD_WARRIOR)/d-2;         [Lizard Warrior Defense decreased by 2]
!!MA:E(MON_LIZARD_WARRIOR)/d-2;         [Lizard Warrior Maximum Damage decreased by 2]
!!MA:C(MON_LIZARD_WARRIOR)/(RES_GOLD)/d20; [Lizard Warrior Cost increased by 20 gold]
!!SN:H^monname^/(MON_LIZARD_WARRIOR)/2/?(desc:z) H^monname^/(MON_LIZARD_WARRIOR)/2/^%(desc)%T(wog.37.monDesc)^;

!!if&i^wog_50_enabled^<>1;
  !!MA:A(MON_WYVERN_MONARCH)/d2;        [Wyvern Monarch Attack increased by 2]
  !!MA:D(MON_WYVERN_MONARCH)/d1;        [Wyvern Monarch Defense increased by 1]
  !!MA:A(MON_CHAOS_HYDRA)/d2;           [Chaos Hydra Attack increased by 2]
  !!MA:P(MON_CHAOS_HYDRA)/d50;          [Chaos Hydra HitPoints increased by 50]
  !!MA:S(MON_CHAOS_HYDRA)/d2;           [Chaos Hydra Speed increased by 2]
  !!MA:C(MON_CHAOS_HYDRA)/(RES_GOLD)/d500; [Chaos Hydra Cost increased by 500 gold]
!!en;

*** Conflux ***
!!UN:P189/?(reducedFirebirdGrowth:y);                       [Reduce Conflux Firebird/Phoenix Growth]
!!MA&i^wog_50_enabled^<>1/(reducedFirebirdGrowth)<>(TRUE):P(MON_PHOENIX)/d-50; [Phoenix HitPoints decreased by 50]

[Wyvern Monarch Timed HP and Cost Increase]
!?FU(OnEveryDay)&i^wog_37_enabled^=1/i^wog_50_enabled^<>1/i^timerOnce^;
; One-time boost to Wyvern Monarch on day 15 and 22
!!if|i^timerDay^=15/i^timerDay^=22;
  !!MA:C(MON_WYVERN_MONARCH)/(RES_GOLD)/d25; [Wyvern Monarch Cost increased by 25 gold on Day 15 and 22]
  !!MA:P(MON_WYVERN_MONARCH)/d20;       [Wyvern Monarch HitPoints increased by 20 on Day 15 and 22]
!!en;

// Set up upgrades for neutral dragons
!?FU(OnAfterErmInstructions)&i^wog_37_enabled^/i^wog_67_enabled^<>(TRUE);
!!UN:P188/?(rampartFarie:y);            [Rampart Faerie Dragon]

!!if&(rampartFarie)<>(TRUE);
  !!MA:U(MON_CRYSTAL_DRAGON)/(MON_FAERIE_DRAGON);
  !!MA:U(MON_FAERIE_DRAGON)/(MON_RUST_DRAGON);
  !!MA:U(MON_RUST_DRAGON)/(MON_CRYSTAL_DRAGON);
!!en;

[Hill Fort]
!?OB(OBJ_HILL_FORT)&i^wog_37_enabled^/i^wog_67_enabled^<>(TRUE);
!!UN:P188/?(rampartFarie:y);            [Rampart Faerie Dragon]

!!if&(rampartFarie)<>(TRUE);
  !!MA:L(MON_CRYSTAL_DRAGON)/0;
  !!MA:L(MON_FAERIE_DRAGON)/0;
  !!MA:L(MON_RUST_DRAGON)/0;
!!en;

[Restore Dragon Levels at Hill Fort]
!$OB(OBJ_HILL_FORT)&i^wog_37_enabled^/i^wog_67_enabled^<>(TRUE);
!!UN:P188/?(rampartFarie:y);            [Rampart Faerie Dragon]

!!if&(rampartFarie)<>(TRUE);
  !!MA:L(MON_CRYSTAL_DRAGON)/6;
  !!MA:L(MON_FAERIE_DRAGON)/6;
  !!MA:L(MON_RUST_DRAGON)/6;
!!en;

[HERO TWEAKING]
!?FU(WOG_198_RebalanceHeroes);

!!HE3:S5/3; [Sylvia now starts with Expert Navigation]
!!HE4&i^wog_210_enabled^<>1/i^wog_203_enabled^<>1:S13/2; [Lord Haart's Estates skill upgraded to Advanced if no Estates scripts are on]
!!HE6:F3/1/1/1; [Christian starts with +1AS and -1DS]
!!HE6:A1/4/13; [Christian always starts with a Ballista]
!!HE8:A1/6/15; [Rion always starts with a First Aid Tent]
!!HE9:S4/0; [Adela's Diplomacy is replaced by Leadership]
!!HE9:S6/1;
!!HE10:S16/1; [Cuthbert starts with Basic Water Magic instead of  ->]
!!HE10:S13/0; [ -> Basic Estates]
!!HE11:S7/0; [Adelaide gets Adv. Water Magic instead of Adv. ->]
!!HE11:S16/2; [ -> Wisdom, so maybe Frost Ring gets used sometimes =)]

!!if&i^wog_39_enabled^<>1;
  !!HE11:M2/1; [Adelaide also starts with Visions and Cure]
  !!HE11:M37/1;
  !!HE13&i^wog_203_enabled^<>1/i^wog_191_enabled^<>1:S11/3; [Sanya starts with Expert Eagle Eye when no Eagle Eye scripts are active]
!!en;

!!HE14:F0/0/2/2; [Loynis starts with one less primary skill point]
!!HE17&i^wog_216_enabled^<>1/i^wog_210_enabled^<>1:S26/2; [Ufretin starts with Adv. Resistance]
!!HE19:S4/0; [Ryland's Diplomacy is replaced by an extra level of ->]
!!HE19:S6/2; [-> Leadership]
!!HE20&i^wog_216_enabled^<>1/i^wog_210_enabled^<>1:S26/3; [Thorgrim starts with Expert Resistance when no Resistance scripts are active]
!!HE22&i^wog_39_enabled^<>1:X4/24/3/2/1; [Clancy now gives +3AS, +2DS, +1Dmg. for Unicorns when Hero Specialization Boost is off]
!!HE22&i^wog_216_enabled^<>1/i^wog_210_enabled^<>1:S26/2; [Clancy gets an extra level of Resistance when no Resistance scripts are active]
!!HE23:F1/1/1/1; [Kyrre's starts with less primary skill points because Logistics is such a powerful specialty]
!!HE25:S10/0; [Uland gets Adv Water and Basic Wisdom instead of]
!!HE25:S16/2; [-> Advanced Wisdom and Basic Ballistics]
!!HE25:S7/1;
!!HE27:A1/6/15; [Gem always starts with a First Aid Tent]
!!HE27:M0/0; [Gem no longer has Summon Boat]
!!HE27:M41/1; [Gem now starts with Bless]
!!HE28&i^wog_202_enabled^<>1/i^wog_103_enabled^<>1:M11/3; [Malcom starts with Expert Eagle Eye when no Eagle Eye scripts are active]
!!HE29:S9/0; [Melodia now starts with Basic Wisdom and Air Magic ->]
!!HE29:S15/1; [-> instead of Basic Wisdom and Basic Luck]
!!HE29:M5/1; [Melodia starts with View Air and Visions as well as Fortune]

!!if&i^wog_39_enabled^<>1;
  !!HE30:S16/1; [Alagar now also has Basic Water Magic (see his bio)]
  !!HE30:M2/1; [Alagar also starts with Visions ->
  !!HE30:M37/1; [-> and Cure, because he trained with Vori too =)]
!!en;

!!HE36:A1/4/13; [Torosar always starts with a Ballista]
!!HE36:S20/1; [Torosar now starts with Basic Artillery instead of ->]
!!HE36:S8/0; [-> Basic Mysticism]
!!HE37&i^wog_39_enabled^<>1:X4/38/3/1/1; [Fafner now gives +3AS, +1DS and +1Dmg. to Nagas/N. Queens]
!!HE40:S7/1; [Astral now has Basic Wisdom and Air Magic instead of ->]
!!HE40:S15/1; [ -> Advanced Wisdom]
!!HE40:M17/1; [Astral now has Lightning Bolt because he deserves better =)]
!!HE45:F0/0/1/3; [Solmyr starts with one less primary skill point]
!!HE45:S25/0; [Solmyr starts with Advanced Wisdom instead of ->]
!!HE45:S7/2; [-> Basic Wisdom and Basic Sorcery]
!!HE46:S4/0; [Cyra's Diplomacy is replaced by Leadership]
!!HE46:S6/1;
!!HE49:S18/0; [Rashka now starts with Basic Wisdom and Offense ->]
!!HE49:S22/1; [-> more in keeping with his bio]
!!HE49&i^wog_39_enabled^<>1:X4/52/3/3/0; [Rashka now adds +3 AS & +3 DS to Efreeti/Efreet Sultans]
!!HE54:S2/0; [Pyre now starts with Advanced Artillery instead of Basic ->]
!!HE54:S20/2; [-> Logistics and Basic Artillery]
!!HE54:A1/4/13; [Pyre starts with a Ballista, and it is equipped]
!!HE55&i^wog_39_enabled^<>1:X4/50/2/3/1; [Nymus adds +2AS, +3DS and +1Dmg. to Pit Fiends/Lords]
!!HE57:S18/0; [Xydar now starts with Basic Wisdom and Basic Fire Magic ->]
!!HE57:S14/1; [-> since he delights with everything to do with fire and lava]
!!HE57:F1/1/1/2; [Xyron - +1K & -1SP to have enough to cast his spell]
!!HE59:S16/1; [Olema now starts with Basic Water Magic and Basic Leadership]
!!HE59:S10/0;
!!HE59:S7/0;
!!HE59:S6/1;

!!if&i^wog_39_enabled^<>1;
  !!HE63:S7/2; [Xarfax now starts with Adv Wisdom and Leadership since ->]
  !!HE63:S6/2; [-> he has such a weak specialty]
  !!HE63:M15/1; [Xarfax also starts with Magic Arrow and Visions]
  !!HE63:M2/1;
!!en;

!!HE63:F1/1/1/2; [Xarfax starts with more knowledge and less spell power]
!!HE65&i^wog_39_enabled^<>1:X4/62/2/2/0; [Vokial now gives +2 AS and +2 DS to vampires/v. lords]
!!HE65:A1/4/13; [Vokial starts with a Ballista, and it is equipped]
!!HE65:F2/1/2/1; [Vokial starts with +1AS and -1DS]

!!if&i^wog_39_enabled^<>1;
  !!HE67:X4/60/3/3/3; [Charna gives +3 AS, +3 DS, +3 Dmg. to Wights]
  !!HE68:X4/66/3/3/0; [Tamika's Black/Dread Knights get +3 AS and +3 DS]
  !!HE72:S17/1; [Septienna now starts with Necromancy & Earth Magic ->]
  !!HE72:S18/0; [-> instead of Basic Necromancy and Basic Scholar]
!!el;
  !!HE(HERO_SEPTIENNA):M(SPELL_DISRUPTING_RAY)/1; // give spell according to description
!!en;

!!HE72:M2/1; [Septienna now starts with Visions]
!!HE72:M12/1; [Septienna now starts with Force Field]
!!HE73:F0/0/1/2; [Aislinn starts with two less primary skill points]
!!HE76:M39/0; [Thant no longer starts with Animate Dead]
!!HE76:M46/1; [Thant now starts with Stone Skin]
!!HE77:F0/1/2/2; [Xsi starts with 1 Defense Skill instead of 1 Attack Skill because of her bio]
!!HE81:A1/4/13; [Arlach always starts with a Ballista]
!!HE85:F1/1/1/1; [Gunnar starts with two less primary skill points]
!!HE86&i^wog_39_enabled^<>1:X4/80/1/3/1; [Synca now gives +1AS, +3DS, and +1 Dmg. to Manticores]
!!HE88:F0/0/1/2; [Alamar now starts with two less primary skill points]
!!HE88:S18/0; [Alamar has one more level of Wisdom instead of ->]
!!HE88:S7/2; [-> Basic Scholar and Basic Wisdom]
!!HE91:F0/0/2/2; [Jeddite now starts with one less primary skill point]
!!HE91:M62/1; [Jeddite now starts with Blind instead of ->
!!HE91:M38/0; [-> Resurrection]
!!HE93:F0/0/1/2; [Deemer now starts with two less primary skill points]
!!HE93:S3/1; [Deemer has Advanced Scouting replaced by Basic Scouting ->]
!!HE93:S7/1; [-> and Basic Wisdom]

!!if&i^wog_39_enabled^<>1;
  !!HE95:S17/1; [Darkstorn starts with an extra SS since his specialty is so weak]
  !!HE(HERO_YOG):X6/(MON_OGRE_MAGE)/0/0/0/(MON_CYCLOPS)/(MON_CYCLOPS_KING); [Yog can now upgrade Ogre Magi and Cyclops to Cyclops Kings]
!!en;

!!HE97:S20/0; [Gurnisson now starts with Basic Offense and ->]
!!HE97:S1/1; [-> Basic Archery]

!!if&i^wog_39_enabled^<>1;
  !!HE99:X4/92/1/3/1; [Shiva now gives +1AS, +3DS, +1 Dmg. to Rocs/Thunderbirds]
  !!HE105:S7/2; [Vey now starts with Advanced Wisdom and Advanced ->]
  !!HE105:S6/2; [-> Leadership]
!!en;

!!HE106:F1/1/1/1; [Dessa now gets one less primary skill point]
!!HE108&i^wog_39_enabled^<>1:A1/4/13; [Zubin starts with a Ballista when HSB isn't active]
!!HE118:A1/4/13; [Gerwulf starts with a Ballista, and it is equipped]

!!if&i^wog_39_enabled^<>1;
  !!HE118:F3/1/1/1; [Gerwulf now starts with some Attack Skill]
  !!HE119:X4/108/0/3/1; [Broghild now gives +3 DS & +1 Dmg. to Wyverns/W. Monarchs]
  !!HE120:S7/0; [Mirlanda now starts with ->]
  !!HE120:S16/1; [Basic Water Magic and Basic Fire Magic]
  !!HE120:S14/1; ["She has learned to walk a fine line between good and evil"]
  !!HE120:M62/1; [Mirlanda now starts with Blind]
!!en;

!!HE121:M41/1; [Rosic now starts with Bless]
!!HE122:S5/3; [Voy now starts with Expert Navigation]
!!HE123:A1/6/15; [Verdish always starts with a First Aid Tent]
!!HE124&i^wog_39_enabled^<>1:S17/1; [Merist starts with an extra skill because of weak specialty]
!!HE128:A1/4/13; [Pasis starts with a Ballista, and it is equipped]

!!if&i^wog_203_enabled^<>1/i^wog_191_enabled^<>1/i^wog_67_enabled^<>1;
  !!HE129:S13/0; [When no Estates scripts are on, Thunar has Basic Estates replaced ->]
  !!HE129:S19/2; [-> by an extra level of Tactics]
!!en;

!!HE130:A1/4/13; [Ignissa starts with a Ballista, and it is equipped]
!!HE131&i^wog_67_enabled^=1:F1/0/1/1; [Lacus starts with three primary skill points when Neutral Town and Rebalanced Hero Abilities is on because Advanced Tactics and +2 speed specialty is a super powerful starting combination =)]
!!HE132:S2/0; [Monere now starts with ->

!!HE132&i^wog_39_enabled^<>1:S24/1; [-> Basic Intelligence when HSB is inactive]
!!HE132&i^wog_39_enabled^=1:S22/2; [-> or Advanced Offense when HSB is active]

!!if&i^wog_203_enabled^<>1/i^wog_191_enabled^<>1/i^wog_67_enabled^<>1;
  !!HE133:S13/0; [Erdamon has Basic Estates replaced by ->]
  !!HE133:S23/1; [ -> Basic Armorer when no Estates script is on]
!!en;

!!HE136:F0/0/2/3; [Elementalists down to 2SP]
!!HE137:F0/0/2/3;
!!HE138:F0/0/2/3;
!!HE139:F0/0/2/3;
!!HE140:F0/0/2/3;
!!HE141:F0/0/2/3;
!!HE142:F0/0/2/3;
!!HE143:F0/0/2/3;
!!HE136&i^wog_39_enabled^<>1:M2/1; [Luna starts with Visions]
!!HE139:S7/0; [Labetha starts with just Basic Earth Magic]

!!if&i^wog_39_enabled^<>1;
  !!HE140:S14/2; [Inteus starts with Advanced Fire Magic and Visions]
  !!HE140:M2/1;
!!en;

!!HE143:S7/0; [Grindan starts with just Basic Earth Magic]
!!HE144:S6/1; [Sir Mullich to start with Basic Leadership since he has one of the most powerful specialties]
!!HE144:F1/1/1/1; [Sir Mullich to start with two less primary skill points]

[HERO TWEAKING: BLESSINGS]
!!HE3:Y21/300/999/2;           [Sylvia 300 gold/day for Exp. Nav. slot]
!!HE3:Y65/200/999/2;           [Sylvia 200 movement/day]

!!if&i^wog_39_enabled^<>1;
  !!HE5:Y21/200/999/2;   [Sorsha 200 gold/day when HSB isn't active]
  !!HE6:Y21/150/999/2;   [Christian 150 gold/day when HSB isn't active]
  !!HE7:Y21/250/999/2;   [Tyris 250 gold/day when HSB isn't active]
  !!HE10:Y7/3/999/2;     [Cuthbert 3 extra mana/day when HSB isn't active]
  !!HE10:Y21/150/999/2;  [Cuthbert 150 gold/day when HSB isn't active]
  !!HE11:Y7/3/999/2;     [Adelaide 3 extra mana/day when HSB isn't active]
  !!HE12:Y21/250/999/2;  [Ingham 250 gold/day when HSB isn't active]
  !!HE13:Y7/3/999/2;     [Sanya 3 extra mana/day when HSB isn't active];
  !!HE19:Y21/200/999/2;  [Ryland 200 gold/day when HSB isn't active]
!!en;

!!HE24:Y9/200/999/2;            [Coronius 200 experience/day]

!!if&i^wog_39_enabled^<>1;
  !!HE29:Y7/3/999/2;     [Melodia 3 extra mana/day when HSB isn't active]
  !!HE29:Y65/200/999/2;  [Melodia 200 gold/day when HSB isn't active]
  !!HE31:Y21/200/999/2;  [Aeris 200 gold/day when HSB isn't active]
  !!HE33:Y7/4/999/2;     [Thane 4 mana/day when HSB isn't active]
  !!HE39:Y21/100/999/2;  [Iona 100 gold/day when HSB isn't active]
  !!HE43:Y7/3/999/2;     [Daramyth 3 extra mana/day when HSB isn't active]
  !!HE59:Y7/3/999/2;     [Olema 3 extra mana/day when HSB isn't active]
  !!HE63:Y21/150/999/2;  [Xarfax 150 gold/day when HSB isn't active]
  !!HE101:Y21/200/999/2; [Krellion 200 gold/day when HSB isn't active]
  !!HE121:Y7/4/999/2;    [Rosic extra 4 mana/day when HSB isn't active]
!!en;

!!HE122:Y21/300/999/2;          [Voy 300 gold bonus/day for Exp. Nav. slot]
!!HE122:Y65/200/999/2;          [Voy 200 movement/day]

!!if&i^wog_39_enabled^<>1/i^wog_67_enabled^<>1;
  !!HE129:Y21/150/999/2; [Thunar 150 gold/day when HSB isn't active]
  !!HE133:Y21/150/999/2; [Erdamon 150 gold/day when HSB isn't active]

  !!HE135&i^wog_205_enabled^<>1:Y9/225/999/2; [Due to biography, Kalt receives an experience/day blessing when HSB isn't active]
  !!HE135&i^wog_205_enabled^=1:Y9/150/999/2;
!!en;

[HERO TWEAKING: NEW SPECIALTIES INCLUDING UPGRADERS OF CREATURES]
!!if&i^wog_39_enabled^<>1;
  !!HE17:X6/16/16/17; [Ufretin Dwarf Upgrade if HSB is inactive]
  !!HE(HERO_JOSEPHINE):X6/(MON_OBSIDIAN_GARGOYLE)/(MON_STONE_GOLEM)/(MON_IRON_GOLEM); [Josephine Gargoyle/Golem Upgrade if HSB is inactive]
  *!HE44:X6/34/34/35; [Theodorus New Specialty - Mage Upgrade]
  !!HE64:X6/58/58/59; [Straker W.Dead Upgrade if HSB is inactive]
!!en;

!!HE76:X6/56/56/57; [Thant Skeleton Upgrade to Replace Animate Dead]

!!if&i^wog_39_enabled^<>1;
  !!HE77:X6/64/64/65; [Xsi New Specialty - Lich Upgrade if HSB is inactive]
  !!HE108:X6/88/88/89; [Zubin New Specialty - Orc Upgrade]

  !!if&i^wog_67_enabled^<>1;
    !!HE132:X0/24; [Monere Intelligence Specialty if HSB is inactive]
    !!HE133:X6/113/113/125; [Erdamon Earth to Magma Elemental Upgrade if HSB is inactive]
    !!HE134:X0/22; [Fiur Offense Specialty if HSB is inactive]
    !!HE135:X2/100; [Filler for Special Icon - Kalt New Specialty is +225 experience per day - see above]
  !!en;
!!en;

[CHANGING BIOGRAPHIES AND SPECIALTY TEXT]
!!SN:H^spec^/(HERO_THANT)/2/z137104;

!!if&i^wog_39_enabled^<>(TRUE);
  !!SN&i^wog_216_enabled^<>1/i^wog_210_enabled^<>1:H^spec^/(HERO_UFRETIN)/2/z137019;
  !!SN:H^spec^/(HERO_JOSEPHINE)/2/z137034;

  !!SN:H^spec^/(HERO_CHARNA)/2/z137110;

  !!SN:H^spec^/(HERO_YOG)/2/z137111;

  !!SN:H^spec^/(HERO_SHIVA)/2/z137113;

  !!SN:H^spec^/(HERO_STRAKER)/2/z137117;
  !!SN:H^spec^/(HERO_ZUBIN)/2/z137077;
  !!SN:H^spec^/(HERO_XSI)/2/z137109;

  !!HE(HERO_INTEUS):B1/z137114;

  !!if&i^wog_67_enabled^<>(TRUE);
    !!SN:H^spec^/(HERO_FIUR)/2/z137105;

    !!SN:H^spec^/(HERO_ERDAMON)/2/z137106;

    !!SN:H^spec^/(HERO_MONERE)/2/z137107;
    !!SN:H^spec^/(HERO_KALT)/2/z137115;
  !!en;
!!en;

!!if&i^wog_203_enabled^<>1/i^wog_191_enabled^<>1/i^wog_67_enabled^<>1;
  !!HE(HERO_THUNAR):B1/z137108;
!!en;

[REBALANCED START CREATURES - STARTING HEROES]
!?FU(WOG_199_RebalancStartArmies1); [Note - this part of the script rebalances starting troops and makes sure that heroes starting on the map start with creatures no slower than speed 4]

!!HE1:C0/1/2/5; [Valeska starts with Archers and Marksman]
!!HE1:C0/2/3/1;
!!HE16:C1/16/14/5; [All Rangers and Druids get dwarves replaced by ->]
!!HE18:C1/16/14/5; [-> 5 Centaurs, except for Dwarf Master Ufretin]
!!HE19:C1/16/14/5;
!!HE20:C1/16/14/5;
!!HE21:C1/16/14/5;
!!HE22:C1/16/14/5;
!!HE23:C1/16/14/5;
!!HE24:C1/16/14/5;
!!HE25:C1/16/14/5;
!!HE26:C1/16/14/5;
!!HE27:C1/16/14/5;
!!HE28:C1/16/14/5;
!!HE29:C1/16/14/5;
!!HE30:C1/16/14/5;
!!HE31:C1/16/14/5;
!!HE17:C1/16/17/5; [Ufretin now starts with Battle Dwarves instead]
!!HE16:C1/18/14/10; [All Rampart Heroes get Wood Elves replaced by 10 Centaurs]
!!HE17:C1/18/14/10;
!!HE18:C1/18/14/10;
!!HE19:C1/18/14/10;
!!HE20:C1/18/14/10;
!!HE21:C1/18/14/10;
!!HE22:C1/18/14/10;
!!HE23:C1/18/14/10;
!!HE24:C1/18/14/10;
!!HE25:C1/18/14/10;
!!HE26:C1/18/14/10;
!!HE27:C1/18/14/10;
!!HE28:C1/18/14/10;
!!HE29:C1/18/14/10;
!!HE30:C1/18/14/10;
!!HE31:C1/18/14/10;
!!HE32:C1/32/30/d; [All Tower Heroes get Stone Golems replaced by ->]
!!HE33:C1/32/30/d; [-> the same number of Stone Gargoyles except for ->]
!!HE34:C1/32/33/2; [-> Golem Master Josephine]
!!HE35:C1/32/30/d;
!!HE36:C1/32/30/d;
!!HE37:C1/32/30/d;
!!HE38:C1/32/30/d;
!!HE39:C1/32/30/d;
!!HE40:C1/32/30/d;
!!HE41:C1/32/30/d;
!!HE42:C1/32/30/d;
!!HE43:C1/32/30/d;
!!HE44:C1/32/30/d;
!!HE45:C1/32/30/d;
!!HE46:C1/32/30/d;
!!HE47:C1/32/30/d;

!!if&i^wog_50_enabled^;                             [Enhanced Monsters]
  !!HE48:C1/44/44/3; [When Enhanced Monsters is active, Starting Inferno Heroes ->]
  !!HE49:C1/44/44/3; [-> start with 3 Gogs for every Gog slot]
  !!HE50:C1/44/44/3;
  !!HE51:C1/44/44/3;
  !!HE52:C1/44/44/3;
  !!HE53:C1/44/44/3;
  !!HE54:C1/44/44/3;
  !!HE55:C1/44/44/3;
  !!HE56:C1/44/44/3;
  !!HE57:C1/44/44/3;
  !!HE58:C1/44/44/3;
  !!HE59:C1/44/44/3;
  !!HE60:C1/44/44/3;
  !!HE61:C1/44/44/3;
  !!HE62:C1/44/44/3;
  !!HE63:C1/44/44/3;
!!en;

!!HE64:C0/0/59/5;
!!HE64:C0/1/59/4; [Straker starts with some Zombies]
!!HE64:C0/2/59/5;
!!HE65:C1/58/60/3; [All Heroes except Straker start with 3 Wights ->]
!!HE66:C1/58/60/3; [-> instead of Walking Dead]
!!HE67:C1/58/60/3;
!!HE68:C1/58/60/3;
!!HE69:C1/58/60/3;
!!HE70:C1/58/60/3;
!!HE71:C1/58/60/3;
!!HE72:C1/58/60/3;
!!HE73:C1/58/60/3;
!!HE74:C1/58/60/3;
!!HE75:C1/58/60/3;
!!HE76:C1/58/60/3;
!!HE77:C1/58/60/3;
!!HE78:C1/58/60/3;
!!HE79:C1/58/60/3;
!!HE71:C0/1/56/0; [Galthran in Necropolis, starts with less Skeletons]
!!HE71:C0/2/56/0;
!!HE87:C0/2/70/0; [Shakti starts with no Troglodytes in Slot 3]
!!HE100:C0/2/84/0; [Gretchin starts with no Goblins in Slot 3]
!!HE112:C1/106/98/15; [Bron starts with Gnolls instead of Level 4 Basilisks]
!!HE113:C0/2/99/1; [Drakon starts with a Gnoll Marauder in Slots 3 and 4]
!!HE113:C0/3/99/1;
!!HE114:C1/100/98/12; [Wystan starts with Gnolls instead of Lizardmen]
!!HE114:C0/2/101/1; [Wystan starts with 1 Lizard Warrior in Slot 3]

[REBALANCED START CREATURES - TAVERN HEROES]
!?FU(OnEveryDay)&i^timerWeekday^=7/i^timerOnce^/i^wog_199_enabled^;
!!FU(WOG_199_RebalancStartArmies2):P;

!?FU(WOG_199_RebalancStartArmies2);
!!HE0:H2/3/1/1;   [Orrin sometimes starts with 1 Marksman]
!!HE1:H2/3/1/2;   [Valeska sometimes starts with 1-2 Marksman]
!!HE2:H2/4/1/3;   [Edric sometimes starts with 1-3 Griffins]
!!HE3:H2/115/1/3; [Sylvia sometimes starts with 1-3 Water Elementals]
!!HE4:H2/1/1/2;   [Lord Haart sometimes starts with 1-2 Halberdiers]
!!HE5:H2/1/2/3;   [Sorsha sometimes starts with 2-3 Halberdiers]
!!HE6:H2/2/1/2;   [Christian sometimes starts with 1-2 Archers]
!!HE7:H2/1/1/4;   [Tyris sometimes starts with 1-4 Halberdiers]
!!HE16:H2/3/1/2;  [Mephala sometimes starts with 1-2 Marksman, see bio]
!!HE17:H0/17/5/6; [Ufretin - Battle Dwarves in Slot 1 (always there)]
!!HE17:H1/17/4/6; [Ufretin - Battle Dwarves in Slot 2 (usually there)]
!!HE17:H2/17/4/6; [Ufretin - Battle Dwarves in Slot 3 (occasionally there)]
!!HE18:H2/18/1/2; [Jenova sometimes starts with 1-2 Wood Elves]
!!HE19:H2/22/1/1; [Ryland sometimes starts with 1 Dendroid Guard]
!!HE20:H2/16/2/4; [Thorgrim sometimes starts with 2-4 Dwarves]
!!HE21:H1/18/2/3; [Ivor starts with 2-3 Wood Elves in Slot 2]
!!HE21:H2/19/1/1; [Ivor sometimes has 1 Grand Elf in Slot 3]
!!HE22:H2/16/1/4; [Clancy sometimes starts with 1-4 Dwarves]
!!HE23:H2/18/1/1; [Kyrre sometimes starts with 1 Wood Elf]
!!HE24:H2/-1/0/0; [Druids - No Elves in the third slot]
!!HE25:H2/-1/0/0;
!!HE26:H2/-1/0/0;
!!HE27:H2/-1/0/0;
!!HE28:H2/-1/0/0;
!!HE29:H2/-1/0/0;
!!HE30:H2/-1/0/0;
!!HE31:H2/-1/0/0;
!!HE34:H2/33/1/1;
!!HE18:H0/14/13/17; [Rampart Heroes start with 13-17 Centaurs in first slot]
!!HE19:H0/14/13/17;
!!HE20:H0/14/13/17;
!!HE21:H0/14/13/17;
!!HE22:H0/14/13/17;
!!HE23:H0/14/13/17;
!!HE24:H0/14/13/17;
!!HE25:H0/14/13/17;
!!HE26:H0/14/13/17;
!!HE27:H0/14/13/17;
!!HE28:H0/14/13/17;
!!HE29:H0/14/13/17;
!!HE30:H0/14/13/17;
!!HE31:H0/14/13/17;
!!HE64:H0/59/4/5; [Straker starts with some Zombies]
!!HE64:H1/59/4/5;
!!HE64:H2/59/4/5;
!!HE71:H1/-1/0/0; [Galthran starts with less Skeletons]
!!HE71:H2/-1/0/0;
!!HE87:H1/-1/0/0; [Shatki starts with less Troglodytes]
!!HE87:H2/-1/0/0;
!!HE112:H1/98/10/15; [Bron starts with more Gnolls]
!!HE112:H2/98/10/15;
!!HE114:H0/100/4/5; [Wystan starts with less Lizardmen]
!!HE114:H1/100/3/5;
!!HE114:H2/101/1/1;
!!HE122:H2/115/1/3; [Voy sometimes starts with 1-3 Water Elementals]

!#FU(WOG_Rebalance_Initialization):P;

; Fix HE:X6 bug of unwanted upgrade to self
; Fix HE:X6 bug of upgrading undefined creatures
!?FU(OnDetermineMonInfoDlgUpgrade_Quit)&i^wog_198_enabled^;
!#VA(monType:x) (upgType:x) (town:x) (hero:x);

!!if&(hero)>(NO_HERO);
  !!if&(monType)=(upgType);
    !!VR(upgType):S(NO_MON);
  ; Fix for Thant's ability to upgrade Skeltons to Skeleton Warriors.
  ; Not sure the reason, but with the HE:X6 Thant is also able to upgrade the monster which should be the upgrade of Skeleton Warrior to Skeleton Warrior
  !!el&(upgType)<>(NO_MON);
    !!MA:U(upgType)/?(secUpgType:y);
    !!VR(upgType)&(secUpgType)=(monType):S(NO_MON);
  !!en;
!!en;
