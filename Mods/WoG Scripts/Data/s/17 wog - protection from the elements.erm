ZVSE2

** ENHANCED PROTECTION FROM THE ELEMENTS
** УСИЛЕННАЯ ЗАЩИТА ОТ НЕУДАРНОЙ МАГИИ

** Option 061 by Petyo Georgiev
** Rewritten by igrik (29.06.2020)

** The Protection from Fire, Protection from Air, Protection from Water and Protection
** from Earth spells give protection against non-damage spells (like Slow, Curse, etc.).
** The chance of resisting such a spell depends on your proficiency at the element your troops
** are protected from (i.e., your Air, Earth, Fire or Water Magic skill level)

* None - 20%
* Basic - 33%
* Advanced or Expert - 50%


!#DC(WOG_OPT_PROTECTION_FROM_SCOOLMAGIC) = 61;


!?FU(WOG_CreateERMHook);
!!UN:P(WOG_OPT_PROTECTION_FROM_SCOOLMAGIC)/?(wogOption:y);   
!!SN&(wogOption)=(TRUE):Ex1/1/4474395/(WOG_OPT_PROTECTION_BattleStack_ApplySpell);

!?FU(WOG_OPT_PROTECTION_BattleStack_ApplySpell);
!!UN:P(WOG_OPT_PROTECTION_FROM_SCOOLMAGIC)/?(wogOption:y); 
!!FU&(wogOption)<>(TRUE):E;   

; [get spell caster type]
!!BG:A?(action:y);
!!if&(action)=(BATTLE_ACTION_HERO_CAST);

; [get spell id]
 !!SN:X?(hookMain:y);
 !!UN:C(hookMain)/(STRUCT_HOOK_CONTEXT_EBX)/(UNC_UINT32)/?(spellId:y);

 ; [if enemy & not damage spell]
 !!if&(spellId)>(SPELL_EARTHQUAKE)/(spellId)<(SPELL_FIRE_ELEMENTAL);    
   !!SS(spellId):F?(spellFlags:y) O?(spellTarget:y);               
   !!VR(spellFlags:y):&262144;                                     
   !!if&(spellFlags:y)=262144/(spellTarget)=-1;  [if enemy & not damage spell]

     ; [if stack target is true]
     !!FU(WOG_Battle_MR_N_GetStackID):P?(stackID:y);
     !!if&(stackID)<>(NO_MON);         [if stack target is true]

       !!VR(spellPower:y):S(NULL);  
       !!FU(WOG_Battle_GetSpellProtectionLvl):P(spellId)/(SPELL_SCHOOL_AIR)/(SPELL_PROTECTION_FROM_AIR)/(stackID)/?(spellPower)   
                                              P(spellId)/(SPELL_SCHOOL_FIRE)/(SPELL_PROTECTION_FROM_FIRE)/(stackID)/?(spellPower) 
                                              P(spellId)/(SPELL_SCHOOL_WATER)/(SPELL_PROTECTION_FROM_WATER)/(stackID)/?(spellPower)
                                              P(spellId)/(SPELL_SCHOOL_EARTH)/(SPELL_PROTECTION_FROM_EARTH)/(stackID)/?(spellPower);   

       !!if&(spellPower)>=0/(spellPower)<=3;
         ; get spell power (rounds)
         !!UN:C(hookMain)/(STRUCT_HOOK_CONTEXT_EBP)/(UNC_UINT32)/?(ebp:y);  
         !!UN:C(ebp)/12/(UNC_UINT32)/?(rounds:y);     [y6 - rounds]
         ;
         !!VR(procents:y):S80;                  has no lvl [protection 20%]
         !!VR(procents)&(spellPower)=(SKILL_BASIC):S67;    [protection 33%]
         !!VR(procents)&(spellPower)=(SKILL_ADVANCED):S50; [protection 50%]
         !!VR(procents)&(spellPower)=(SKILL_EXPERT):S50;   [protection 50%]
         
         !!VR(protection:y):S(rounds) +1 *(procents) :100; [set procents]
         !!VR(protection)&(protection)<1:S1;         [if less than one, set one round]
         !!UN:C(ebp)/12/(UNC_UINT32)/(protection);      [set new count rounds]
       !!en;
     !!en;
   !!en;
 !!en;
!!en;

** End of Script **
