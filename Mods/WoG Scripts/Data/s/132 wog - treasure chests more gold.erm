ZVSE2

** SOME TREASURE CHESTS CONTAIN MORE GOLD
** РАСШИРЕННЫЕ СУНДУКИ С СОКРОВИЩАМИ

** Option 132 by Donald X. Vaccarino
** Rewritten by igrik (24.11.2020)

* Some standard Treasure Chests contain more gold.
* There's an 8% chance per chest that it contains 2500 gold 
*      and a 2% chance per chest that it contains 3000 gold.


!#DC(WOG_OPT_TREASURE_CHESTS_MORE_GOLD) = 132;


; function for passing through all map objects
!?FU(WOG_OnIterateAllMapObjects);
!#VA(x:x) (y:x) (z:x);
!#VA(objType:x);
!#VA(objSubtype:x);
!#VA(isYellowSquare:x); boolean.
!#VA(isPassable:x);     boolean.

; check: is entry to the object
!!FU&(isYellowSquare)<>(TRUE):E;

; check: object type
!!FU&(objType)<>(OBJ_TREASURE_CHEST):E;

; check option is enabled
!!UN:P(WOG_OPT_TREASURE_CHESTS_MORE_GOLD)/?(wogOption:y); 
!!FU&(wogOption)<>(TRUE):E;

; object reconfigure
!!VR(random:y):R0/0/99; 
!!CH(x)/(y)/(z)&(random)<8:B5;  [8% chance for 2500 gold chest]
!!CH(x)/(y)/(z)&(random)>97:B6; [2% chance for 3000 gold chest]


** End of Script **
