ZVSE2

** BATTLE EXTENDER
** —ÀŒ∆Õ€≈ ¡»“¬€
** Option 041 by Alexis Koz (script41)
** Updated by Archer30

** Changes Parameters of monsters:
** - Each level 1-6 monster gets additional hit points.
** - Each shooter decreases its number of shots to 3/4 of its original quantity.
** - Each monster gets additional Defense.
** - Each monster has reduced Speed.

** -----------------------------------------------------------------------------

!?FU(OnEveryDay_Quit)&i^timerOnce^/i^timerDay^=1;
!!UN:P41/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; set creatures costs for upgraded level 6 monsters
!!MA:C(MON_CHAMPION)/(RES_ORE)/2;
!!MA:C(MON_WAR_UNICORN)/(RES_GEMS)/1;
!!MA:C(MON_NAGA_QUEEN)/(RES_MERCURY)/1;
!!MA:C(MON_EFREET_SULTAN)/(RES_SULFUR)/1;
!!MA:C(MON_DREAD_KNIGHT)/(RES_CRYSTAL)/1;
!!MA:C(MON_SCORPICORE)/(RES_CRYSTAL)/1;
!!MA:C(MON_CYCLOPS_KING)/(RES_GEMS)/1;
!!MA:C(MON_WYVERN_MONARCH)/(RES_MERCURY)/1;
!!MA:C(MON_MAGIC_ELEMENTAL)/(RES_SULFUR)/1;
; set creatures params
!!DO(WOG_41_SetCreaturesParams)/0/121/1:P;
!!DO(WOG_41_SetCreaturesParams)/123/129/2:P;
!!DO(WOG_41_SetCreaturesParams)/130/144/1:P;
!!DO(WOG_41_SetCreaturesParams)/150/173/1:P;
!!FU(GetMaxMonsterId):P?(lastMon:y);
!!DO(WOG_41_SetCreaturesParams)/192/(lastMon)/1:P;

!?FU(WOG_41_SetCreaturesParams);
; get creature lvl
!!MA:Lx16/?y2;
!!VRy3:S16-y2;
; set HP
!!MA:Px16/?y2;
!!VRy2:*y3:10;
!!MA:Px16/y2;
; set Shots
!!MA:Nx16/?y2;
!!VRy2:*3:4;
!!MA:Nx16/y2;
; set Defense
!!MA:Dx16/?y2;
!!VRy2:*13:10+1;
!!MA:Dx16/y2;
; set Speed
!!MA:Sx16/?y2;
!!VRy2:*7:10+1;
!!MA:Sx16/y2;

** End of Script **
