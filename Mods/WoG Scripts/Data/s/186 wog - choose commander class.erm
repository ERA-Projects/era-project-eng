ZVSE2

** CHOOSE COMMANDER CLASS
** ВЫБОР ТИПА КОМАНДИРА

** Option 186 by Timothy Pulver
** Rewritten by igrik (30.09.2020)
** a bit Rewritten by daemon_n (07.09.2022)

** When you hire a Commander, you can choose any Commander type regardless of the hero's class.


** В оригинальном скрипте AI командир устанавливался случайного класса (VR:S0 R8)
** Я не вижу в этом никакого смысла, поэтому для AI функционала не будет (note: igrik)


!#DC(WOG_OPT_CHOOSE_COMMANDER_CLASS) = 186;


!?FU(OnAfterCommanderBuy)&1000;
  !!UN:P(WOG_OPT_CHOOSE_COMMANDER_CLASS)/?(wogOption:y);

  !!if&(wogOption)>0;
    !!OW:A(CURRENT_PLAYER)/?(heroId:y);

    !!if&(heroId)<>(NO_HERO);
      !!FU(RadioDlg_Reset):P;
      !!FU(RadioDlg_SetTitle):P^%T(wog.186.title)^;

      !!re i/(MON_PALADIN_A)/(MON_ASTRAL_SPIRIT_A);
        !!SN:H^monname^/i/0/?(npcName:z);
        !!FU(RadioDlg_AddItem):P(npcName)/i;
      !!en;

      !!FU(RadioDlg_AddItem):P^%T(wog.186.standart)^/0;     [don't change the class]
      !!FU(RadioDlg_AddItem):P^%T(wog.186.cancel)^/(DLG_RESULT_CANCEL);
      !!FU(RadioDlg_Show):P?s/?(cType:y);

      !!if&(cType)=(DLG_RESULT_CANCEL);                                [if cancel]
        !!CO(heroId):E0;                                    [remove Commander]
        !!OW:R(CURRENT_PLAYER)/(RES_GOLD)/d1000;
        *!IF:M^%T(wog.186.noHired)^;                        [show text: no hired]

      !!el&(cType)>0;
        !!VR(cType):-(MON_PALADIN_A);
        !!CO(heroId):T(cType);
      !!en;

    !!en;
  !!en;
** End of Script **
