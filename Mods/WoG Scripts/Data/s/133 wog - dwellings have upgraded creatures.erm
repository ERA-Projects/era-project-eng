ZVSE2

** SOME DWELLINGS HAVE UPGRADED CREATURES
** УЛУЧШЕННЫЕ ВНЕШНИЕ ЖИЛИЩА

** Option 133 by Donald X. Vaccarino
** Rewritten by igrik (24.11.2020)
** updated by daemon_n and Archer30 (Oct 2022) // added fix of the upgraded Dwellings town growth and limitation on upgrades

* Approximately 25% of Dwellings will sell upgraded creatures.


!#DC(WOG_OPT_DWELLINGS_UPGRADED_CREATURES) = 133;


; Disable as this part has compatibility impact to external scripts 
!?FU(WOG_CreateERMHook);
!!FU:E;

!!SN:Ex1/1/6029924/(WOG_OnCalculateTownGrowthFromDwelling); 5C0264

!?FU(WOG_OnCalculateTownGrowthFromDwelling);
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBP)/4/?(ebp:y) C(ebp)/8/4/?(monType:y);
  !!if&(monType)<=(MON_PHOENIX);
    !!VR(ind:y):S(monType) %2; *14 -(monType) *-1 -1;
    !!if&(ind);
      !!VR(monType):-1;                 [This is wrong on elementals. And it could be wrong with external scripts!]
      !!UN:C(ebp)/8/4/(monType:y);
    !!en;    
  !!en;

; function for passing through all map objects
!?FU(WOG_OnIterateAllMapObjects);
!#VA(x:x) (y:x) (z:x);
!#VA(objType:x);
!#VA(objSubtype:x);
!#VA(isYellowSquare:x); boolean.
!#VA(isPassable:x);     boolean.

; check: is entry to the object
!!FU&(isYellowSquare)<>(TRUE):E;

; check option is enabled
!!UN:P(WOG_OPT_DWELLINGS_UPGRADED_CREATURES)/?(wogOption:y); 
!!FU&(wogOption)<>(TRUE):E;

; check: object type
!!FU&(objType)<>(OBJ_CREATURE_GENERATOR_1):E;

; generate 25% chance
!!VR(random:y):R0/0/3; 
!!FU&(random)<>0:E;

; get type creature
!!DW(x)/(y)/(z):M0/?(typeMon:y)/d; 

; get creature upgrade
!!FU(GetUpgradedMonster):P(typeMon)/?(typeUpgrade:y);
!!FU&(typeUpgrade)<=(NO_MON):E;

; Exit if the upgraded monster has a different level with the original
!!MA:L(typeMon)/?(monLevel:y) L(typeUpgrade)/?(upgLevel:y);
!!FU&(upgLevel)<>(monLevel):E;

; Exit if the upgraded monster is stronger than Blood Dragon (weakest Level 8)
!!MA:F(typeUpgrade)/?(upgFv:y) F(MON_BLOOD_DRAGON)/?(bloodDragonFv:y);
!!FU&(upgFv)>=(bloodDragonFv):E;

; set upgrade type guards, if level >= 4
!!DW(x)/(y)/(z)&(upgLevel)>=4:G0/(typeUpgrade)/d;

; set upgrade type creature
!!DW(x)/(y)/(z):M0/(typeUpgrade)/d;

** End of Script **
