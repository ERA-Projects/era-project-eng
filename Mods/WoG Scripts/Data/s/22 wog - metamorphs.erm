ZVSE2

** Metamorphs by Timothy Pulver
** Version 1.3
** Script56.erm
** Updated by rennyo 26.02.2012
** Updated August 23, 2004
** Updated by Archer30 on May 2022

** This script changes all Earth Messengers into Metamorphs. Metamorphs stacks transform
** in battle on their first action into another type of creature. The type is determined
** randomly but their total Health will be equal or lower in the new form. They'll change
** again each time they completely kill a stack of enemy monsters, unless the Health of
** one creature of that type exceeds the total Health of the (transformed) Metamorph
** stack. After battle, transformed Metamorphs will change back into their normal
** (Metamorph) form. Metamorphs won't transform on Cursed Ground.

** Note: this script won't operate in MP (network) human-to-human battles.

Permanent Variables used:  v4500-v4602, z206-z209
Temporary Variables used:  v1-v10, v13-v14, v24

--------------------------------------------------------------------------------------------------------------------

 [Check if script is enabled: v1]
!?FU(OnAfterErmInstructions);
!!UN:P56/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

 [Set Name and Description for Metamorph (Earth Messenger)]
!!SN:H^monname^/(MON_EARTH_MESSENGER)/0/z156000;
!!SN:H^monname^/(MON_EARTH_MESSENGER)/1/z156001;
!!SN:H^monname^/(MON_EARTH_MESSENGER)/2/z156002;

 [Set new bonus lines for Metamorphs (but only when in Metamorph form)]
!!EA(MON_EARTH_MESSENGER):F97/10/?(slot:y); [Bonus line number for Quicksand Post-Attack Spell: v2]
!!EA(MON_EARTH_MESSENGER):B(slot)/1/87/43/0/5/10/15/20/25/30/35/40/45/50; [5% Magic Resistance per Rank]
!!EA(MON_EARTH_MESSENGER):F112/67/?(slot); [Bonus line number for Summon Earth Elemental Pre-Attack Spell: v3]
!!EA(MON_EARTH_MESSENGER):B(slot)/1/114/75/0/35/40/45/50/55/60/65/70/75/80; [75% Chance of Regeneration: Rank 1]

--------------------------------------------------------------------------------------------------------------------

 [Store information about creature type (Metamorphs) in v variables)]
; Note: OnBeforeBattleUniversal doesn't work for some reason
!?FU(OnSetupBattlefield)&1000/-998;
!!UN:P56/?(wogOption:y);                [Check if script is enabled: y-1]
!!FU&(wogOption)<>(TRUE):E;             [Exit if script isn't enabled]

!!DO(WOG_56_StoreMetamorphInfo)/0/41/1:P; [Cycle through all battlefield stacks]

 [Battle location: v998/v999/v1000]
!!TR998:G?(terrainOverlay:y);           [Check for special terrian type: y-5]
!!VRi^wog_56_isCursedGround^:S0;        [Initialize i^wog_56_isCursedGround^ to 0]
!!VRi^wog_56_isCursedGround^&(terrainOverlay)=21:S1; [Set i^wog_56_isCursedGround^ to 21 if battle is on Cursed Ground: i^wog_56_isCursedGround^]

--------------------------------------------------------------------------------------------------------------------

 [Store information about creature type (Metamorphs) in v variables]
!?FU(WOG_56_StoreMetamorphInfo);
!!VRy1:S4500 +x16; [Index number for monster Type: y1]
!!VRy2:S4550 +x16; [Index number for number in stack: y2]
!!BMx16:T?vy1 B?vy2; [Store monster Type in vy1, Number in vy2]

--------------------------------------------------------------------------------------------------------------------

 [Check if Metamorphed creature defeats an enemy stack]
!?FU(OnBeforeBattleAction)&i^wog_56_isCursedGround^=0/1000/-998;
!!UN:P56/?y-1; [Check if script is enabled: y-1]
!!FU&y-1<>1:E; [Exit if script isn't enabled]

!!VRi^wog_56_targetStack^:S-1; [Initialize i^wog_56_targetStack^ to -1]
!!BG:N?v1 A?v2; [Current stack number: v1, Action: v2]
!!VRv3:S4500 +v1; [Index for original acting monster: v3]
!!VRv4:Svv3; [Original monster number of acting stack: v4]

!!BG:E?y-2; [Destination monster stack: y-2]
!!BMy-2&y-2>=0:T?y-3 R?y-4; [Target type (y-3), Retaliations left (y-4)]
!!VRy-5&y-2>=0:S4500 +y-2; [Index for original target monster: y-5]
!!VRy-6&y-2>=0:Svy-5; [Original monster number of target stack: y-6]
!!VRv4601:S-1; [Initialize v4601 to -1]
!!VRv4601&y-2>=0/y-6=(MON_EARTH_MESSENGER)/v2=6/y-4>0:Sv1; [Set v4601 to attacking stack number if target is a Metamorph and it can retaliate]
!!VRv4602:S-1; [Initialize v4602 to -1]
!!VRv4602&y-2>=0/y-6=(MON_EARTH_MESSENGER)/v2=6/y-4>0:Sy-2; [Set v4602 to target's stack number if target is a Metamorph and it can retaliate]

!!BG&v4=(MON_EARTH_MESSENGER)/v2>=6/v2<=7:E?i^wog_56_targetStack^; [Target monster stack: i^wog_56_targetStack^]
!!FU&i^wog_56_targetStack^<0:E; [Exit if no valid target monster]

!!BMi^wog_56_targetStack^&v4=(MON_EARTH_MESSENGER)/v2>=6/v2<=7:T?v4543; [Target type: v4543]
!!BG&v4=(MON_EARTH_MESSENGER)/v2>=6/v2<=7:N?v4544; [(Metamorph's) stack #: v4544]

--------------------------------------------------------------------------------------------------------------------

 [Determine if victorious Metamorphed stack transforms again]
!?FU(OnAfterBattleAction)&i^wog_56_targetStack^>=0/i^wog_56_isCursedGround^=0/1000/-998; 
!!UN:P56/?y-1; [Check if script is enabled: y-1]
!!BU:C?y-3; [Check if battle is ending this turn: y-3=1 if yes]
!!FU|y-1<>1/y-3=1:E; [Exit if script isn't enabled or battle is ending]

!!BMi^wog_56_targetStack^:N?v1; [Number of target monsters still alive: v1]
!!FU&v1>0:E; [Exit if any still alive]

!!VRv1:Sv4544; [(Metamorph's) stack number: v1]
!!BMv1:T?v2 N?v3; [Type that Metamorph is currently: v2, Number in stack: v3]
!!MA:Pv2/?v4; [Health of one (Metamorph): v4]
!!VRv4:*v3; [Total health of (Metamorph) stack: v4]
!!MA:Pv4543/?v5; [Health of one target creature: v5]
!!FU&v2=v4543:E; [Exit if target creature is the same type as Metamorph's current type]
!!FU&v4<v5:E; [Exit if target creature's health is higher than (Metamorphs)]
!!FU(WOG_CheckIfMonsterIsValid):Pv4543/?y30; [Exit if target creature is a War Machine, commander or emissary]
!!FU&y30<>(TRUE):E;

!!VRy-4:Sv1 *-1 -1; [Negative of Metamorph stack number: y-4]
!!EAy-4:E?y-5/d/d/d; [Experience of Metamorph stack: y-5]
!!VRv9:Sv4 :v5; [Number of creatures (Metamorphs) can transform to: v9]
!!VRv9&v9<1:S1; [Minimum 1]
!!BMv1:P?v6 O?v7 I?v8; [Position: v6, Hero's slot: v7, Side: v8]

!!BMv1:F?y-8; [Read Metamorph's Flags: y-8]
!!VRy-9:Sy-8; [Copy Metamorph's Flags to y-9]
!!VRy-10:Sy-8; [Copy Metamorph's Flags to y-10]
!!VRy-9:&8388608; [See if Metamorph is a clone: y-9=8388608 if yes]
!!VRy-10:&4194304; [See if Metamorph was summoned: y-10=4194304 if yes]
!!VRy-8:|4194304; [Set Metamorph's "summoned" bit if not set]
!!VRy-8:|8388608; [Set Metamorph's "clone" bit if not set]
!!VRy-8:|(MON_FLAG_NO_COLORING);
!!BMv1:Fy-8; [Write Metamorph's Flags: y-8]

!!BMv1:K1;                              [Kill the Metamorph]
!!SN&i^battle_isVisible^:D;

!!BU:Sv4543/v9/v6/v8/v7/1; [Summon new (Metamorph) creature stack: type=v4543]
!!BU:Ev6/?y-1; [Stack number of new (Metamorph) creature stack: y-1]

*!BMy-1&v4543>=174/v4543<=191:E2; [Set Commander Monster Metamorph spell castings to 2]
*!BMi^wog_56_targetStack^&v4543>=174/v4543<=191:U4/?y-14; [Target Commander's spell number: y-14]
*!BMy-1&v4543>=174/v4543<=191:U4/y-14; [Set Commander Monster Metamorph's spell number]

!!BMy-1&y-1>=0:F?y-2; [New (Metamorph's) Flags: y-2]
!!VRy-2:|67108864; [Add "done acting this round" Flag]
!!VRy-2&y-9=8388608:|8388608; [Set new Metamorph stack's "clone" bit if it was a clone]
!!VRy-2&y-10=4194304:|4194304; [Set new Metamorph stack's "summoned" bit if it was summoned]
!!BMy-1&y-1>=0:Fy-2; [Write modified Flags to (Metamorph) stack: y-2]

!!VRy-6:Sy-1 *-1 -1; [Negative of New Metamorph stack number: y-6]
!!EAy-6:Ov4543; [Copy Bonus Lines for new type (v4543) to Metamorph stack (y-6)]
!!EAy-6:Ey-5/2/v4543/d; [Set Experience of Metamorph stack: y-5]

!!VRz1&v5=1:Sz156003;
!!VRz1&v5>1:Sz156004;
!!UN:N3/2/v4543/1; [New creature name (plural): z2]
!!VRz3:Sz156005;
!!BU:Mz3; [Display scroll bar message]

!!BG:N?v10; [Next stack to act: v10]
!!BMv10:P?v13; [Next stack's position: v13]
!!BMv10:C2/v13/3/1/0; [Next stack casts dummy spell on self]

--------------------------------------------------------------------------------------------------------------------

 [Post-Action Trigger if Metamorph is being attacked and retaliates]
 [to see if it transforms into the creature type that attacked it]
!?FU(OnAfterBattleAction)&v4601>=0/i^wog_56_isCursedGround^=0/1000/-998; [After action - only if v4601 isn't less than 0]
!!UN:P56/?y-1; [Check if script is enabled: y-1]
!!BU:C?y-3; [Check if battle is ending this turn: y-3=1 if yes]
!!FU|y-1<>1/y-3=1:E; [Exit if script isn't enabled or battle is ending]

!!BMv4601:N?y-11; [Number of attacking monsters still alive after retaliation: y-11]
!!FU&y-11>0:E; [Exit if any still alive]

!!VRv1:Sv4602; [(Metamorph's) stack number: v1]
!!BMv1:T?v2 N?v3; [Type that Metamorph is currently: v2, Number in stack: v3]
!!MA:Pv2/?v4; [Health of one (Metamorph): v4]
!!VRv4:*v3; [Total health of (Metamorph) stack: v4]
!!BMv4601:T?y-7; [Type of attacking creature: y-7]
!!MA:Py-7/?v5; [Health of one attacking creature: v5]
!!FU&v2=y-7:E; [Exit if attacking creature is the same type as Metamorph's current type]
!!FU&v4<v5:E; [Exit if attacking creature's health is higher than (Metamorphs)]
!!FU(WOG_CheckIfMonsterIsValid):Py-7/?y30; [Exit if target creature is a War Machine, commander or emissary]
!!FU&y30<>(TRUE):E;

!!VRy-4:Sv1 *-1 -1; [Negative of Metamorph stack number: y-4]
!!EAy-4:E?y-5/d/d/d; [Experience of Metamorph stack: y-5]
!!VRv9:Sv4 :v5; [Number of creatures (Metamorphs) can transform to: v9]
!!VRv9&v9<1:S1; [Minimum 1]
!!BMv1:P?v6 O?v7 I?v8; [Position: v6, Hero's slot: v7, Side: v8]

!!BMv1:F?y-8 R?y-12; [Read Metamorph's Flags (y-8) and Retaliations (y-12)]
!!VRy-9:Sy-8; [Copy Metamorph's Flags to y-9]
!!VRy-10:Sy-8; [Copy Metamorph's Flags to y-10]
!!VRy-9:&8388608; [See if Metamorph is a clone: y-9=8388608 if yes]
!!VRy-10:&4194304; [See if Metamorph was summoned: y-10=4194304 if yes]
!!VRy-8:|4194304; [Set Metamorph's "summoned" bit if not set]
!!VRy-8:|8388608; [Set Metamorph's "clone" bit if not set]
!!VRy-8:|(MON_FLAG_NO_COLORING);
!!BMv1:Fy-8; [Write Metamorph's Flags: y-8]

!!BMv1:K1;                              [Kill the Metamorph]
!!SN&i^battle_isVisible^:D;

!!BU:Sy-7/v9/v6/v8/v7/1; [Summon new (Metamorph) creature stack: type=y-7]
!!BU:Ev6/?y-1; [Stack number of new (Metamorph) creature stack: y-1]

*!BMy-1&y-7>=174/y-7<=191:E2; [Set Commander Monster Metamorph spell castings to 2]
*!BMv4601&y-7>=174/y-7<=191:U4/?y-14; [Target Commander's spell number: y-14]
*!BMy-1&y-7>=174/y-7<=191:U4/y-14; [Set Commander Monster Metamorph's spell number]

!!BMy-1&y-1>=0:F?y-2; [New (Metamorph's) Flags: y-2]
!!VRy-2:|67108864; [Add "done acting this round" Flag]
!!VRy-2&y-9=8388608:|8388608; [Set new Metamorph stack's "clone" bit if it was a clone]
!!VRy-2&y-10=4194304:|4194304; [Set new Metamorph stack's "summoned" bit if it was summoned]
!!BMy-1&y-1>=0:Fy-2 Ry-12; [Write modified Flags (y-2) and Retaliations (y-12) to (Metamorph) stack]
!!VRy-6:Sy-1 *-1 -1; [Negative of New Metamorph stack number: y-6]
!!EAy-6:Oy-7; [Copy Bonus Lines for new type (y-7) to Metamorph stack (y-6)]
!!EAy-6:Ey-5/2/y-7/d; [Set Experience of Metamorph stack: y-5]
!!VRz1&v5=1:Sz156006;
!!VRz1&v5>1:Sz156007;
!!UN:N3/2/y-7/1; [New creature name (plural): z2]
!!VRz3:Sz156008;
!!BU:Mz3; [Display scroll bar message]
!!BG:N?v10; [Next stack to act: v10]
!!BMv10:P?v13; [Next stack's position: v13]
!!BMv10:C2/v13/3/1/0; [Next stack casts dummy spell on self]

--------------------------------------------------------------------------------

 [Battle Stack Obtain Turn trigger]
!?FU(OnBattleStackObtainsTurn)&i^wog_56_isCursedGround^=0/1000/-998/i^battle_round^>=0;
!!UN:P56/?y-1; [Check if script is enabled: y-1]
!!FU&y-1<>1:E; [Exit if script isn't enabled]

!!VRv1:Si^battle_current_stack^; [Current stack number: v1]
!!BMi^battle_current_stack^:T?v2 N?v5; [Type of monster: v2, Number in stack: v5]

!!FU(WOG_56_TransformMetamorphInit)&v2=(MON_EARTH_MESSENGER)/v5>=1:P; [Initial Transformation of Metamorph]

--------------------------------------------------------------------------------

 [Initial Transformation of Metamorph]
!?FU(WOG_56_TransformMetamorphInit);
!!VRv3:S0 R164; [Random number from 0..164 for monster: v3]
!!VRv4:S-1; [Initialize v4 to -1]
!!MA:Pv2/?v6; [Health of one Metamorph: v6]
!!VRv6:*v5; [Total health of Metamorph stack: v6]

; Determine new creature
; Note that this is not the same as FU(GetRandomMonster)
!!DO(WOG_56_DetermineNewCreature)/v3/164/1:P;
!!DO(WOG_56_DetermineNewCreature)/0/v3/1&v4<0:P; [Determine new creature part 2: v4]

!!BMv1:P?v8 O?v14; [Position of Metamorph: v8, Hero's slot: v14]
!!VRy-4:Sv1 *-1 -1; [Negative of Metamorph stack number: y-4]
!!EAy-4:E?y-5/d/d/d; [Experience of Metamorph stack: y-5]
!!BMv1:I?v9; [Side that owns Metamorph: v9]

!!BMv1:F?y-8; [Read Metamorph's Flags: y-8]
!!VRy-9:Sy-8; [Copy Metamorph's Flags to y-9]
!!VRy-10:Sy-8; [Copy Metamorph's Flags to y-10]
!!VRy-9:&8388608; [See if Metamorph is a clone: y-9=8388608 if yes]
!!VRy-10:&4194304; [See if Metamorph was summoned: y-10=4194304 if yes]
!!VRy-8:|4194304; [Set Metamorph's "summoned" bit if not set]
!!VRy-8:|8388608; [Set Metamorph's "clone" bit if not set]
!!VRy-8:|(MON_FLAG_NO_COLORING);
!!BMv1:Fy-8; [Write Metamorph's Flags: y-8]

!!BMv1:K1;                              [Kill the Metamorph]
!!SN&i^battle_isVisible^:D;

!!BU:Sv4/v7/v8/v9/v14/1; [Summon new creature stack: Type is v4]
!!BU:Ev8/?v24; [Stack number of new summoned stack: v24]
!!EAy-4:Ov4; [Copy Bonus Lines for new type (v4) to (Metamorph) stack (y-4)]
!!EAy-4:Ey-5/2/v4/d; [Set Experience of (Metamorph) stack: y-5]

!!BMv24:F?y-2; [New (Metamorph's) Flags: y-2]
!!VRy-2&y-9=8388608:|8388608; [Set new Metamorph stack's "clone" bit if it was a clone]
!!VRy-2&y-10=4194304:|4194304; [Set new Metamorph stack's "summoned" bit if it was summoned]
!!BMv24:Fy-2; [Write modified Flags to (Metamorph) stack: y-2]
!!BMv24&v24>=0:C4/v8/3/1/0; [Metamorph casts dummy spell on self]

!!VRz1&v5=1:Sz156009;
!!VRz1&v5>1:Sz156010;
!!UN:N3/2/v4/1; [New creature name (plural): z2]
!!VRz3:Sz156011;
!!BU:Mz3; [Display scroll bar message]

--------------------------------------------------------------------------------------------------------------------

 [Determine random creature Metamorph transforms into: v4]
!?FU(WOG_56_DetermineNewCreature);
!!VRx16&x16>=122:+1; [Skip "Not Used (1)"]
!!VRx16&x16>=124:+1; [Skip "Not Used (2)"]
!!VRx16&x16>=126:+1; [Skip "Not Used (3)"]
!!VRx16&x16>=128:+1; [Skip "Not Used (4)"]
!!VRx16&x16>=145:+5; [Skip War Machines]
!!VRx16&x16>=160:+4; [Skip Emissaries]
!!VRx16&x16>=(MON_EARTH_MESSENGER):+1; [Skip Earth Messenger (Metamorph)]
!!VRx16&x16>=174:+18; [Skip Commanders]

!!MA:Px16/?y1; [Health of one of creature at x16: y1]
!!VRv7:Sv6 :y1; [Number of creatures it can transform to: v7]

!!if&v7>0;
  !!VRv4:Sx16; [Set v4 to creature type: v4]
  !!VRx16:S999; [Exit loop]
!!en;

--------------------------------------------------------------------------------------------------------------------

 [After battle, set correct Metamorphs in hero's army]
!?FU(OnAfterBattleUniversal)&1000/-998;
!!UN:P56/?(wogOption:y);                [Check if script is enabled]
!!FU&(wogOption)<>(TRUE):E;             [Exit if script isn't enabled]

; Set correct Metamorphs in hero's army (if the hero has retreated then it's not required, as the hero lose all the troops)
!!DO(WOG_56_RestoreMetamorph)/(BATTLE_ATTACKER_STACK_FIRST)/(BATTLE_ATTACKER_STACK_LAST)/1&i^wog_isRetreated_0^<>(TRUE):P?(armyUpdated1:y);
!!DO(WOG_56_RestoreMetamorph)/(BATTLE_DEFENDER_STACK_FIRST)/(BATTLE_DEFENDER_STACK_LAST)/1&i^wog_isRetreated_1^<>(TRUE):P?(armyUpdated2:y);
!!SN|(armyUpdated1)/(armyUpdated2):D;   [Redraw screen]

--------------------------------------------------------------------------------------------------------------------

 [After battle, set correct Metamorphs in hero's army]
!?FU(WOG_56_RestoreMetamorph);
!#VA(armyUpdated:x);

!!VR(armyUpdated):S(FALSE);
!!VRy1:S4500 +x16; [Stack number index: y1]
!!VRy10:Svy1; [Type of creature at start of battle: y10]
!!BMx16:N?y2 O?y3; [Number of creatures in stack: y2, Hero slot #: y3]

 [Exit if not a Metamorph at start of battle,]
 [all killed, or a summoned creature there now]
!!FU|y10<>(MON_EARTH_MESSENGER)/y2<1/y3<0:E;

!!VR(armyUpdated):S(TRUE);

!!MA:P(MON_EARTH_MESSENGER)/?y4; [Health of one Metamorph: y4]
!!BMx16:T?y5; [Type of creature: y5]
!!MA:Py5/?y6; [Health of one creature: y6]
!!VRy7:Sy6 *y2; [Total health of remaining stack of creatures: y7]
!!VRy8:Sy7 :y4; [Number of Metamorphs to convert to: y8]
!!VRy8&y8<1:S1; [If less than 1, make it 1]
!!BMx16:B?y12; [Starting number of summoned stack: y12]
!!VRy13:S4550 +x16; [Starting Metamorph index number]
!!VRy8&y12=y2:Svy13; [Set to starting Metamorph number if none lost: y8]
!!BMx16:I?y11; [Check which side owns it: y11]
!!BMx16:Ny8; [Set to correct number]
!!BA:My11/y3/(MON_EARTH_MESSENGER)/y8; [Set Metamorphs of correct number]
!!VRy14:Sx16 *-1 -1; [Negative of stack number: y14]
!!EAy14:E?y15/d/d/d; [Experience of stack: y15]
!!BA:Hy11/?y16; [Hero number: y16]
!!HEy16&y16>0/y15>0:C0/y3/d/d/y15/3; [Add back any experience from before the battle]
