ZVSE2

** LEVEL 7+ TROOPS ONLY GAIN 50% EXPERIENCE
** СУЩЕСТВА 7 УРОВНЯ ПОЛУЧАЮТ ТОЛЬКО 50% ОПЫТА


** Option 245 by Timothy Pulver
** Rewritten by igrik (04.03.2021)

* Level 7+ troops only gain 50% of experience in battle instead of the normal 100%. 
* This option helps to balance them against lower level troops.

* If stack experience is disabled, this option has no effect.


!#DC(WOG_OPT_CREATURES_7_LEVEL_50_EXPO) = 245;


!?FU(OnAfterErmInstructions);
!!UN:P(WOG_OPT_CREATURES_7_LEVEL_50_EXPO)/?(wogOption:y);  
!!FU&(wogOption)<>(TRUE):E;

!!UN:P(WOG_OPT_STACK_EXPERIENCE)/?(wogOption:y);  
!!FU&(wogOption)<>(TRUE):E;


!!FU(GetMaxMonsterId):P?(monLastID:y);
 
!!re i/(MON_FIRST)/(monLastID)/1/:;  
  !!MA:Li/?(level:y);

  ; Set 50% Exp. Multiplier if Level 7+
  !!EAi&(level)>=(MON_MAX_LEVEL):M500;
!!en:;

** End of Script **
